/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
100 REM Analytical Geometry (AnaGeo - analytische Geometrie)
110 REM Marco Vieth
120 REM 2.12.1989
130 REM
140 CLEAR:DEFINT a-z:DEFREAL m:DEFSTR t
150 DIM m[5,1],t[9],p[4,1]
160 j=2:GOSUB 410:j=0:GOSUB 410
170 sk=50:RESTORE 1710:FOR i=0 TO 4:READ x,y:x=x*sk:y=y*sk:p[i,0]=x:p[i,1]=y:NEXT:cx=320:cy=200
180 '
190 GOSUB 1530
200 RESTORE 1660:t[0]="Hauptmenue":GOSUB 1570
210 t[0]=t:IF i=8 THEN MODE 2:STOP:GOTO 190
220 CLS
230 ON i GOSUB 270,450,480,560,650,770,850
240 GOTO 200
250 '
260 '
270 CLS:RESTORE 1670:GOSUB 1580
280 IF i=3 THEN RETURN
290 CLS:PRINT t[i]:PRINT
300 ON i GOSUB 330,400
310 GOTO 270
320 '
330 s=5:z=10:j=0:GOSUB 1150
340 i=0:j=0:GOSUB 1290
350 i=1:GOSUB 1290
360 i=0:j=1:GOSUB 1290
370 i=1:GOSUB 1290
380 RETURN
390 '
400 j=0
410 m[j,0]=1:m[j,1]=0:m[j+1,0]=0:m[j+1,1]=1
420 RETURN
430 '
440 '
450 CLS
460 m[2,0]=1:m[2,1]=0:m[3,0]=0:m[3,1]=1:GOTO 900
470 '
480 CLS:PRINT"Skalierungsmatrix eingeben :"
490 j=2:m[j,1]=0:m[j+1,0]=0
500 m[j,0]=ABS(m[j,0]):m[j+1,1]=ABS(m[j+1,1]):s=5:z=3:GOSUB 1150
510 i=0:j=2:GOSUB 1290
520 i=1:j=3:GOSUB 1290
530 GOTO 900
540 '
550 '
560 CLS:RESTORE 1680:GOSUB 1580
570 IF i=4 THEN RETURN
580 CLS:PRINT t[i]:PRINT
590 j=2:m[j,1]=0:m[j+1,0]=0
600 IF i=1 THEN m[j,0]=1:m[j+1,1]=-1 ELSE IF i=2 THEN m[j,0]=-1:m[j+1,1]=1 ELSE  m[j,0]=-1:m[j+1,1]=-1
610 GOSUB 900
620 GOTO 560
630 '
640 '
650 CLS:RESTORE 1690:GOSUB 1580
660 IF i=3 THEN RETURN
670 CLS:PRINT t[i]:PRINT
680 CLS:PRINT"Scherungsmatrix eingeben :"
690 i2=i
700 j=2:GOSUB 410:s=5:z=3:GOSUB 1150
710 IF i2=1 THEN j=j+1:i=0 ELSE i=1
720 GOSUB 1290
730 GOSUB 900
740 GOTO 650
750 '
760 '
770 CLS
780 INPUT"Winkel(0..360)";m:IF m<0 OR m>360 THEN 770
790 DEG
800 j=2:m[j,0]=COS(m):m[j,1]=SIN(m):m[j+1,0]=-m[j,1]:m[j+1,1]=m[j,0]
810 GOSUB 900
820 RETURN
830 '
840 '
850 PRINT t[0]
860 PRINT"nicht implementiert."
870 CALL &BB18:CLS
880 RETURN
890 '
900 zi=4:st=0:st2=2:GOSUB 1080
910 CLS
920 PRINT"aktuelle Matrix :";
930 s=1:z=2:j=0:GOSUB 1150
940 LOCATE 1,7:PRINT"verknuepft (mult) mit :";
950 z=8:j=2:GOSUB 1150
960 LOCATE 1,13:PRINT"ergibt :";
970 z=14:j=4:GOSUB 1150
980 CALL &BB18
990 MODE 1
1000 PRINT CHR$(23)+CHR$(1);:j=0:GOSUB 1800:'zeigen
1010 FOR i=1 TO 20:CALL &BD19:NEXT:'wait
1020 j=0:GOSUB 1820:PRINT CHR$(23)+CHR$(0);:'loeschen ohne Koord.sys.
1030 j=4:GOSUB 1750
1040 zi=0:st=4:GOSUB 1120
1050 RETURN
1060 '
1070 'Matrixmult:
1080 j=zi:FOR i=0 TO 1:m[j,i]=m[st,0]*m[st2,i]+m[st,1]*m[st2+1,i]:NEXT
1090 j=zi+1:FOR i=0 TO 1:m[j,i]=m[st+1,0]*m[st2,i]+m[st+1,1]*m[st2+1,i]:NEXT
1100 RETURN
1110 'Matrixcopy:
1120 FOR j=st TO st+1:FOR i=0 TO 1:m[zi,i]=m[j,i]:NEXT:zi=zi+1:NEXT
1130 RETURN
1140 '
1150 LOCATE s,z:PRINT CHR$(135);:LOCATE s+20,z:PRINT CHR$(139)
1160 LOCATE s,z+1:PRINT CHR$(133);" ";
1170 FOR i=0 TO 1:PRINT USING"+###.###";m[j,i];:PRINT" ";::NEXT
1180 PRINT CHR$(138);
1190 LOCATE s,z+2:PRINT CHR$(133);" ";
1200 FOR i=0 TO 1:PRINT USING"+###.###";m[j+1,i];:PRINT" ";:NEXT
1210 PRINT CHR$(138);
1220 LOCATE s,z+3:PRINT CHR$(141);:LOCATE s+20,z+3:PRINT CHR$(142);
1230 RETURN
1240 '
1250 LOCATE s,z:PRINT"A = ";a2:LOCATE s+15,z:PRINT"B = ";b2
1260 LOCATE s,z+1:PRINT"C = ";c2:LOCATE s+15,z+1:PRINT"D = ";d2;
1270 RETURN
1280 '
1290 LOCATE s+2+i*9,z+1+(j AND 1):INPUT"",t
1300 IF t="" THEN RETURN
1310 m[j,i]=VAL(t)
1320 LOCATE s+2+i*9,z+1+(j AND 1):PRINT USING"+###.###";m[j,i];
1330 RETURN
1340 '
1350 '
1360 GOSUB 1400
1370 LOCATE x[x]-1,y:PRINT CHR$(1)+CHR$(9);
1380 RETURN
1390 '
1400 IF x2=x AND y2=y THEN RETURN
1410 LOCATE x[x2]-1,y2:PRINT" ";
1420 RETURN
1430 '
1440 WINDOW#2,5,35,22,24:CLS#2
1450 PRINT#2,"Zahl von "mi" bis "ma" :";
1460 WINDOW#3,5,35,23,24:CLS#3
1470 INPUT#3,i
1480 IF i<mi OR i>ma THEN 1460
1490 CLS#2
1500 WHILE INKEY$<>"" :CALL &BD19:WEND
1510 RETURN
1520 '
1530 MODE 1:INK 0,1:INK 1,24:INK 2,12:INK 3,6
1540 PRINT STRING$(40,35):WINDOW#1,1,2,2,4:PRINT#1,"######";:WINDOW#1,39,40,2,4:PRINT#1,"######";:LOCATE 13,3:PEN 3:PRINT"AnaGeo (v1.0)":PEN 1:PRINT:PRINT STRING$(40,35)
1550 WINDOW 1,40,7,25
1560 RETURN
1570 '
1580 WINDOW#2,10,30,10,22:CLS#2:PRINT#2,t[0]+":":PRINT#2
1590 READ ma:FOR i=1 TO ma:READ t[i]:PRINT#2,HEX$(i)+") "+t[i]:NEXT
1600 PRINT#2:PRINT#2,"Ihre Wahl : ";
1610 t=INKEY$:call &bd19:IF t="" THEN 1610
1620 i=VAL(t):IF i<1 OR i>ma THEN 1610
1630 PRINT#2,t:t=t[i]:RETURN
1640 '
1650 '
1660 DATA 8,Matrixeingabe,Identitaet,Skalierung,Spiegelung,Scherung,Rotation,Translation,Ende
1670 DATA 3,eingeben,Einheitsmatrix,Hauptmenue
1680 DATA 4,x-Achsenspieg.,y-Achsenspieg.,Punktspiegelung,Hauptmenue
1690 DATA 3,x-Scherung,y-Scherung,Hauptmenue
1700 '
1710 DATA 0,0,1,0,1,1,0,1,0,0
1720 '
1730 '
1740 'Bild zeigen:
1750 GOSUB 1800
1760 CALL &BB18
1770 GOTO 1530
1780 '
1790 'Phase zeigen:
1800 MOVE 0,cy:DRAW 639,cy,1:MOVE cx,0:DRAW cx,399
1810 '
1820 i=0:GOSUB 1880:PLOT x,y,3
1830 FOR i=1 TO 4:GOSUB 1880:DRAW x,y:NEXT
1840 PLOT cx,400,1
1850 RETURN
1860 '
1870 'Koordinatenberechnung
1880 x=p[i,0]:y=p[i,1]:mx=m[j,0]*x+m[j+1,0]*y:my=m[j,1]*x+m[j+1,1]*y
1890 x=mx+cx:y=cy+my
1900 RETURN
1910 '
*/ });
