/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
994 rem ellipses - Ellipses (Ellipsen)
995 rem Jaap Wieles
996 rem http://www.basicode.de/kc/togo.htm
997 rem Modifications:
998 chain merge "basicode"
1000 A=100:GOTO 20:REM ELLIPSEN FANTASIE
1010 FF=1:IF (HG<330) OR (VG<300) THEN FF=.5
1020 P1=4*ATN(1):EO=-1:AD=.5
1030 DM=6:REM MAXIMUM AANTAL ZIJDEN MAG OOK GROTER ZIJN
1040 DIM X0(DM),X1(DM),Y0(DM),Y1(DM)
1050 FOR I=0 TO DM:X0(I)=0:Y0(I)=0:NEXT I
1060 CC(0)=0:CC(1)=0:CN=0:GOSUB 600
1070 FOR FG=1 TO 4 STEP AD
1080 EO=EO*-1
1090 FOR AH=3 TO DM:REM AANTAL ZIJDEN
1100 HO=0:VE=0:CC(0)=4:SR$="Toets is stop de figuur."
1110 GOSUB 650
1120 ES=60:REM MAAK GROTER VOOR MOIRE EFFECTEN
1130 FOR K=0 TO ES*FF:H1=K*2*P1/(ES*FF)
1140 FOR I=0 TO AH:X1(I)=X0(I):Y1(I)=Y0(I):NEXT I
1150 FOR I=0 TO AH:H2=I*2*P1/AH
1160 CL=(I-7*INT(I/7))+1:CC(0)=CL
1170 R1=.22:R2=.22
1180 X1=R1*COS(H1+FG*H2)
1190 Y1=R1*SIN(H1-EO*FG*H2)
1200 X2=R2*COS(H2)
1210 Y2=R2*SIN(H2)
1220 REM PROBEER OOK EENS
1230 REM R1=.28:R2=.12:X1=R1*COS(H1):Y1=R1*SIN(-H1*2)
1240 REM X2=R2*COS(H1)*COS(H2):Y2=R2*SIN(H2*2)
1250 X=X1+X2:X0(I)=X:HO=.5+X
1260 Y=Y1+Y2:Y0(I)=Y:VE=.5+Y
1270 IF I=0 THEN  GOSUB 620:GOTO 1290
1280 GOSUB 630
1290 NEXT I
1300 IF K=0 THEN 1380
1310 CC(0)=6:FOR I=0 TO AH
1320 HO=.5+X1(I):VE=.5+Y1(I):GOSUB 620
1330 HO=.5+X0(I):VE=.5+Y0(I):GOSUB 630
1340 GOSUB 200:IF IN=0 THEN 1370
1350 HO=0:VE=0:CN=1:GOSUB 650:CN=0:CC(0)=4
1360 I=AH:K=60*FF:GOTO 1410
1370 NEXT I
1380 NEXT K
1390 HO=0:VE=0:CN=1:GOSUB 650:CN=0:CC(0)=4
1400 GOSUB 200:IF IN<>0 THEN 1400
1410 SR$="Toets is vervolg. Return is stop.":GOSUB 650
1420 GOSUB 210:IF IN=0 THEN 1420
1430 IF IN=13 THEN 1480
1440 GOSUB 600:NEXT AH
1450 NEXT FG
1460 HO=0:VE=0:CC(0)=2:SR$="Toets voor einde.":GOSUB 650
1470 GOSUB 200:IF IN=0 THEN 1470
1480 GOTO 950
1490 REM
30000 REM HET PROGRAMMA TEKENT ELLIPSEN
30010 REM DIE DOOR NETTEN MET ELKAAR
30020 REM VERBONDEN ZIJN
30030 REM DE ELLIPSEN ONSTAAN DOOR DE
30040 REM GELIJKSTANDIGE HOEKPUNTEN
30050 REM VAN VEELHOEKEN MET ELKAAR TE
30060 REM VERBINDEN
30070 REM DE NETTEN ZIJN DE ZIJDEN VAN
30080 REM DE VEELHOEKEN.
30090 REM HET AANTAL FIGUREN WORDT IN
30100 REM HET PROGRAMMA BEPAALD DOOR
30110 REM AH EN DM EN AD.
30120 REM DOOR TE MANIPULEREN MET DE
30130 REM HOEKEN H1 EN H2 EN
30140 REM DE VECTOREN R1 EN R2 KUNNEN
30150 REM EEN GROOT AANTAL VERSCHIL-
30160 REM LENDE FIGUREN ONTSTAAN.
30170 REM DIT PROGRAMMA IS EERDER VER-
30180 REM SCHENEN IN DE RUN-RUBRIEK
30190 REM VAN HET ALGEMEEN DAGBLAD.
30200 REM WIE DE GWBASIC-VERSIE VAN
30210 REM DIT PROGRAMMA EN VAN
30220 REM KRONKELS WIL. KAN EEN
30230 REM GEFOR MATEERDE DISKETTE MET
30240 REM EEN GEFRANKEERDE RETOUR
30250 REM ENVELOP STUREN AAN :
32000 REM ----------------------------
32010 REM JAAP WIELES
32020 REM HOOFDWAL 68
32030 REM 4207 EE  GORINCHEM
32040 REM 01830-30492
32050 REM ----------------------------
32060 REM TROS-RADIO dd 920923
32070 REM spezial DS Kultur 920930
*/ });
