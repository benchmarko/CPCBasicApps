/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem energy1 - Energy Collectors 1
2 rem (c) Marco Vieth, 1985
3 rem
4 rem first version in MODE 1 and cursor keys control
5 rem
10 REM ENERGIESAMMLER
20 MODE 2
30 PRINT"                 S P I E L R E G E L N :":PRINT:PRINT
40 PRINT"     BEI DIESEM SPIEL FUEHREN SIE EIN PFEILRAUMSCHIFF":PRINT
50 PRINT"     DAS SIE GESCHICKT DURCH DIE ANGRIFFE DER MONSTER":PRINT
60 PRINT"     MIT DEN PFEILTASTEN HINDURCHSTEUERN MUESSEN.":PRINT
70 PRINT"     DURCH DIE COPY-TASTE KOENNEN SIE UNBESETZTE FELDER":PRINT
80 PRINT"     ENERGIELOS MACHEN, SO DASS DIE MONSTER NICHT AUF":PRINT
90 PRINT"     IHNEN LANDEN KOENNEN. WARTEN SIE ABER SO LANGE,BIS":PRINT
100 PRINT"          SIE   A L L E   PUNKTE ":PRINT
110 PRINT"     DAVON BEKOMMEN HABEN. VORSICHT VOR DEN BOMBEN ! ! !":PRINT
120 PRINT"     BERUEHRT MAN SIE, BEKOMMT MAN 50 PUNKTE ABZUG ! !":PRINT
130 PRINT"             ALSO VIEL SPASS BEIM SPIEL :"
140 PRINT"     E   N   E   R   G   I   E   S   A   M   M   L   E   R"
150 LOCATE 13,25:PRINT"(BITTE EINE TASTE DRUECKEN) !"
160 CALL &BB18
170 MODE 1
180 DIM ver(40,24):DIM SCH(40,24)
190 x=1:y=1 : FOR za=1 TO 60 :a=INT(40*RND(1)+1) :b=INT(24*RND(1)+1)
200 ver(a,b)=1 : LOCATE a,b:PRINT CHR$(252); : NEXT za
210 LOCATE 1,1:PRINT CHR$(241); : LOCATE 4,25:PRINT"PUNKTE :"
220 PU=1000:GOSUB 600
230 M=30:N=12:D(1)=30:E(1)=12:D(2)=30:E(2)=12
240 LOCATE M,N:PRINT CHR$(224);
241 LOCATE 19,1:PRINT CHR$(206);" ";CHR$(206);:ver(19,1)=1:ver(21,1)=1
242 LOCATE 19,24:PRINT CHR$(206);" ";CHR$(206);:ver(19,24)=1:ver(21,24)=1
243 ver(20,1)=0:ver(20,24)=0:ver(20,2)=0:ver(20,23)=0
250 EVERY 40,3 GOSUB 620
260 IF INKEY(0)=0 THEN y=y-1:GOSUB 360 :GOTO 260
270 IF INKEY(1)=0 THEN x=x+1:GOSUB 400 :GOTO 270
280 IF INKEY(2)=0 THEN y=y+1:GOSUB 320 :GOTO 280
290 IF INKEY(8)=0 THEN x=x-1: GOSUB 450 :GOTO 290
300 IF INKEY(9)=0 THEN GOSUB 560
310 call &bd19:GOTO 260
320 GOSUB 500
330 IF ver(x,y)=1 THEN y=y-1:SOUND 2,600,10 :PU=PU-50:GOSUB 600
340 IF y-1<1 OR ver(x,y-1)=1 THEN 440
350 LOCATE x,y-1:PRINT" ";:LOCATE x,y:PRINT CHR$(241);:RETURN
360 GOSUB 500
370 IF ver(x,y)=1 THEN y=y+1:SOUND 2,400,10 :PU=PU-50:GOSUB 600
380 IF y+1>40 THEN 440
390 LOCATE x,y+1:PRINT" ";:LOCATE x,y:PRINT CHR$(240);:RETURN
400 GOSUB 500
410 IF ver(x,y)=1 THEN x=x-1:SOUND 2,500,10 :PU=PU-50:GOSUB 600
420 IF x-1<1 OR ver(x-1,y)=1 THEN 440
430 LOCATE x-1,y:PRINT" ";:LOCATE x,y:PRINT CHR$(243);
440 RETURN
450 GOSUB 500
460 IF ver(x,y)=1 THEN x=x+1:SOUND 2,700,10 :PU=PU-50:GOSUB 600
470 IF x+1<1 THEN 490
480 LOCATE x+1,y:PRINT" ";:LOCATE x,y:PRINT CHR$(242);
490 RETURN
500 t!=time+40:while time<t!:call &bd19:wend
502 IF x=20 AND y=0 THEN y=24 :LOCATE 20,1:PRINT" ";: GOTO 510
505 IF x=20 AND y=25 THEN y=1 :LOCATE 20,24:PRINT" ";
510 IF y>=24 THEN y=24
520 IF x>40 THEN x=40
530 IF y<1 THEN y=1
540 IF x<1 THEN x=1
550 SOUND 1,x*20,5,4:SOUND 4,y*20,5,4 : RETURN
560 SCH(X,Y)=SCH(X,Y)+1
570 IF SCH(X,Y)=5 THEN LOCATE X,Y:PAPER 2:PRINT CHR$(128);
580 IF SCH(X,Y)>30 AND SCH(X,Y)<40 THEN LOCATE X,Y:PAPER 3:PRINT CHR$(128);:PU=PU+10:GOSUB 600
590 PAPER 0 : RETURN
600 WINDOW #1,20,35,25,25:PRINT#1,PU;:IF PU<1 THEN CLS#1:PRINT #1,"VERLOREN":STOP
610 RETURN
620 PAPER 0 : FOR T=1 TO 2
630 M(T)=INT(X*RND(1)+1) : N(T)=INT(24*RND(1)+1)
640  IF SCH(M(T),N(T))>30 THEN 630
650 IF VER(M(T),N(T))=1 THEN 630
660 LOCATE D(T),E(T):PRINT" "; : D(T)=M(T):E(T)=N(T)
670 LOCATE M(T),N(T):PRINT CHR$(225); : SOUND 2,0,10,4,0,0,5
680 IF X=M(T) AND Y=N(T) THEN PRINT"BOMM":STOP
690 NEXT T : RETURN
*/ });
