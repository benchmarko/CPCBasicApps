/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem energy0 - Energy Collectors 0
2 rem (c) Marco Vieth, 1985
3 rem
4 rem second version in MODE 0 and Joystick control
5 rem
10 REM ENERGIESAMMLER
20 MODE 2
30 PRINT"                 S P I E L R E G E L N :":PRINT:PRINT
40 PRINT"     BEI DIESEM SPIEL FUEHREN SIE EIN PFEILRAUMSCHIFF":PRINT
50 PRINT"     DAS SIE GESCHICKT DURCH DIE ANGRIFFE DER MONSTER":PRINT
60 PRINT"     MIT DEM JOYSTICK  HINDURCHSTEUERN MUESSEN.":PRINT
70 PRINT"     DURCH DIE FEUER-TASTE KOENNEN SIE UNBESETZTE FELDER":PRINT
80 PRINT"     ENERGIELOS MACHEN, SO DASS DIE MONSTER NICHT AUF":PRINT
90 PRINT"     IHNEN LANDEN KOENNEN. WARTEN SIE ABER SO LANGE,BIS":PRINT
100 PRINT"          SIE   A L L E   PUNKTE ":PRINT
110 PRINT"     DAVON BEKOMMEN HABEN. VORSICHT VOR DEN BOMBEN ! ! !":PRINT
120 PRINT"     BERUEHRT MAN SIE, BEKOMMT MAN 50 PUNKTE ABZUG ! !":PRINT
130 PRINT"             ALSO VIEL SPASS BEIM SPIEL :"
140 PRINT"     E   N   E   R   G   I   E   S   A   M   M   L   E   R"
150 LOCATE 13,25:PRINT"(BITTE EINE TASTE DRUECKEN) !"
160 CALL &BB18
170 MODE 0 :INK 3,8,21:PEN #1,1:PAPER #1,0
180 DIM ver(20,24):DIM SCH(20,24)
190 x=1:y=1:a=x:b=y : FOR za=1 TO 30 : a=INT(20*RND(1)+1) :b=INT(24*RND(1)+1)
200 ver(a,b)=1 : LOCATE a,b:PRINT CHR$(229); : NEXT za
210 PEN 13:LOCATE 1,1:PRINT CHR$(241); : LOCATE 1,25:PRINT"PUNKTE :":PEN 1
220 PU=5000:GOSUB 600
230 M=15:N=12:D(1)=15:E(1)=12:D(2)=15:E(2)=12
240 PEN 10:LOCATE M,N:PRINT CHR$(224);
241 LOCATE  9,1:PRINT CHR$(206);" ";CHR$(206);:ver(9,1)=1:ver(11,1)=1
242 LOCATE  9,24:PRINT CHR$(206);" ";CHR$(206);:ver(9,24)=1:ver(11,24)=1
243 ver(10,1)=0:ver(10,24)=0:ver(10,2)=0:ver(10,23)=0 : PEN 1
250 EVERY 40,3 GOSUB 620
260 IF JOY(0)=1 AND y>1 THEN b=y:y=y-1:cr=240:GOSUB 500:GOTO 260
270 IF JOY(0)=2 AND y<24 THEN b=y:y=y+1:cr=241:GOSUB 500 :GOTO 270
280 IF JOY(0)=4 AND x>1 THEN a=x:x=x-1:cr=242:GOSUB 500:GOTO 280
290 IF JOY(0)=8 AND x<20 THEN a=x:x=x+1:cr=243:GOSUB 500:GOTO 290
300 IF JOY(0)>15 THEN GOSUB 560
310 call &bd19:GOTO 260
500 t!=time+40:while time<t!:call &bd19:wend
502 DI:IF x=10 AND y=1 THEN y=24 :LOCATE 10,1:PRINT" ";: GOTO 510
505 IF x=10 AND y=25 THEN y=1 :LOCATE 10,24:PRINT" ";
510 IF ver(x,y)=1 THEN x=a:y=b:SOUND 2,500,10 :PU=PU-50:GOSUB 600
530 LOCATE a,b:PRINT" ";
535 LOCATE x,y:PRINT CHR$(cr);
540 a=x:b=y
550 SOUND 1,x*20,5,4:SOUND 4,y*20,5,4 :EI:RETURN
560 SCH(X,Y)=SCH(X,Y)+1
570 IF SCH(X,Y)=5 THEN PEN 2:DI:LOCATE X,Y:PRINT CHR$(252);
580 IF SCH(X,Y)>30 AND SCH(X,Y)<40 THEN PEN 3:DI:LOCATE X,Y:PRINT CHR$(228);:PU=PU+10:GOSUB 600
585 IF SCH(X,Y)>40 THEN PEN 7:DI:LOCATE X,Y:PRINT CHR$(238);
586 EI
590 PAPER 0 : PEN 1 : RETURN
600 WINDOW #1,10,19,25,25:PRINT#1,PU;:IF PU<1 THEN CLS#1:PRINT #1,"VERLOREN":STOP
605 IF PU>=10000 THEN 700
610 RETURN
620 REM monster setzen
625 ah=POS(#0):bh=VPOS(#0):PAPER 0:FOR T=1 TO 2
630 M(T)=INT(X*RND(1)+1) : N(T)=INT(24*RND(1)+1)
640  IF SCH(M(T),N(T))>30 THEN 630
650 IF VER(M(T),N(T))=1 THEN 630
660 LOCATE D(T),E(T):PAPER 0:PEN 1:PRINT" "; : D(T)=M(T):E(T)=N(T)
670 LOCATE M(T),N(T):PEN 15:PRINT CHR$(225); : SOUND 2,0,10,3,0,0,5
680 IF X=M(T) AND Y=N(T) THEN GOTO 800
690 NEXT T :PEN 1: LOCATE ah,bh:RETURN
700 REM GEWONNEN
710 nichts=REMAIN(3)
720 MODE 1:PRINT "GEWONNEN"
730 FOR M=200 TO 50 STEP -1
740 SOUND 1,M,4,4:SOUND 4,M+20,4,4
750 NEXT M
760 MODE 2:PEN 1:PAPER 0:LIST
800 REM VERLOREN
810 nichts=REMAIN(3)
820 LOCATE X,Y:PRINT CHR$(253);" BOMM"
830 FOR M=6 TO 2 STEP -1 : SOUND 2,0,38,M,0,0,5 : FOR P=1 TO 240/10:call &bd19:NEXT P : NEXT M
840 LOCATE 1,1
850 t$=INKEY$:IF t$<>"" THEN 850
860 STOP
*/ });
