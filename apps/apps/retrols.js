/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem retrols - Retro-Loadsheet
2 rem (c) Sakis Kaffesakis, 2018
3 rem https://amstradsakis.blogspot.com/2020/04/retro-loadsheet.html
4 rem https://www.cpcwiki.eu/forum/games/retro-loadsheet-on-amstrad-cpc!-new-program-for-aircraft-weight-and-balance/
5 rem
10 REM RETRO-LOADSHEET ON AMSTRAD CPC! COPYRIGHT by SAKIS KAFFESAKIS 2018
15 BORDER 0:INK 1,18:INK 0,0
20 GOSUB 500
30 GOSUB 9000
34 MODE 2:BORDER 0:PEN 1:INK 0,9:INK 1,26
36 MOVE 150,120:DRAW 490,120:DRAW 490,280:DRAW 150,280:DRAW 150,120
40 LOCATE 34,10:INPUT"USER? ",USER$:LOCATE 34,12:INPUT"PASSWORD? ",PASS$
50 IF (USER$="KAF" AND PASS$="0") OR (USER$="OPS" AND PASS$="4") THEN CLS:GOTO 70
60 CLS:LOCATE 34,16:PRINT"ACCESS DENIED":GOTO 36
70 CLS:LOCATE 30,6:PRINT"1: AIRCRAFT DATA":LOCATE 30,8:PRINT"2: FLIGHT DATA":LOCATE 30,10:PRINT"3: PAX/BAG DATA":LOCATE 30,12:PRINT"4: SPECIAL LOAD":LOCATE 30,14:PRINT"5: LOAD DISTRIBUTION":LOCATE 30,16:PRINT"6: ENTER TRIP INFO"
80 LOCATE 30,18:PRINT"7: LOADSHEET": LOCATE 30,20:PRINT"8: NOTOC":LOCATE 30,22:PRINT"9: RESTART"
85 IF RF=0 THEN LOCATE 64,23:PRINT"STATUS: RO"
86 IF RF=1 THEN LOCATE 64,23:PRINT"STATUS: RF"
87 MOVE 30,20:DRAW 610,20:DRAW 610,380:DRAW 30,380:DRAW 30,20
90 A$=INKEY$
100 IF A$="1" THEN GOTO 1000
110 IF A$="2" THEN GOTO 2000 
120 IF A$="3" THEN GOTO 3000
130 IF A$="4" THEN GOTO 5000   
140 IF A$="5" THEN GOTO 4000
150 IF A$="6" THEN GOTO 6000 
160 IF A$="7" THEN GOTO 7000 
170 IF A$="8" THEN GOTO 8000 
175 IF A$="9" THEN GOTO 8500    
180 GOTO 90
500 MODE 1:LOCATE 12,8:PRINT"RETRO-LOADSHEET":LOCATE 18,11:PRINT"ON":LOCATE 14,14:PRINT"AMSTRAD CPC!" 
510 LOCATE 8,22:PRINT"by SAKIS KAFFESAKIS ";CHR$(164);" 2018"
520 LOCATE 1,24:PRINT"Contact for comments:skafesakis@yahoo.gr"
530 T$=INKEY$
540 IF T$<>"" THEN 560
550 GOTO 530
560 MODE 2:LOCATE 12,12:PRINT"THIS PROGRAM IS JUST FOR FUN - DO NOT USE FOR REAL FLIGHTS!":LOCATE 33,15:PRINT"INACCURATE DATA!"
565 LOCATE 30,24:PRINT"PRESS 1 TO CONTINUE"
570 T$=INKEY$
580 IF T$="1" THEN RETURN
590 GOTO 570
1000 CLS:LOCATE 30,3:PRINT"SELECT REGISTRATION:":LOCATE 27,8:PRINT"1: B737-800: SX-KAF (189Y)":LOCATE 30,10:PRINT "(BASIC TRIM SUPPORTED)":LOCATE 27,13:PRINT"2: A320-200: SX-OFI (180Y)":LOCATE 31,15:PRINT"(NO TRIM SUPPORTED)" 
1001 MOVE 160,310:DRAW 470,310:DRAW 470,140:DRAW 160,140:DRAW 160,310
1005 LOCATE 30,18:PRINT"PRESS 0 FOR MENU"
1007 GOSUB 9590
1010 C$=INKEY$
1015 IF C$="1" AND REG=2 THEN CREG=1:GOTO 9500
1016 IF C$="2" AND REG=1 THEN CREG=2:GOTO 9500 
1020 IF C$="1" THEN REG$="SX-KAF":REG=1:BAS=BASA:CRE=CREA:CAP=189:CAP$="189Y":ULD=0:GOTO 1042
1030 IF C$="2" THEN REG$="SX-OFI":REG=2:BAS=BASB:CRE=CREB:CAP=180:CAP$="180Y":GOSUB 1800:GOTO 1042
1035 IF C$="0" THEN GOTO 70
1040 GOTO 1010
1042 GOSUB 9590:LOCATE 30,18:PRINT"    DATA ENTERED  ":LOCATE 30,22:PRINT"PRESS 0 FOR MENU":LOCATE 30,24:PRINT"PRESS 1 TO EDIT A/C DATA"
1043 D$=INKEY$
1044 IF D$="0" THEN GOTO 70
1045 IF D$="1"  THEN GOTO 1050
1046 GOTO 1043
1050 IF REG=2 THEN GOTO 1065
1060 CLS:LOCATE 7,3:PRINT REG$:LOCATE 3,6:PRINT"1: BASIC WEIGHT:     ";BASA:LOCATE 3,8:PRINT"2: CREW WEIGHT:      ";CREA:LOCATE 3,10:PRINT"3: PANTRY CODE:      "
1061 IF PANAFIXED>0 THEN LOCATE 25,10:PRINT "FIXED:";PANAFIXED;"KGS":GOTO 1070
1063 LOCATE 24,10:PRINT PANA:GOTO 1070 
1065 CLS:LOCATE 7,3:PRINT REG$:LOCATE 3,6:PRINT"1: BASIC WEIGHT:     ";BASB:LOCATE 3,8:PRINT"2: CREW WEIGHT:      ";CREB:LOCATE 3,10:PRINT"3: PANTRY CODE:      "
1067 IF PANBFIXED>0 THEN LOCATE 25,10:PRINT "FIXED:";PANBFIXED;"KGS":GOTO 1070
1068 LOCATE 24,10:PRINT PANB
1070 LOCATE 30,13:PRINT"PRESS 1,2,3 TO EDIT DATA":LOCATE 30,15:PRINT"PRESS 0 FOR AIRCRAFT MENU"  
1071 MOVE 320,230:DRAW 320,380:DRAW 10,380:DRAW 10,230:DRAW 320,230
1075 LOCATE 17,24:PRINT"IF YOU EDIT A/C DATA, TRIP INFO DATA WILL BE LOST"
1080 E$=INKEY$
1090 IF E$="0" THEN CLS:GOTO 1000
1100 IF E$="1" THEN GOTO 1110
1102 IF E$="2" THEN GOTO 1120
1104 IF E$="3" THEN GOTO 1130
1105 GOTO 1080
1110 LOCATE 3,17:INPUT"BASIC WEIGHT: ";BASF
1111 IF BASF<40000 OR BASF>55000 THEN GOSUB 9200:GOTO 1050
1112 BLOCK=0:TRIP=0:TAXI=0:CCOC=0:CCAB=0:DOW=0:DOI=0
1115 IF REG=1 THEN BASA=BASF
1116 IF REG=2 THEN BASB=BASF   
1117 GOTO 1050
1120 LOCATE 3,17:INPUT"CREW WEIGHT (70-120)  ";CREF :
1121 IF CREF<70 OR CREF>120 THEN GOSUB 9200:GOTO 1050
1122 BLOCK=0:TRIP=0:TAXI=0:CCOC=0:CCAB=0:DOW=0:DOI=0
1124 IF REG=1 THEN CREA=CREF
1125 IF REG=2 THEN CREB=CREF
1127 GOTO 1050
1130 LOCATE 3,18:PRINT"PANTRY CODE (0 FOR EMPTY, 1 FOR ONE WAY, 2 FOR TWO WAY, 3 TO EDIT)"
1131 Q$=INKEY$
1132 IF Q$="0" THEN PANF=0:GOTO 1142
1133 IF Q$="1" THEN PANF=1:GOTO 1142
1134 IF Q$="2" THEN PANF=2:GOTO 1142
1135 IF Q$="3" THEN GOSUB 1900:GOTO 1147
1136 GOTO 1131
1142 IF REG=1 THEN PANA=PANF:PANAFIXED=0:PANBFIXED=0
1143 IF REG=2 THEN PANB=PANF:PANAFIXED=0:PANBFIXED=0  
1147 REM FIX PAN KGS
1148 BLOCK=0:TRIP=0:TAXI=0:CCOC=0:CCAB=0:DOW=0:DOI=0 
1150 GOSUB 9090
1190 GOTO 1050
1800 LOCATE 30,18:PRINT"ULD OR BULK? (B/U)
1805 RF=0:GOSUB 10000
1810 J$=INKEY$
1820 IF J$="B" OR J$="b" THEN ULD=0:GOTO 1870
1830 IF J$="U" OR J$="u" THEN ULD=1:GOTO 1850
1840 GOTO 1810
1850 LOCATE 26,20:PRINT"CURRENT WEIGHT OF ULD: ";ULDW;"KGS":LOCATE 26,22:PRINT"PRESS 1 TO ACCEPT OR 2 TO EDIT"
1851 F$=INKEY$
1852 IF F$="1" THEN ULDW=ULDW:GOTO 1870
1853 IF F$="2" THEN GOTO 1855
1854 GOTO 1851
1855 LOCATE 25,24:INPUT"ULD NEW WEIGHT (60-140): ";ULDW
1860 IF ULDW<60 OR ULDW>140 THEN LOCATE 30,24:PRINT SPC(40):GOTO 1855
1870 LOCATE 25,20:PRINT SPC(34):LOCATE 25,22:PRINT SPC(34):LOCATE 25,24:PRINT SPC(41):RETURN
1900 LOCATE 3,21:INPUT"ENTER PANTRY KGS (WATER UPLIFT INCL.): ";PANFIXED
1910 IF PANFIXED<0 OR PANFIXED>500 THEN GOSUB 9200:RETURN:GOTO 1050 
1913 IF REG=1 THEN PANAFIXED=PANFIXED 
1915 IF REG=2 THEN PANBFIXED=PANFIXED 
1920 IF REG=1 THEN PANA=PANAFIXED  
1930 IF REG=2 THEN PANB=PANBFIXED
1940 RETURN
1990 END
2000 CLS:LOCATE 5,4:PRINT"1: DATE:               ";DATE$:LOCATE 5,6: PRINT"2: FLIGHT NUMBER:      ";FLT$:LOCATE 5,8:PRINT"3: STATION:            ";STA$:LOCATE 5,10:PRINT"4: DESTINATION:        ";DEST$
2005 MOVE 10,190:DRAW 630,190:DRAW 630,380:DRAW 10,380:DRAW 10,190
2010 LOCATE 5,12:PRINT"5: PAX WEIGHT USED:    ";PAXW$ 
2015 IF PAXW$="FIXED" THEN LOCATE 34,12:PRINT"(";"M";MALEW;"F";FEMAW;"C";CHILW;"I";INFAW;")"
2020 LOCATE 43,7:PRINT"PRESS 1,2,3,4,5 TO EDIT FLIGHT DATA":LOCATE 43,9:PRINT"PRESS 0 FOR MENU"
2030 H$=INKEY$
2040 IF H$="0" THEN GOTO 70
2050 IF H$="1" THEN GOTO 2100
2060 IF H$="2" THEN GOTO 2200 
2070 IF H$="3" THEN GOTO 2300
2080 IF H$="4" THEN GOTO 2400
2090 IF H$="5" THEN GOTO 2500
2095 GOTO 2030
2100 LOCATE 5,17:INPUT"DATE (FORMAT 01JAN18): ";DATE$
2105 IF LEN(DATE$)<>7 THEN DATE$="":GOSUB 9200
2110 GOTO 2000
2200 LOCATE 5,17:INPUT"FLIGHT NUMBER: ";FLT$
2205 IF LEN(flt$)<4 OR LEN(flt$)>8 THEN flt$="":GOSUB 9200 
2210 GOTO 2000
2300 LOCATE 5,17:INPUT"STATION (IATA CODE): ";STA$
2305 IF LEN(sta$)<>3 THEN sta$="":GOSUB 9200
2310 GOTO 2000
2400 LOCATE 5,17:INPUT"DESTINATION (IATA CODE): ";DEST$ 
2405 IF LEN(dest$)<>3 THEN dest$="":GOSUB 9200 
2407 IF DEST$=STA$ THEN DEST$="":GOSUB 9200:GOTO 2000
2410 GOTO 2000
2500 LOCATE 5,16:PRINT"PRESS 1 FOR STANDARD WEIGHT OR 2 FOR CHARTER WEIGHT OR 3 TO EDIT "
2510 LOCATE 5,18:PRINT"STANDARD: M 88 F 70 C 35 I 0":LOCATE 5,20:PRINT"CHARTER:  M 76 F 76 C 35 I 10"
2520 I$=INKEY$
2530 IF I$="1" THEN PAXW$="STANDARD":MALEW=88:FEMAW=70:CHILW=35:INFAW=0:GOTO 2000
2540 IF I$="2" THEN PAXW$="CHARTER":MALEW=76:FEMAW=76:CHILW=35:INFAW=10:GOTO 2000
2545 IF I$="3" THEN PAXW$="FIXED":GOTO 2600
2550 GOTO 2520
2599 REM FIX PAX WEIGHT USED
2600 LOCATE 5,18:PRINT SPC(29):LOCATE 5,20:PRINT SPC(29)
2610 LOCATE 5,18:INPUT"MALE WEIGHT (60-120):   ";MALEW
2620 IF MALEW<60 OR MALEW>120 THEN GOSUB 2800:GOTO 2000
2630 LOCATE 5,20:INPUT"FEMALE WEIGHT (60-120): ";FEMAW
2640 IF FEMAW<60 OR FEMAW>120 THEN GOSUB 2800:GOTO 2000 
2650 LOCATE 5,22:INPUT"CHILD WEIGHT (25-45):   ";CHILW
2660 IF CHILW<25 OR CHILW>45 THEN GOSUB 2800:GOTO 2000 
2670 LOCATE 5,24:INPUT"INFANT WEIGHT (0-20):   ";INFAW
2680 IF INFAW<0 OR INFAW>20 THEN GOSUB 2800:GOTO 2000 
2690 GOTO 2000
2800 LOCATE 35,22:PRINT"WRONG DATA"
2805 MALEW=88:FEMAW=70:CHILW=35:INFAW=0:PAXW$="STANDARD"
2810 R$=INKEY$
2820 IF R$<>"" THEN RETURN
2830 GOTO 2810
3000 IF REG<>1 AND REG<>2 THEN GOTO 9400
3001 CLS:LOCATE 5,3:PRINT"DATE";SPC(8);"FLT NUMBER";SPC(8);"FROM";SPC(8);"TO";SPC(8);"CAPACITY"    
3002 IF BAGPW=1E-10 THEN BAGPW=0
3010 LOCATE 4,5:PRINT DATE$:LOCATE 20,5:PRINT FLT$:LOCATE 35,5:PRINT STA$:LOCATE 46,5:PRINT DEST$:LOCATE 58,5:PRINT CAP$ 
3015 LOCATE 12,7:PRINT"1: PAX DATA":LOCATE 43,7:PRINT"2: BAG DATA          3: SECTION DATA"
3020 LOCATE 3,9:PRINT " M     F     C     I      TTL          BAGS       KGS"
3030 LOCATE 3,11:PRINT MALE:LOCATE 9,11:PRINT FEMA:LOCATE 15,11:PRINT CHIL:LOCATE 21,11:PRINT INFA:LOCATE 27,11:PRINT MALE+FEMA+CHIL;"+";INFA
3040 LOCATE 41,11:PRINT BAGPW:LOCATE 51,11:PRINT KGSPW
3045 LOCATE 68,9:PRINT"OA:":LOCATE 68,11:PRINT"OB:":LOCATE 68,13:PRINT"OC:":
3046 IF REG=1 THEN LOCATE 73,9:PRINT OAA:LOCATE 73,11:PRINT OBA:LOCATE 73,13:PRINT OCA
3047 IF REG=2 THEN LOCATE 73,9:PRINT OAB:LOCATE 73,11:PRINT OBB:LOCATE 73,13:PRINT OCB
3050 LOCATE 8,14:PRINT"PRESS 1,2,3 TO EDIT PAX, BAG OR SECTION DATA":LOCATE 8,16:PRINT"PRESS 0 FOR MENU"
3051 MOVE 10,380:DRAW 630,380:DRAW 630,310:DRAW 10,310:DRAW 10,380
3052 MOVE 10,310:DRAW 498,310:DRAW 498,210:DRAW 10,210:DRAW 10,310
3053 MOVE 630,310:DRAW 630,180:DRAW 498,180:DRAW 498,310  
3054 MOVE 300,210:DRAW 300,310
3060 J$=INKEY$
3070 IF J$="0" THEN GOTO 70
3080 IF J$="1" THEN GOTO 3100
3090 IF J$="2" THEN GOTO 3200 
3092 IF J$="3" THEN GOTO 3130
3095 GOTO 3060
3100 WCAP=0:WPAX=0:LOCATE 5,18:INPUT"MALE:     ";MALE:LOCATE 5,20:INPUT"FEMALE:   ";FEMA:LOCATE 5,22:INPUT"CHILDREN: ";CHIL:LOCATE 5,24:INPUT"INFANTS:  ";INFA
3110 IF MALE<0 OR MALE>1000 OR FEMA<0 OR FEMA>1000 OR CHIL<0 OR CHIL>1000 OR INFA<0 OR INFA>1000 THEN WPAX=1:GOSUB 9200:GOTO 3000
3115 IF MALE+FEMA+CHIL>CAP THEN WCAP=1:GOSUB 9200:GOTO 3000
3116 IF MALE+FEMA=0 AND infa>0 THEN WPAX=1:GOSUB 9200:GOTO 3000 
3117 IF infa>(MALE+FEMA)*2 THEN WPAX=1:GOSUB 9200:GOTO 3000
3120 GOTO 3000
3130 REM SECTIONS
3135 IF MALE+FEMA+CHIL+INFA=0 THEN GOTO 3140 ELSE GOTO 3150
3140 CLS:LOCATE 30,12:PRINT"ENTER PAX DATA FIRST"
3141 O$=INKEY$
3142 IF O$<>"" THEN GOTO 3000
3143 GOTO 3141
3150 IF REG=1 THEN GOTO 3160
3151 IF REG=2 THEN GOTO 3180
3160 LOCATE 5,18:INPUT"OA (1-11, MAX 63):  ";OAA:LOCATE 5,20:INPUT"OB (12-22, MAX 60): ";OBA:LOCATE 5,22:INPUT"OC (23-33, MAX 66): ";OCA 
3161 IF OAA<0 OR oba<0 OR oca<0 THEN OAA=0:OBA=0:OCA=0:GOSUB 9300:GOTO 3120 
3163 IF OAA+OBA+OCA<>MALE+FEMA+CHIL THEN GOSUB 9300:OAA=0:OBA=0:OCA=0:LOCATE 53,18:PRINT SPC(26):LOCATE 53,20:PRINT SPC(26):LOCATE 53,22:PRINT SPC(26):GOTO 3120
3164 IF OAA>63 OR OBA>60 OR OCA>66 THEN GOSUB 9300:OAA=0:OBA=0:OCA=0:LOCATE 53,18:PRINT SPC(26):LOCATE 53,20:PRINT SPC(26):LOCATE 53,22:PRINT SPC(26):GOTO 3120
3165 GOTO 3000
3180 LOCATE 5,18:INPUT"OA (1-10, MAX 60):  ";OAB:LOCATE 5,20:INPUT"OB (10-20, MAX 60): ";OBB:LOCATE 5,22:INPUT"OC (20-30, MAX 60): ";OCB
3181 IF Oab<0 OR OBB<0 OR ocB<0 THEN OAB=0:OBB=0:OCB=0:GOSUB 9300:GOTO 3120 
3183 IF OAB+OBB+OCB<>MALE+FEMA+CHIL THEN GOSUB 9300:OAB=0:OBB=0:OCB=0:LOCATE 53,18:PRINT SPC(26):LOCATE 53,20:PRINT SPC(26):LOCATE 53,22:PRINT SPC(26):LOCATE 53,24:PRINT SPC(26):GOTO 3120 
3184 IF OAB>60 OR OBB>60 OR OCB>60 THEN GOSUB 9300:OAB=0:OBB=0:OCB=0:LOCATE 53,18:PRINT SPC(26):LOCATE 53,20:PRINT SPC(26):LOCATE 53,22:PRINT SPC(26):LOCATE 53,24:PRINT SPC(26):GOTO 3120   
3185 GOTO 3000
3200 LOCATE 5,19:PRINT"FOR ACTUAL KGS PRESS 1":LOCATE 5,21:PRINT "FOR STANDARD KGS PRESS 2":LOCATE 5,23:PRINT "PRESS 0 FOR PAX/BAG MENU"
3201 K$=INKEY$
3202 IF K$="1" THEN GOTO 3209
3203 IF K$="2" THEN GOTO 3250
3204 IF K$="0" THEN GOTO 3000
3205 GOTO 3201
3209 LOCATE 5,19:PRINT SPC(40): LOCATE 5,21:PRINT SPC(40):LOCATE 5,23:PRINT SPC(40):LOCATE 5,19:INPUT"BAGS: ";BAGPW:LOCATE 5,21:INPUT"KGS: ";KGSPW 
3215 IF BAGPW>0 AND KGSPW<=0 THEN BAGPW=0:KGSPW=0:GOSUB 9800:GOTO 3000
3216 IF KGSPW>0 AND BAGPW<=0 THEN BAGPW=0:KGSPW=0:GOSUB 9800:GOTO 3000 
3217 IF KGSPW<0 OR BAGPW<0 THEN BAGPW=0:KGSPW=0:GOSUB 9800:GOTO 3000 
3230 GOTO 3400 
3250 LOCATE 5,19:PRINT SPC(40): LOCATE 5,21:PRINT SPC(40):LOCATE 5,23:PRINT SPC(40)
3260 LOCATE 5,19:INPUT"BAGS: ";BAGSPW:LOCATE 5,21:INPUT"BAG STANDARD WEIGHT: ";BAGSD
3261 IF BAGSPW<0 THEN BAGSPW=0:KGSPW=0:BAGPW=0:GOSUB 9800:GOTO 3000
3262 IF BAGSD<5 OR BAGSD>32 THEN BAGPW=0:BAGSPW=0:KGSPW=0:BAGSD=0:RF=0:GOTO 9450
3265 BAGPW=BAGSPW:KGSPW=BAGPW*BAGSD
3270 GOTO 3400
3400 RF=0:GOTO 3000
4000 REM LOAD DISTRIBUTION
4001 IF REG=0 THEN GOTO 9400
4003 IF KGSPW=0 AND (SL1I=1 OR SL2I=1) THEN GOSUB 4994:GOTO 70
4004 IF BAGPW=0 THEN BAGPW=1E-10
4006 CLS
4007 IF REG=1 THEN GOSUB 4800
4008 IF REG=2 THEN GOSUB 4900
4010 LOCATE 5,1:PRINT"DEADLOAD:";CINT(BAGPW);"BAGS";SPC(4);KGSPW;"KGS"
4011 IF SL1I=1 THEN LOCATE 40,1:PRINT SL13$;" (PW INCL.)"
4012 IF SL1I=2 THEN LOCATE 40,1:PRINT SL13$;"/";SL1PCS;"/";SL1KGS
4013 IF SL2I=2 THEN LOCATE 60,1:PRINT SL23$;"/";SL2PCS;"/";SL2KGS 
4014 IF SL2I=1 THEN LOCATE 60,1:PRINT SL23$;" (PW INCL.)"
4016 GOSUB 4700:LOCATE 2,3:PRINT"MAX:":LOCATE 8,3:PRINT CPT4$;MAXCPT4;"KGS":LOCATE 25,3:PRINT CPT3$;MAXCPT3;"KGS":LOCATE 44,3:PRINT CPT2$;MAXCPT2;"KGS":LOCATE 62,3:PRINT CPT1$;MAXCPT1;"KGS"
4020 LOCATE 5,5:PRINT CPT4BAGS;"B":LOCATE 12,5:PRINT CPT4KGS;"KGS"
4025 IF SL1CPT=4 THEN LOCATE 9,7:PRINT SL13$
4030 IF SL2CPT=4 THEN LOCATE 9,9:PRINT SL23$
4040 LOCATE 23,5:PRINT CPT3BAGS;"B":LOCATE 30,5:PRINT CPT3KGS;"KGS" 
4045 IF SL1CPT=3 THEN LOCATE 27,7:PRINT SL13$
4050 IF SL2CPT=3 THEN LOCATE 27,9:PRINT SL23$
4051 IF ULD=1 THEN LOCATE 34,9:PRINT"ULD:";CPT3ULD 
4060 LOCATE 42,5:PRINT CPT2BAGS;"B":LOCATE 49,5:PRINT CPT2KGS;"KGS"
4065 IF SL1CPT=2 THEN LOCATE 46,7:PRINT SL13$
4070 IF SL2CPT=2 THEN LOCATE 46,9:PRINT SL23$
4071 IF ULD=1 THEN LOCATE 52,9:PRINT"ULD:";CPT2ULD 
4080 LOCATE 60,5:PRINT CPT1BAGS;"B":LOCATE 67,5:PRINT CPT1KGS;"KGS" 
4085 IF SL1CPT=1 THEN LOCATE 64,7:PRINT SL13$
4090 IF SL2CPT=1 THEN LOCATE 64,9:PRINT SL23$
4091 IF ULD=1 THEN LOCATE 70,9:PRINT"ULD:";CPT1ULD
4095 LOCATE 2,11:PRINT"TTL:":LOCATE 9,11:PRINT TTLCPT4;"KGS":LOCATE 27,11:PRINT TTLCPT3;"KGS":LOCATE 45,11:PRINT TTLCPT2;"KGS":LOCATE 63,11:PRINT TTLCPT1;"KGS"
4096 TTLALL=TTLCPT1+TTLCPT2+TTLCPT3+TTLCPT4
4097 IF ULD=1 THEN LOCATE 49,20:PRINT "TTL ULDs/ ULDs WEIGHT:";TTLULD;"/";TTLULDW
4098 LOCATE 59,23:PRINT"TTL CPT KGS:";TTLALL 
4100 LOCATE 48,14:PRINT"PRESS 1 FOR LOAD DISTRIBUTION":LOCATE 48,16:PRINT"PRESS 0 FOR MENU"
4110 DA$=INKEY$
4120 IF DA$="0" THEN GOTO 70
4130 IF DA$="1" THEN GOTO 4197
4140 GOTO 4110
4197 IF KGSPW=0 AND ULD=0 THEN TTLCPT1=0:TTLCPT2=0:TTLCPT3=0:TTLCPT4=0:GOTO 4271 
4200 LOCATE 2,14:PRINT CPT1$:LOCATE 7,14:INPUT"BAGS: ";CPT1BAGS
4203 IF CPT1BAGS>BAGPW OR CPT1BAGS<0 THEN GOSUB 4750:GOTO 4000
4204 IF ULD=1 THEN LOCATE 22,14:INPUT"CPT1 ULDs ";CPT1ULD:CPT1ULD=CINT(CPT1ULD)
4205 IF CPT1ULD<0 OR CPT1ULD>3 THEN GOSUB 4750:GOTO 4000
4206 CPT1KGS=(KGSPW/BAGPW)*CPT1BAGS:CPT1KGS=CINT(CPT1KGS)
4209 IF CPT1KGS>MAXCPT1 THEN GOSUB 4620:GOTO 4000
4220 LOCATE 2,16:PRINT CPT2$:LOCATE 7,16:INPUT"BAGS: ";CPT2BAGS  
4223 IF CPT1BAGS+CPT2BAGS>BAGPW OR CPT2BAGS<0 THEN GOSUB 4750:GOTO 4000 
4224 IF ULD=1 THEN LOCATE 22,16:INPUT"CPT3 ULDs ";CPT2ULD:CPT2ULD=CINT(CPT2ULD) 
4225 IF CPT2ULD<0 OR CPT2ULD>2 THEN GOSUB 4750:GOTO 4000 
4226 CPT2KGS=(KGSPW/BAGPW)*CPT2BAGS:CPT2KGS=CINT(CPT2KGS)  
4229 IF CPT2KGS>MAXCPT2 THEN GOSUB 4620:GOTO 4000
4240 LOCATE 2,18:PRINT CPT3$:LOCATE 7,18:INPUT"BAGS: ";CPT3BAGS  
4243 IF CPT1BAGS+CPT2BAGS+CPT3BAGS>BAGPW OR CPT3BAGS<0 THEN GOSUB 4750:GOTO 4000 
4244 IF ULD=1 THEN LOCATE 22,18:INPUT"CPT4 ULDs ";CPT3ULD:CPT3ULD=CINT(CPT3ULD)  
4245 IF CPT3ULD<0 OR CPT3ULD>2 THEN GOSUB 4750:GOTO 4000 
4246 CPT3KGS=(KGSPW/BAGPW)*CPT3BAGS:CPT3KGS=CINT(CPT3KGS) 
4249 IF CPT3KGS>MAXCPT3 THEN GOSUB 4620:GOTO 4000  
4260 LOCATE 2,20:PRINT CPT4$:LOCATE 7,20:INPUT"BAGS: ";CPT4BAGS 
4261 IF BAGPW=1E-10 THEN BAGPW=CINT(BAGPW) 
4263 IF BAGPW>0 AND (CPT1BAGS+CPT2BAGS+CPT3BAGS+CPT4BAGS<>BAGPW OR CPT4BAGS<0) THEN GOSUB 4750:GOTO 4000
4265 IF BAGPW=0 THEN BAGPW=1E-10
4266 CPT4KGS=(KGSPW/BAGPW)*CPT4BAGS:CPT4KGS=CINT(CPT4KGS) 
4267 IF BAGPW=1E-10 THEN BAGPW=CINT(BAGPW) 
4269 IF CPT4KGS>MAXCPT4 THEN GOSUB 4620:GOTO 4000 
4270 GOSUB 4480:TTLCPT1=CINT(CPT1KGS)+CPT1ULD*ULDW:TTLCPT2=CINT(CPT2KGS)+CPT2ULD*ULDW::TTLCPT3=CINT(CPT3KGS)+CPT3ULD*ULDW::TTLCPT4=CINT(CPT4KGS):TTLULD=CPT1ULD+CPT2ULD+CPT3ULD:TTLULDW=TTLULD*ULDW
4271 IF KGSPW=0 THEN CPT1BAGS=0:CPT2BAGS=0:CPT3BAGS=0:CPT4BAGS=0:CPT1KGS=0:CPT2KGS=0:CPT3KGS=0:CPT4KGS=0
4272 IF SL1=0 AND SL2=0 THEN GOTO 4450
4273 IF SL1=0 AND SL2=1 THEN GOTO 4290
4280 LOCATE 2,22:PRINT"ENTER CPT FOR ";SL13$:LOCATE 21,22:INPUT"";SL1CPT
4282 IF REG=1 AND SL1CPT<>1 AND SL1CPT<>2 AND SL1CPT<>3 AND SL1CPT<>4 THEN GOSUB 4750:GOTO 4000
4283 IF REG=2 AND SL1CPT<>1 AND SL1CPT<>3 AND SL1CPT<>4 AND SL1CPT<>5 THEN GOSUB 4750:GOTO 4000 
4284 IF REG=2 AND SL1CPT>1 THEN SL1CPT=SL1CPT-1
4285 GOSUB 4410:GOSUB 4420
4286 GOSUB 4600 
4287 IF TTLCPT1>MAXCPT1 OR TTLCPT2>MAXCPT2 OR TTLCPT3>MAXCPT3 OR TTLCPT4>MAXCPT4 THEN GOSUB 4620:GOTO 4000  
4288 IF SL2=0 THEN GOTO 4450
4290 LOCATE 2,24:PRINT"ENTER CPT FOR ";SL23$:LOCATE 21,24:INPUT"";SL2CPT 
4292 IF REG=1 AND SL2CPT<>1 AND SL2CPT<>2 AND SL2CPT<>3 AND SL2CPT<>4 THEN GOSUB 4750:GOTO 4000 
4293 IF REG=2 AND SL2CPT<>1 AND SL2CPT<>3 AND SL2CPT<>4 AND SL2CPT<>5 THEN GOSUB 4750:GOTO 4000   
4294 IF REG=2 AND SL2CPT>1 THEN SL2CPT=SL2CPT-1 
4295 GOSUB 4400:GOSUB 4420
4296 GOSUB 4550 
4297 IF TTLCPT1>MAXCPT1 OR TTLCPT2>MAXCPT2 OR TTLCPT3>MAXCPT3 OR TTLCPT4>MAXCPT4 THEN GOSUB 4620:GOTO 4000   
4299 GOTO 4450
4400 IF SL2CPT=1 AND SL2I=1 AND CPT1BAGS<=0 THEN GOSUB 4750:GOTO 4000
4402 IF SL2CPT=2 AND SL2I=1 AND CPT2BAGS<=0 THEN GOSUB 4750:GOTO 4000 
4403 IF SL2CPT=3 AND SL2I=1 AND CPT3BAGS<=0 THEN GOSUB 4750:GOTO 4000 
4404 IF SL2CPT=4 AND SL2I=1 AND CPT4BAGS<=0 THEN GOSUB 4750:GOTO 4000 
4405 RETURN
4410 IF SL1CPT=1 AND SL1I=1 AND CPT1BAGS<=0 THEN GOSUB 4750:GOTO 4000
4411 IF SL1CPT=2 AND SL1I=1 AND CPT2BAGS<=0 THEN GOSUB 4750:GOTO 4000
4412 IF SL1CPT=3 AND SL1I=1 AND CPT3BAGS<=0 THEN GOSUB 4750:GOTO 4000
4413 IF SL1CPT=4 AND SL1I=1 AND CPT4BAGS<=0 THEN GOSUB 4750:GOTO 4000
4415 RETURN
4420 IF SL1I=1 AND SL2I=1 THEN GOTO 4430 ELSE RETURN
4430 IF SL1CPT=1 AND SL2CPT=1 AND CPT1BAGS<=1 THEN GOSUB 4750:GOTO 4000
4431 IF SL1CPT=2 AND SL2CPT=2 AND CPT2BAGS<=1 THEN GOSUB 4750:GOTO 4000 
4432 IF SL1CPT=3 AND SL2CPT=3 AND CPT3BAGS<=1 THEN GOSUB 4750:GOTO 4000
4433 IF SL1CPT=4 AND SL2CPT=4 AND CPT4BAGS<=1 THEN GOSUB 4750:GOTO 4000 
4434 RETURN
4450 REM FINAL CHECK DATA ENTERED
4455 RF=1
4460 CLS:LOCATE 28,12:PRINT"LOAD DISTRIBUTION APPROVED"
4470 H$=INKEY$  
4472 IF H$<>"" THEN GOTO 4000 
4475 GOTO 4470
4480 IF CPT1KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW+1 THEN CPT1KGS=CPT1KGS-1
4481 IF CPT1KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW-1 THEN CPT1KGS=CPT1KGS+1
4482 IF CPT1KGS=0 AND CPT2KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW-1 THEN CPT2KGS=CPT2KGS+1
4483 IF CPT1KGS=0 AND CPT2KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW+1 THEN CPT2KGS=CPT2KGS-1
4485 IF CPT1KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW+2 THEN CPT1KGS=CPT1KGS-2
4486 IF CPT1KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW-2 THEN CPT1KGS=CPT1KGS+2
4487 IF CPT1KGS=0 AND CPT2KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW-2 THEN CPT2KGS=CPT2KGS+2
4488 IF CPT1KGS=0 AND CPT2KGS>0 AND CPT1KGS+CPT2KGS+CPT3KGS+CPT4KGS=KGSPW+2 THEN CPT2KGS=CPT2KGS-2
4499 RETURN
4500 LOCATE 48,14:PRINT SPC(30):LOCATE 48,16:PRINT SPC(30):LOCATE 7,18:PRINT"FLT SEEMS TO BE FERRY":LOCATE 7,20:PRINT "ATTN: IF FLT IS NOT FERRY PLS ENTER DATA" 
4510 T$=INKEY$
4520 IF T$<>"" THEN RETURN
4530 GOTO 4510
4550 IF SL2CPT=1 AND SL2I=2 THEN TTLCPT1=TTLCPT1+SL2KGS 
4560 IF SL2CPT=2 AND SL2I=2 THEN TTLCPT2=TTLCPT2+SL2KGS
4570 IF SL2CPT=3 AND SL2I=2 THEN TTLCPT3=TTLCPT3+SL2KGS 
4580 IF SL2CPT=4 AND SL2I=2 THEN TTLCPT4=TTLCPT4+SL2KGS 
4590 RETURN
4600 IF SL1CPT=1 AND SL1I=2 THEN TTLCPT1=TTLCPT1+SL1KGS
4602 IF SL1CPT=2 AND SL1I=2 THEN TTLCPT2=TTLCPT2+SL1KGS
4604 IF SL1CPT=3 AND SL1I=2 THEN TTLCPT3=TTLCPT3+SL1KGS
4606 IF SL1CPT=4 AND SL1I=2 THEN TTLCPT4=TTLCPT4+SL1KGS
4610 RETURN
4620 LOCATE 30,24:PRINT"EXCEEDING CPT MAX LOAD"
4621 RF=0:GOSUB 10000
4622 CPT1KGS=0:CPT2KGS=0:CPT3KGS=0:CPT4KGS=0:CPT1BAGS=0:CPT2BAGS=0:CPT3BAGS=0:CPT4BAGS=0:TTLCPT1=0:TTLCPT2=0:TTLCPT3=0:TTLCPT4=0:SL1CPT=0:SL2CPT=0
4625 L$=INKEY$
4627 IF L$<>"" THEN RETURN
4630 GOTO 4625
4699 END
4700 IF REG=1 THEN MAXCPT4=570:MAXCPT3=3467:MAXCPT2=2670:MAXCPT1=888:CPT1$="CPT1":CPT2$="CPT2":CPT3$="CPT3":CPT4$="CPT4"
4710 IF REG=2 THEN MAXCPT4=1497:MAXCPT3=2268:MAXCPT2=2426:MAXCPT1=3402:CPT1$="CPT1":CPT2$="CPT3":CPT3$="CPT4":CPT4$="CPT5" 
4720 RETURN
4750 LOCATE 35,24:PRINT"WRONG DATA"
4752 RF=0
4753 GOSUB 10000
4755 CPT1KGS=0:CPT2KGS=0:CPT3KGS=0:CPT4KGS=0:CPT1BAGS=0:CPT2BAGS=0:CPT3BAGS=0:CPT4BAGS=0:TTLCPT1=0:TTLCPT2=0:TTLCPT3=0:TTLCPT4=0:SL1CPT=0:SL2CPT=0 
4760 DB$=INKEY$
4770 IF DB$<>"" THEN RETURN
4780 GOTO 4760
4799 END
4800 REM CPT DESIGN
4810 MOVE 26,343:DRAW 614,343:DRAW 614,247:DRAW 26,247:DRAW 26,343:MOVE 28,343:DRAW 28,247:MOVE 29,343:DRAW 29,247:MOVE 611,343:DRAW 611,247:MOVE 612,343:DRAW 612,247:MOVE 27,343:DRAW 27,247:MOVE 613,343:DRAW 613,247
4820 MOVE 174,343:DRAW 174,247:MOVE 320,343:DRAW 320,247:MOVE 321,343:DRAW 321,247:MOVE 322,343:DRAW 322,247:MOVE 323,343:DRAW 323,247:MOVE 470,343:DRAW 470,247
4830 MOVE 30,242:DRAW 90,242:MOVE 550,242:DRAW 610,242
4850 RETURN
4900 MOVE 174,247:DRAW 26,259:MOVE 174,350:DRAW 26,338:MOVE 174,350:DRAW 614,350:DRAW 614,247:DRAW 174,247:DRAW 174,350
4905 MOVE 28,338:DRAW 28,259:MOVE 29,338:DRAW 29,259:MOVE 27,338:DRAW 27,259:MOVE 26,338:DRAW 26,259:MOVE 611,350:DRAW 611,247:MOVE 612,350:DRAW 612,247:MOVE 613,350:DRAW 613,247
4908 MOVE 321,350:DRAW 321,247:MOVE 464,350:DRAW 464,247:MOVE 465,350:DRAW 465,247:MOVE 466,350:DRAW 466,247:MOVE 467,350:DRAW 467,247 
4910 MOVE 550,242:DRAW 610,242:MOVE 317,242:DRAW 257,242:MOVE 128,244:DRAW 93,244
4950 RETURN
4990 CLS:LOCATE 28,12:PRINT"THERE IS NOTHING TO LOAD"
4991 F$=INKEY$ 
4992 IF F$<>"" THEN RETURN
4993 GOTO 4991
4994 CLS:LOCATE 28,12:PRINT"ENTER BAG DATA FIRST"
4995 F$=INKEY$
4996 IF F$<>"" THEN RETURN 
4997 GOTO 4995
4999 END
5000 REM SPECIAL LOAD ENTER
5005 CLS:LOCATE 13,2:PRINT"CODE      PCS        KGS          DESCRIPTION      PW"
5010 LOCATE 5,4:PRINT "1.":LOCATE 13,4:PRINT SL13$
5011 IF SL1I=2 THEN LOCATE 22,4:PRINT SL1PCS:LOCATE 33,4:PRINT SL1KGS
5013 LOCATE 47,4:PRINT SL1D$
5015 IF SL1I=1 THEN LOCATE 64,4:PRINT"PW INCL."
5018 IF SL1I=2 THEN LOCATE 64,4:PRINT"PW NOT INCL."
5020 LOCATE 5,6:PRINT "2.":LOCATE 13,6:PRINT SL23$
5021 IF SL2I=2 THEN LOCATE 22,6:PRINT SL2PCS:LOCATE 33,6:PRINT SL2KGS
5023 LOCATE 47,6:PRINT SL2D$
5025 IF SL2I=1 THEN LOCATE 64,6:PRINT"PW INCL." 
5030 IF SL2I=2 THEN LOCATE 64,6:PRINT"PW NOT INCL." 
5040 LOCATE 18,10:PRINT"PRESS 1 OR 2 TO ENTER/EDIT SPECIAL LOAD OR 3 TO CANCEL":LOCATE 18,12:PRINT"PRESS 0 FOR MENU"
5045 MOVE 10,290:DRAW 10,390:DRAW 630,390:DRAW 630,290:DRAW 10,290
5050 S$=INKEY$
5060 IF S$="0" THEN GOTO 70
5070 IF S$="1" THEN GOTO 5100
5080 IF S$="2" THEN GOTO 5200
5085 IF S$="3" THEN GOTO 5300
5090 GOTO 5050
5100 REM SL1
5110 LOCATE 5,14:SL1=1:INPUT"CODE: ";SL13$
5120 IF LEN(SL13$)<3 OR LEN(SL13$)>4 THEN SL13$="":GOSUB 5740:GOTO 5000
5135 LOCATE 5,16:INPUT"DESCRIPTION: ";SL1D$ 
5140 IF LEN(SL1D$)>10 THEN SL1D$=LEFT$(SL1D$,10)  
5150 LOCATE 5,18:PRINT"INCLUDED IN PW? (Y/N)      (IF YES WEIGHT WILL BE CALCULATED AS MEAN)"
5160 T$=INKEY$
5165 IF T$="Y" OR T$="y" THEN SL1I=1:SL1=1:SL1PCS=0:SL1KGS=0:GOTO 5400
5170 IF T$="N" OR T$="n" THEN SL1I=2:SL1=1:GOTO 5175
5173 GOTO 5160
5175 LOCATE 5,20:INPUT"PCS: ";SL1PCS
5176 IF SL1PCS<=0 THEN GOSUB 5740:GOTO 5000
5180 LOCATE 5,22:INPUT"KGS: ";SL1KGS:SL1KGS=INT(SL1KGS)
5185 IF SL1KGS<=0 THEN SL1KGS=0:GOSUB 5740:GOTO 5000
5199 GOTO 5400
5200 REM SL2
5210 LOCATE 5,14:SL2=1:INPUT"CODE: ";SL23$ 
5220 IF LEN(SL23$)<3 OR LEN(SL23$)>4 THEN SL23$="":GOSUB 5750:GOTO 5000
5235 LOCATE 5,16:INPUT"DESCRIPTION: ";SL2D$   
5240 IF LEN(SL2D$)>10 THEN SL2D$=LEFT$(SL2D$,10) 
5250 LOCATE 5,18:PRINT"INCLUDED IN PW? (Y/N)      (IF YES WEIGHT WILL BE CALCULATED AS MEAN)"
5260 T$=INKEY$ 
5265 IF T$="Y" OR T$="y" THEN SL2I=1:SL2=1:SL2PCS=0:SL2KGS=0:GOTO 5400
5270 IF T$="N" OR T$="n" THEN SL2I=2:SL2=1:GOTO 5275
5272 GOTO 5260
5275 LOCATE 5,20:INPUT"PCS: ";SL2PCS 
5276 IF SL2PCS<=0 THEN GOSUB 5750:GOTO 5000
5280 LOCATE 5,22:INPUT"KGS: ";SL2KGS:SL2KGS=INT(SL2KGS)  
5285 IF SL2KGS<=0 THEN SL2KGS=0:GOSUB 5750:GOTO 5000 
5299 GOTO 5400
5300 IF SL1=0 AND SL2=0 THEN GOSUB 9700:GOTO 5000
5310 LOCATE 5,16:PRINT"PRESS 1 TO CANCEL SPECIAL LOAD 1":LOCATE 5,18:PRINT"PRESS 2 TO CANCEL SPECIAL LOAD 2":LOCATE 5,20:PRINT"PRESS 0 FOR MENU"
5320 U$=INKEY$
5330 IF U$="0" THEN GOTO 5000
5335 REM PLS ADD CPT CNX LOAD DATA!!!!!
5340 IF U$="1" THEN SL1=0:SL1I=0:SL1D$="":SL13$="":SL1KGS=0:SL1PCS=0:GOTO 5400
5350 IF U$="2" THEN SL2=0:SL2I=0:SL2D$="":SL23$="":SL2KGS=0:SL2PCS=0:GOTO 5400 
5360 GOTO 5320
5400 RF=0:GOTO 5000
5740 SL1=0:SL1I=0:SL1D$="":SL13$="":SL1KGS=0:SL1PCS=0:RF=0
5749 RETURN
5750 SL2=0:SL2I=0:SL2D$="":SL23$="":SL2KGS=0:SL2PCS=0:RF=0
5760 RETURN
5999 END
6000 REM F
6005 IF REG<>1 AND REG<>2 THEN GOTO 6950
6010 CLS:LOCATE 5,5:PRINT" BLOCK       TAXI       TRIP        CREW          DOW        DOI"       
6020 LOCATE 5,7:PRINT BLOCK:LOCATE 17,7:PRINT TAXI:LOCATE 28,7:PRINT TRIP:LOCATE 40,7:PRINT CCOC;"/":LOCATE 44,7:PRINT CCAB:LOCATE 54,7:PRINT DOW:LOCATE 65,7:PRINT DOI
6025 IF BLOCK>0 AND RF=1 THEN LOCATE 32,3:PRINT"AZFW:";MALE*MALEW+FEMA*FEMAW+CHIL*CHILW+INFA*INFAW+TTLALL+DOW
6030 LOCATE 25,10:PRINT"PRESS 1 TO ENTER/EDIT TRIP INFO":LOCATE 25,12:PRINT"PRESS 0 FOR MENU"
6035 MOVE 15,272:DRAW 15,380:DRAW 625,380:DRAW 625,272:DRAW 15,272 
6040 V$=INKEY$
6050 IF V$="0" THEN GOTO 70
6055 IF V$="1" THEN GOTO 6100
6060 GOTO 6040
6100 LOCATE 5,14:INPUT"BLOCK FUEL:   ";BLOCK
6110 IF BLOCK<=2500 OR BLOCK>25000 THEN GOSUB 6900:GOTO 6000
6120 LOCATE 5,16:INPUT"TAXI FUEL:    ";TAXI
6130 IF TAXI<=50 OR TAXI>BLOCK OR TAXI>1000 THEN GOSUB 6900:GOTO 6000
6140 LOCATE 5,18:INPUT"TRIP FUEL:    ";TRIP
6150 IF TRIP<=100 OR TRIP<TAXI OR TRIP>BLOCK THEN GOSUB 6900:GOTO 6000 
6160 LOCATE 5,20:INPUT"COCKPIT CREW: ";CCOC
6170 IF CCOC<2 OR CCOC>4 THEN GOSUB 6900:GOTO 6000 
6173 LOCATE 32,20:INPUT"CABIN CREW: ";CCAB
6177 IF CCAB<0 OR CCAB>6 THEN GOSUB 6900:GOTO 6000
6178 IF REG=1 THEN SDOW=BASA+(CCOC*CREA)+(CCAB*CREA)+PANAK
6179 IF REG=2 THEN SDOW=BASB+(CCOC*CREB)+(CCAB*CREB)+PANBK 
6180 LOCATE 5,22:PRINT"SYSTEM DOW:";SDOW:LOCATE 32,22:PRINT"ACCEPT? (Y/N)"
6183 G$=INKEY$
6185 IF G$="Y" OR G$="y" THEN LOCATE 32,22:PRINT SPC(15):DOW=SDOW:GOTO 6195
6186 IF G$="n" OR G$="N" THEN GOTO 6188
6187 GOTO 6183
6188 LOCATE 32,22:INPUT"ENTER NEW DOW: ";FDOW
6190 IF FDOW<40000 OR FDOW>55000 THEN GOSUB 6800:GOTO 6000
6193 DOW=FDOW
6195 GOTO 6199
6199 GOSUB 6300
6200 LOCATE 5,24:PRINT"SYSTEM DOI:";SDOI:LOCATE 32,24:PRINT"ACCEPT? (Y/N)"
6203 H$=INKEY$ 
6205 IF H$="Y" OR H$="y" THEN LOCATE 32,24:PRINT SPC(15):DOI=SDOI:GOTO 6219
6206 IF H$="n" OR H$="N" THEN GOTO 6212
6210 GOTO 6203
6212 LOCATE 32,24:INPUT"ENTER NEW DOI: ";FDOI
6215 IF FDOI<30 OR FDOI>70 THEN GOSUB 6800:GOTO 6000 
6217 DOI=FDOI
6219 GOTO 6000
6230 LOCATE 58,15:PRINT"DATA ENTERED"
6240 R$=INKEY$
6250 IF R$<>"" THEN GOTO 6000
6260 GOTO 6240
6300 IF REG=1 AND CCAB>4 THEN SDOI=49.66+PANAK/70*0.1-CCOC*CREA*0.017+(CCAB-4)*CREA*0.015
6301 IF REG=1 AND CCAB<=2 THEN SDOI=49.66+PANAK/70*0.1-CCOC*CREA*0.017-CCAB*CREA*0.015
6302 IF REG=1 AND CCAB=3 THEN SDOI=49.66+PANAK/70*0.1-CCOC*CREA*0.017-1*CREA*0.015
6303 IF REG=1 AND CCAB=4 THEN SDOI=49.66+PANAK/70*0.1-CCOC*CREA*0.017
6304 SDOI=ROUND(SDOI,2):IF REG=2 THEN SDOI=51.9
6305 RETURN
6800 BLOCK=0:TAXI=0:TRIP=0:CCOC=0:CCAB=0:DOW=0:DOI=0
6810 LOCATE 63,16:PRINT"WRONG DATA"
6820 T$=INKEY$
6830 IF T$<>"" THEN RETURN
6840 GOTO 6820
6900 LOCATE 34,22:PRINT"WRONG DATA"
6910 BLOCK=0:TAXI=0:TRIP=0:CCOC=0:CCAB=0:DOW=0:DOI=0
6920 W$=INKEY$
6930 IF W$<>"" THEN RETURN
6940 GOTO 6920
6950 CLS:LOCATE 28,12:PRINT"SELECT REGISTRATION FIRST"
6960 T$=INKEY$
6970 IF T$<>"" THEN GOTO 70
6980 GOTO 6960
6990 END
7000 REM Ls
7005 IF REG<>1 AND REG<>2 THEN GOSUB 9400:GOTO 70
7006 IF FLT$="" OR DATE$="" OR STA$="" OR DEST$="" THEN GOSUB 7775:GOTO 70 
7011 IF REG=1 AND MALE+FEMA+CHIL<>OAA+OBA+OCA THEN GOSUB 7750:GOTO 70
7012 IF REG=2 AND MALE+FEMA+CHIL<>OAB+OBB+OCB THEN GOSUB 7750:GOTO 70
7013 IF RF<>1 THEN GOSUB 8400:GOTO 70 
7014 IF BLOCK=0 OR TAXI=0 OR TRIP=0 OR CCOC=0 OR DOW=0 OR DOI=0 THEN GOSUB 7770:GOTO 70   
7015 GOSUB 7700
7020 GOSUB 7800
7050 IF REG=1 THEN GOSUB 7300
7053 IF REG=2 AND AZFW>MAXZFW OR ATOW>MAXTOW OR ALW>MAXLW THEN GOSUB 7600:GOTO 70
7054 IF REG=2 THEN GOTO 7100
7055 IF AZFW>MAXZFW OR ATOW>MAXTOW OR ALW>MAXLW OR LIZFW1<A OR LIZFW1>B OR LITOW1<C OR LITOW1>D OR LILAW1<E OR LILAW1>F THEN GOSUB 7600:GOTO 70
7100 GOSUB 7600:REM LS 
7110 IF REG=1 THEN:CLS:LOCATE 3,1:PRINT"L O A D S H E E T";SPC(37);"APPROVED BY: ";USER$
7111 IF REG=2 THEN:CLS:LOCATE 3,1:PRINT"W E I G H T S H E E T";SPC(33);"APPROVED BY: ";USER$
7115 LOCATE 3,3:PRINT STA$;SPC(2);DEST$;SPC(5);FLT$;"/";LEFT$(DATE$,2);SPC(6);REGP$;SPC(6);CAP$;SPC(8);CCOC;"/";CCAB;SPC(8);DATE$
7120 IF REG=1 THEN LOCATE 3,5:PRINT"LOAD IN COMPARTMENTS";SPC(5);TTLALL;SPC(5);"1/";TTLCPT1;"2/";TTLCPT2;"3/";TTLCPT3;"4/";TTLCPT4
7125 IF REG=2 THEN LOCATE 3,5:PRINT"LOAD IN COMPARTMENTS";SPC(5);TTLALL;SPC(5);"1/";TTLCPT1;"3/";TTLCPT2;"4/";TTLCPT3;"5/";TTLCPT4 
7130 LOCATE 3,7:PRINT"PASSENGER/CABIN BAG ";SPC(5);PAXKGS;SPC(5);MALE;"/";FEMA;"/";CHIL;"/";INFA;SPC(3);"TTL:";MALE+FEMA+CHIL+INFA
7135 LOCATE 3,9:PRINT"TOTAL TRAFFIC LOAD  ";SPC(5);TTLTRAF
7140 LOCATE 3,11:PRINT"DRY OPERATING WEIGHT";SPC(5);DOW
7145 LOCATE 3,13:PRINT"ZERO FUEL WEIGHT ACTUAL";SPC(5);AZFW;SPC(5);"MAX";SPC(5);MAXZFW
7150 LOCATE 3,14:PRINT"TAKE OFF FUEL";SPC(5);BLOCK-TAXI
7155 LOCATE 3,15:PRINT"TAKE OFF WEIGHT ACTUAL ";SPC(5);ATOW;SPC(5);"MAX";SPC(5);MAXTOW
7160 LOCATE 3,16:PRINT"TRIP FUEL    ";SPC(5);TRIP
7165 LOCATE 3,17:PRINT"LANDING WEIGHT ACTUAL  ";SPC(5);ALW;SPC(5);"MAX";SPC(5);MAXLW
7180 LOCATE 3,19:PRINT"DOI";SPC(5);DOI
7183 IF REG=1 THEN LOCATE 3,20:PRINT"LIZFW:";LIZFW1;"LITOW";LITOW1;"LILAW";LILAW1
7184 IF REG=1 THEN LOCATE 3,21:PRINT"MACZFW";MACZ;"MACTOW";MACT;"MACLAW";MACL
7185 IF REG=1 THEN LOCATE 3,24:PRINT"OA/";OAA;"OB/";OBA;"OC/";OCA 
7186 IF REG=2 THEN LOCATE 3,24:PRINT"OA/";OAB;"OB/";OBB;"OC/";OCB
7190 LOCATE 3,22:PRINT"UNDERLOAD BEFORE LMC";SPC(5);UNDERLOAD;SPC(15);"LMC TTL + - "
7195 IF SL1=0 AND SL2=0 THEN LOCATE 3,25:PRINT"NOTOC: NO" ELSE LOCATE 3,25:PRINT"NOTOC: YES"
7199 LOCATE 20,25:PRINT"PAX WEIGHTS USED: ";SPC(5);"M";MALEW;"  F";FEMAW;"  C";CHILW;"  I";INFAW
7210 LS$=INKEY$
7220 IF LS$<>"" THEN GOTO 70
7230 GOTO 7210
7299 END
7300 REM UNDEX CALCULATION REG1
7303 TF=BLOCK-TAXI:LF=BLOCK-TAXI-TRIP
7305 INDEX1CAB=(-OAA/5*3.7)+(OCA/5*3.375) 
7309 INDEX1COP=(-TTLCPT1*0.01203)+(-TTLCPT2*0.00742)+(TTLCPT3*0.00596)+(TTLCPT4*0.01177):GOTO 7311
7310 INDEX1COP=(-TTLCPT1/250*3.03)+(-TTLCPT2/250*1.89)+(TTLCPT3/250*1.5)+(TTLCPT4/250*2.95)
7311 LIZFW1=ROUND((DOI+INDEX1CAB+INDEX1COP),2)
7315 REM REG1 BLOCK FUEL INDEX
7316 IF TF<=4000 THEN INDEX1B=(TF-0)/4000:GOTO 7346
7317 IF TF<=4900 THEN INDEX1B=(TF-4000)/900+1:GOTO 7346
7318 IF TF<=5500 THEN INDEX1B=(TF-4900)/600+2:GOTO 7346 
7319 IF TF<=6000 THEN INDEX1B=(TF-5500)/500+3:GOTO 7346 
7320 IF TF<=6400 THEN INDEX1B=(TF-6000)/400+4:GOTO 7346
7321 IF TF<=6800 THEN INDEX1B=(TF-6400)/400+5:GOTO 7346
7322 IF TF<=7100 THEN INDEX1B=(TF-6800)/300+6:GOTO 7346
7323 IF TF<=7400 THEN INDEX1B=(TF-7100)/300+7:GOTO 7346
7324 IF TF<=7700 THEN INDEX1B=(TF-7400)/300+8:GOTO 7346
7325 IF TF<=8000 THEN INDEX1B=9:GOTO 7346
7326 IF TF<=8800 THEN INDEX1B=-(TF-8000)/800+9:GOTO 7346
7327 IF TF<=9400 THEN INDEX1B=-(TF-8800)/600+8:GOTO 7346
7328 IF TF<=10000 THEN INDEX1B=-(TF-9400)/600+7:GOTO 7346
7329 IF TF<=10600 THEN INDEX1B=-(TF-10000)/600+6:GOTO 7346
7330 IF TF<=11300 THEN INDEX1B=-(TF-10600)/700+5:GOTO 7346
7331 IF TF<=12000 THEN INDEX1B=-(TF-11300)/700+4:GOTO 7346
7332 IF TF<=12600 THEN INDEX1B=-(TF-12000)/600+3:GOTO 7346
7333 IF TF<=13300 THEN INDEX1B=-(TF-12600)/700+2:GOTO 7346
7334 IF TF<=14000 THEN INDEX1B=-(TF-13300)/700+1:GOTO 7346
7335 IF TF<=14700 THEN INDEX1B=-(TF-14000)/700+0:GOTO 7346
7336 IF TF<=15400 THEN INDEX1B=-(TF-14700)/700-1:GOTO 7346
7337 IF TF<=16100 THEN INDEX1B=-(TF-15400)/700-2:GOTO 7346 
7338 IF TF<=16800 THEN INDEX1B=-(TF-16100)/700-3:GOTO 7346 
7339 IF TF<=17500 THEN INDEX1B=-(TF-16800)/700-4:GOTO 7346
7340 IF TF<=18200 THEN INDEX1B=-(TF-17500)/700-5:GOTO 7346
7341 IF TF<=18900 THEN INDEX1B=-(TF-18200)/700-6:GOTO 7346 
7342 IF TF<=19500 THEN INDEX1B=-(TF-18900)/600-7:GOTO 7346 
7343 IF TF<=20100 THEN INDEX1B=-(TF-19500)/600-8:GOTO 7346
7344 IF TF<=20800 THEN INDEX1B=-(TF-20100)/700-9:GOTO 7346
7345 IF TF>20800 THEN INDEX1B=-10
7346 LITOW1=ROUND((DOI+INDEX1CAB+INDEX1COP+INDEX1B),2)
7349 REM LILAW CALCULATION
7350 IF LF<=4000 THEN INDEX1L=(LF-0)/4000:GOTO 7385
7351 IF LF<=4900 THEN INDEX1L=(LF-4000)/900+1:GOTO 7385
7352 IF LF<=5500 THEN INDEX1L=(LF-4900)/600+2:GOTO 7385
7353 IF LF<=6000 THEN INDEX1L=(LF-5500)/500+3:GOTO 7385
7354 IF LF<=6400 THEN INDEX1L=(LF-6000)/400+4:GOTO 7385
7355 IF LF<=6800 THEN INDEX1L=(LF-6400)/400+5:GOTO 7385
7356 IF LF<=7100 THEN INDEX1L=(LF-6800)/300+6:GOTO 7385
7357 IF LF<=7400 THEN INDEX1L=(LF-7100)/300+7:GOTO 7385
7358 IF LF<=7700 THEN INDEX1L=(LF-7400)/300+8:GOTO 7385
7359 IF LF<=8000 THEN INDEX1L=9:GOTO 7385
7360 IF LF<=8800 THEN INDEX1L=-(LF-8000)/800+9:GOTO 7385
7361 IF LF<=9400 THEN INDEX1L=-(LF-8800)/600+8:GOTO 7385
7362 IF LF<=10000 THEN INDEX1L=-(LF-9400)/600+7:GOTO 7385
7363 IF LF<=10600 THEN INDEX1L=-(LF-10000)/600+6:GOTO 7385
7364 IF LF<=11300 THEN INDEX1L=-(LF-10600)/700+5:GOTO 7385
7365 IF LF<=12000 THEN INDEX1L=-(LF-11300)/700+4:GOTO 7385
7366 IF LF<=12600 THEN INDEX1L=-(LF-12000)/600+3:GOTO 7385
7367 IF LF<=13300 THEN INDEX1L=-(LF-12600)/700+2:GOTO 7385
7368 IF LF<=14000 THEN INDEX1L=-(LF-13300)/700+1:GOTO 7385
7369 IF LF<=14700 THEN INDEX1L=-(LF-14000)/700+0:GOTO 7385
7370 IF LF<=15400 THEN INDEX1L=-(LF-14700)/700-1:GOTO 7385
7371 IF LF<=16100 THEN INDEX1L=-(LF-15400)/700-2:GOTO 7385
7372 IF LF<=16800 THEN INDEX1L=-(LF-16100)/700-3:GOTO 7385
7373 IF LF<=17500 THEN INDEX1L=-(LF-16800)/700-4:GOTO 7385
7374 IF LF<=18200 THEN INDEX1L=-(LF-17500)/700-5:GOTO 7385
7375 IF LF<=18900 THEN INDEX1L=-(LF-18200)/700-6:GOTO 7385
7376 IF LF<=19500 THEN INDEX1L=-(LF-18900)/600-7:GOTO 7385
7377 IF LF<=20100 THEN INDEX1L=-(LF-19500)/600-8:GOTO 7385
7378 IF LF<=20800 THEN INDEX1L=-(LF-20100)/700-9:GOTO 7385
7379 IF LF>20800 THEN INDEX1L=-10 
7385 LILAW1=ROUND((DOI+INDEX1CAB+INDEX1COP+INDEX1L),2)
7390 MACZ=(35000*(LIZFW1-45)/AZFW+658.3-627.1)/1.558
7391 MACT=(35000*(LITOW1-45)/ATOW+658.3-627.1)/1.558
7392 MACL=(35000*(LILAW1-45)/ALW+658.3-627.1)/1.558
7395 MACZ=ROUND(MACZ,2):MACT=ROUND(MACT,2):MACL=ROUND(MACL,2)
7400 D=ROUND(49+(ATOW-40000)*0.0011515,2):B=ROUND(59+(AZFW-40000)*0.00073733,2):F=ROUND(64+(ALW-40000)*0.00072,2)
7410 IF AZFW<62500 THEN A=ROUND(30-(AZFW-40000)*0.000533,2)
7420 IF AZFW>62500 THEN A=ROUND(18+(AZFW-62500)*0.000727,2)
7430 IF ATOW<62500 THEN C=ROUND(30-(ATOW-40000)*0.000533,2)
7440 IF ATOW>62500 THEN C=ROUND(18+(ATOW-62500)*0.000727,2)
7445 IF ATOW>73000 THEN D=ROUND(87-(ATOW-73000)*0.001333,2)
7450 IF ALW<62500 THEN E=ROUND(30-(ALW-40000)*0.000533,2)
7460 IF ALW>62500 THEN E=ROUND(18+(ALW-62500)*0.000727,2)
7499 RETURN
7600 REM OVER!!!!
7620 CLS:LOCATE 31,4:PRINT"MAXIMUM A/C LIMITS"
7625 LOCATE 13,9:PRINT"AZFW: ";AZFW;SPC(8);"MAX ZFW: ";MAXZFW;SPC(8)
7627 IF AZFW<=MAXZFW THEN LOCATE 60,9:PRINT"OK" ELSE LOCATE 55,9:PRINT"OVER";AZFW-MAXZFW;"KGS"
7630 LOCATE 13,12:PRINT"ATOW: ";ATOW;SPC(8);"MAX TOW: ";MAXTOW;SPC(8) 
7635 IF ATOW<=MAXTOW THEN LOCATE 60,12:PRINT"OK" ELSE LOCATE 55,12:PRINT"OVER";ATOW-MAXTOW;"KGS"
7637 LOCATE 13,15:PRINT"ALAW: ";ALW;SPC(8);"MAX LAW: ";MAXLW;SPC(8) 
7638 IF ALW<=MAXLW THEN LOCATE 60,15:PRINT"OK" ELSE LOCATE 55,15:PRINT"OVER";ALW-MAXLW;"KGS" 
7639 IF REG=2 THEN GOTO 7680
7640 LOCATE 13,18:PRINT"LIZFW:";LIZFW1:LOCATE 34,18:PRINT"LIM ";A;"/";B"
7641 IF LIZFW1<A OR LIZFW1>B THEN LOCATE 55,18:PRINT"OUT OF LIMITS" ELSE LOCATE 60,18:PRINT"OK"
7642 LOCATE 13,20:PRINT"LITOW:";LITOW1:LOCATE 34,20:PRINT"LIM ";C;"/";D
7643 IF LITOW1<C OR LITOW1>D THEN LOCATE 55,20:PRINT"OUT OF LIMITS" ELSE LOCATE 60,20: PRINT"OK"
7644 LOCATE 13,22:PRINT"LILAW:";LILAW1;SPC(8):LOCATE 34,22:PRINT"LIM ";E;"/";F
7645 IF LILAW1<E OR LILAW1>F THEN LOCATE 55,22:PRINT"OUT OF LIMITS" ELSE LOCATE 60,22:PRINT"OK"
7680 V$=INKEY$
7681 IF V$<>"" THEN RETURN 
7682 GOTO 7680
7700 REM A/C DATA
7710 IF REG=1 THEN MAXZFW=61688:MAXTOW=79015:MAXLW=65802
7720 IF REG=2 THEN MAXZFW=62500:MAXTOW=76250:MAXLW=66000
7721 IF REG=1 THEN REGP$="SXKAF"
7722 IF REG=2 THEN REGP$="SXOFI"
7730 RETURN
7750 CLS:LOCATE 23,12:PRINT"PAX DATA AND SECTION DATA INCOMPATIBLE"
7755 K$=INKEY$
7756 IF K$<>"" THEN RETURN
7757 GOTO 7755
7770 CLS:LOCATE 32,5:LOCATE 30,12:PRINT"ENTER TRIP INFO"
7771 G$=INKEY$
7772 IF G$<>"" THEN RETURN
7773 GOTO 7771
7775 CLS:LOCATE 33,12:PRINT"ENTER FLT DATA"
7776 K$=INKEY$ 
7777 IF K$<>"" THEN RETURN 
7778 GOTO 7776
7800 REM LOADSHEET CALCULATIONS
7810 PAXKGS=MALE*MALEW+FEMA*FEMAW+CHIL*CHILW+INFA*INFAW
7820 TTLTRAF=TTLALL+PAXKGS 
7825 AZFW=DOW+TTLTRAF
7830 ATOW=AZFW+BLOCK-TAXI
7835 ALW=ATOW-TRIP
7840 UNDERZ=MAXZFW-AZFW:UNDERT=MAXTOW-ATOW:UNDERL=MAXLW-ALW
7841 IF UNDERZ<=UNDERT AND UNDERZ<=UNDERL THEN UNDERLOAD=UNDERZ 
7842 IF UNDERT<=UNDERZ AND UNDERT<=UNDERL THEN UNDERLOAD=UNDERT
7843 IF UNDERL<=UNDERZ AND UNDERL<=UNDERT THEN UNDERLOAD=UNDERL
7850 RETURN
7999 END
8000 REM NOTOC
8010 IF SL1=0 AND SL2=0 THEN GOSUB 9600:GOTO 70
8020 IF SL1CPT=0 AND SL2CPT=0 THEN GOSUB 9630:GOTO 70
8030 IF FLT$="" OR DATE$="" OR STA$="" OR DEST$="" THEN GOSUB 9650:GOTO 70 
8035 IF RF=0 THEN GOSUB 8400:GOTO 70
8050 CLS:LOCATE 5,3:PRINT flt$;SPC(5);DATE$;SPC(5);STA$;SPC(5);DEST$;SPC(5);REG$;SPC(5);"PREPARED BY: ";USER$:LOCATE 30,6:PRINT"NOTIFICATION TO CPT":LOCATE 5,8:PRINT"SPECIAL LOAD:"
8055 IF SL1=0 AND SL2=1 THEN GOTO 8100
8060 LOCATE 5,12:PRINT "1.";SPC(14);SL13$;SPC(14);SL1D$;SPC(14);
8061 NSL1CPT=SL1CPT:IF REG=2 AND SL1CPT>=2 THEN NSL1CPT=NSL1CPT+1
8062 LOCATE 63,12:PRINT"CPT";NSL1CPT 
8068 IF SL2=0 THEN GOTO 8076
8070 LOCATE 5,14:PRINT "2.";SPC(14);SL23$;SPC(14);SL2D$;SPC(14); 
8071 NSL2CPT=SL2CPT:IF REG=2 AND SL2CPT>=2 THEN NSL2CPT=NSL2CPT+1 
8072 LOCATE 63,14:PRINT"CPT";NSL2CPT 
8076 LOCATE 10,21:PRINT"PILOT IN COMMAND";SPC(29);"RAMP  AGENT"
8077 LOCATE 13,23:PRINT"SIGNATURE";SPC(34);"SIGNATURE"
8078 MOVE 15,15:DRAW 625,15:DRAW 625,385:DRAW 15,385:DRAW 15,15
8080 R$=INKEY$
8090 IF R$<>"" THEN GOTO 70
8095 GOTO 8080
8100 LOCATE 5,12:PRINT "1.";SPC(14);SL23$;SPC(14);SL2D$;SPC(14);
8110 NSL2CPT=SL2CPT:IF REG=2 AND SL2CPT>=2 THEN NSL2CPT=NSL2CPT+1
8112 LOCATE 56,12:PRINT"CPT";NSL2CPT 
8116 GOTO 8076
8400 CLS:LOCATE 7,12:PRINT"LOAD DISTRIBUTION NOT APPROVED YET - PLS ENTER LOAD DISTRIBUTION DATA"
8410 L$=INKEY$
8420 IF L$<>"" THEN RETURN
8430 GOTO 8410
8498 END
8499 REM RESTART
8500 CLS:LOCATE 32,12:PRINT"RESTART ? (Y/N)":LOCATE 29,15:PRINT"ALL DATA WILL BE LOST"
8510 Z$=INKEY$
8520 IF Z$="Y" OR Z$="y" THEN CLEAR:GOTO 10
8530 IF Z$="N" OR Z$="n" THEN GOTO 70
8540 GOTO 8510
8999 END
9000 REM A/C DATA
9010 BASA=41640:CREA=90:PANA=2:PANAK=210
9020 BASB=41987:CREB=90:PANB=2:PANBK=210
9025 PAXW$="STANDARD":MALEW=88:FEMAW=70:CHILW=35:INFAW=0
9027 ULD=0:ULDW=90
9030 RETURN
9090 REM PANT
9100 IF REG=1 AND PANAFIXED>0 THEN PANAK=PANAFIXED
9110 IF REG=1 AND PANA=0 THEN PANAK=0
9115 IF REG=1 AND PANA=1 THEN PANAK=105
9118 IF REG=1 AND PANA=2 THEN PANAK=210
9121 IF REG=2 AND PANBFIXED>0 THEN PANBK=PANBFIXED
9123 IF REG=2 AND PANB=0 THEN PANBK=0  
9125 IF REG=2 AND PANB=1 THEN PANBK=105
9128 IF REG=2 AND PANB=2 THEN PANBK=210
9130 RETURN
9190 REM WDATA
9200 LOCATE 35,22:PRINT"WRONG DATA" 
9205 IF WCAP=1 THEN LOCATE 32,24:PRINT"EXCEEDS CAPACITY":MALE=0:FEMA=0:CHIL=0:INFA=0
9206 IF WPAX=1 THEN MALE=0:FEMA=0:CHIL=0:INFA=0     
9210 G$=INKEY$  
9220 IF G$<>"" THEN RETURN
9230 GOTO 9210   
9300 REM SEC
9310 LOCATE 35,20:PRINT"WRONG DATA"
9320 G$=INKEY$ 
9330 IF G$<>"" THEN RETURN
9340 GOTO 9310
9400 REM reg
9410 CLS:LOCATE 26,12:PRINT"SELECT REGISTRATION FIRST"
9420 M$=INKEY$
9430 IF M$<>"" THEN GOTO 70
9440 GOTO 9420
9450 REM WBG
9460 CLS:LOCATE 16,12:PRINT"BAG STANDARD WEIGHT CANNOT BE UNDER 5 OR OVER 32"
9470 P$=INKEY$
9480 IF P$<>"" THEN GOTO 3000
9490 GOTO 9470
9500 REM A/CC
9510 CLS: LOCATE 2,10:PRINT"IF YOU CHANGE A/C, SECTION DATA, COMPARTMENT DATA, AND TRIP INFO WILL BE LOST": LOCATE 33,12:PRINT"PROCEED ? (Y/N)"
9520 N$=INKEY$
9521 IF N$="N"OR N$="n" THEN GOTO 1000
9522 IF N$="Y"OR N$="y" THEN GOTO 9526
9525 GOTO 9520
9526 RF=0:GOSUB 10000:REG=CREG:OAA=0:OBA=0:OCA=0:OAB=0:OBB=0:OCB=0
9527 IF REG=1 THEN REG$="SX-KAF":ULD=0:CAP$="189Y"
9528 IF REG=2 THEN REG$="SX-OFI":CAP$="180Y"
9530 REM LOST DATA
9531 BLOCK=0:TRIP=0:TAXI=0:CCOC=0:CCAB=0:DOW=0:DOI=0
9532 CPT1KGS=0:CPT2KGS=0:CPT3KGS=0:CPT4KGS=0:TTLCPT1=0:TTLCPT2=0:TTLCPT3=0:TTLCPT4=0:TTLALL=0:SL1CPT=0:SL2CPT=0
9535 GOSUB 9090
9537 IF REG=2 THEN LOCATE 20,18:PRINT"SX-OFI:":GOSUB 1800
9540 GOTO 1000
9550 CLS:LOCATE 30,12:PRINT"DATA CANNOT BE UNDER ZERO"
9560 ZA$=INKEY$
9570 IF ZA$<>"" THEN RETURN
9580 GOTO 9560
9590 REM REG
9595 LOCATE 3,22:PRINT"SELECTED REG:":LOCATE 3,24:PRINT REG$
9596 IF REG=2 AND ULD=1 THEN LOCATE 11,24:PRINT"ULD "
9597 IF REG=2 AND ULD=0 THEN LOCATE 11,24:PRINT"BULK"
9598 RETURN
9600 REM NOTOC
9605 CLS:LOCATE 20,12:PRINT"NOTOC NOT NEEDED - NO SPECIAL LOAD ENTERED"
9610 P$=INKEY$
9615 IF P$<>"" THEN RETURN
9620 GOTO 9610
9630 REM NOTOC
9635 CLS:LOCATE 10,12:PRINT"PLS FIRST ENTER SPECIAL LOAD IN CPT IN LOAD DISTRIBUTION SECTION"
9640 P$=INKEY$
9645 IF P$<>"" THEN RETURN 
9647 GOTO 9640
9650 REM FLT DATA
9655 CLS:LOCATE 32,12:PRINT"ENTER FLT DATA" 
9660 Q$=INKEY$
9665 IF Q$<>"" THEN RETURN 
9670 GOTO 9660
9700 REM CNXSL
9705 CLS:LOCATE 30,12:PRINT"NO SPECIAL LOADS ENTERED"
9710 U$=INKEY$
9715 IF U$<>"" THEN RETURN
9720 GOTO 9710
9750 REM W SL
9760 LOCATE 35,22:PRINT"WRONG DATA"
9762 IF SL1=1 THEN SL1=0:SL1KGS=0:SL1D$="":SL13$="":SL1I=0:SL1PCS=0
9763 IF SL2=1 THEN SL2=0:SL2KGS=0:SL2D$="":SL23$="":SL2I=0:SL2PCS=0
9764 RF=0
9765 G$=INKEY$
9770 IF G$<>"" THEN RETURN 
9775 GOTO 9765
9800 REM WRONG BAG
9805 RF=0
9810 LOCATE 35,22:PRINT"WRONG DATA"
9820 D$=INKEY$
9830 IF D$<>"" THEN RETURN
9840 GOTO 9820
10000 TTLALL=0:TTLCPT1=0:TTLCPT2=0:TTLCPT3=0:TTLCPT4=0:CPT1BAGS=0:CPT2BAGS=0:CPT3BAGS=0:CPT4BAGS=0:CPT1KGS=0:CPT2KGS=0:CPT3KGS=0:CPT4KGS=0:TTLULD=0:TTLULDW=0:CPT1ULD=0:CPT2ULD=0:CPT3ULD=0:SL1CPT=0:SL2CPT=0:RETURN
*/ });
