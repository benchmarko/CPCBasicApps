/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem endofmon - End of Month (Monatsende)
2 rem (c) Marco Vieth, 198x
3 rem
10 REM MONATSENDE v2.0
20 MODE 2
30 PRINT TAB(14);"M   O   N   A   T   S   E   N   D   E  :":PRINT:PRINT:PRINT
40 PRINT"DIESES PROGRAMM HILFT BEIM AUSWECHNEN DER STRICHE AM ENDE DES":PRINT:PRINT
50 PRINT"MONATS. NACHDEM MAN DIE PLUSSE,HALBE STRICHE,GANZE STRICHE,":PRINT:PRINT
60 PRINT"PUNKTE,DOPPELPUNKTE,SUPERPUNKTE UND MINUSSE EINGEGEBEN HAT,":PRINT:PRINT
70 PRINT"ERZEUGT DER COMPUTER EINE LISTE,AUF DER ALLES UEBERSICHTLICH":PRINT:PRINT
80 PRINT"STEHT. AUSSERDEM WURDEN DIE ARBEITEN ZUSAMMENGEZAEHLT UND ALS":PRINT:PRINT
90 PRINT"GESAMTSTRICHE AUFGEFUEHRT. DER COMPUTER HAT AUCH AUSGERECHNET,":PRINT:PRINT
100 PRINT"WAS MAN BEZAHLEN MUSS BZW. BEKOMMT."
110 LOCATE 12,25:PRINT"(BITTE DRUECKEN SIE EINE TASTE)!"
120 CALL &BB18
200 REM   ***   HAUPTPROGRAMM   ***
210 MODE 1:CLEAR
212 PRINT:PRINT:PRINT"WIEVIELE MITGLIEDER ?":INPUT MTG
214 IF MTG>15 OR MTG<1 THEN 210 
216 FOR SCHL=1 TO MTG
220 PRINT:PRINT:PRINT"NAME DES ";SCHL;". MITGLIEDES :":INPUT N$  
225 RESTORE:FS=0:GS=0:FS=0:BEZ=0:BEK=0:BS=0
230 CLS
240 WINDOW #1,1,13,1,12
250 WINDOW #2,14,27,1,12
260 WINDOW #3,28,40,1,12 
270 WINDOW #4,1,13,13,25
280 WINDOW #5,14,27,13,25   
290 WINDOW #6,28,40,13,25    
300 FOR Z=1 TO 6
310 READ AR$,U
320 PRINT#Z,"WIEVIEL ";AR$;" HAT "
330 PRINT#Z,N$;" ERREICHT ??"
340 INPUT #Z,R(Z)
350 GS=GS+U*R(Z)
360 NEXT Z
370 FOR Z=1 TO 1
380 CLS#Z
390 READ AR$,U     
400 PRINT#Z,"WIEVIEL ";AR$;" HAT " 
410 PRINT#Z,N$;" ERREICHT ??"         
420 INPUT #Z,R(6+Z)
430 NEXT Z
440 FS=GS-60:REM AUSRECHNUNG DER FEHLENDEN STRICHE
450 FOR Z=1 TO 11
460 READ SS,BS:REM LESEN VON STRICHANZAHL UND UMRECHNUNGSFAKTOR
470 IF GS>=SS THEN 500
480 NEXT Z
490 PRINT"FALSCHE EINGABE ! ! !":STOP
500 H1(SCHL)=CINT((FS+60)*1.625) 
505 IF FS<0 THEN FS=ABS(FS):BEZ=FS*BS:GOTO 590 
510 BEZ=0:BEK=BS
590 REM
600 MODE 1:PRINT TAB(12);N$
610 LOCATE 5,2:PRINT STRING$(30,"*"):PRINT
620 PRINT TAB(5);"ERGEBNISSE DER ANALYSE :":PRINT
630 LOCATE 5,6:PRINT STRING$(30,"*"):PRINT
640 PRINT SPACE$(5);"PLUSSE        :";R(1)
650 PRINT SPACE$(5);"HALBE STRICHE :"R(2)
660 PRINT SPACE$(5);"GANZE STRICHE :"R(3)
670 PRINT SPACE$(5);"PUNKTE        :";R(4)
680 PRINT SPACE$(5);"DOPPELPUNKTE  :";R(5)
690 PRINT SPACE$(5);"SUPERPUNKTE   :";R(6)
700 PRINT SPACE$(5);"MINUSSE       :";R(7):PRINT
710 PRINT TAB(5);STRING$(30,"*"):PRINT   
720 PRINT SPACE$(11);"ERGEBNISSE :":PRINT
730 PRINT SPACE$(5);"GESAMT STRICHE:";GS
740 PRINT SPACE$(5);"FEHL/UEBER STR:";FS
750 PRINT SPACE$(5);"BEZAHLEN      :";BEZ;" DM"
760 PRINT SPACE$(5);"BEKOMMEN      :";BEK;" DM"
770 IF BEZ <>0 THEN PRINT SPACE$(5);"(UMRECHNUNG   :";BS;" )"
780 LOCATE 5,25
790 PRINT STRING$(30,"*")
800 CALL &BB18
805 MODE 1
810 NEXT SCHL
820 GOTO 5000
1000 REM     *****     D A T E N     *****
1010 DATA PLUSSE,0.25 , HALBE STRICHE,0.5 , GANZE STRICHE,1
1020 DATA PUNKTE,2 , DOPPELPUNKTE,4 , SUPERPUNKTE,8
1030 DATA MINUSSE,-1
1100 DATA 100,3
1110 DATA 90,2.50
1120 DATA 85,1.50
1130 DATA 75,1
1140 DATA 70,0.50
1150 DATA 65,0.25
1160 DATA 60,0.00
1170 DATA 48,0.08
1180 DATA 36,0.15
1190 DATA 24,0.22
1200 DATA 12,0.30
1210 DATA 0,0.50
5000  REM BALKENDIAGRAMM
5020 MODE 0:BORDER 4:INK 0,4:INK 1,26:LOCATE 4,12:PRINT "BALKENDIAGRAMM":t=TIME
5030 IF TIME-t<500 THEN 5030   
5040 ANZ=MTG : ZAHL=0:REM ZAEHLER DER BALKEN
5050 SYMBOL AFTER 200:SYMBOL 255,58,68,138,146,162,68,184,0
5060 STP=CINT((525+ANZ)/ANZ)  :REM SCHRITTWEITE
5070 DEG:t60=TAN(60):s30=SIN(30):c30=COS(30)  
5080 MODE 1:BORDER 13:INK 0,13:INK 1,17:INK 2,8:INK 3,4:TAG
5090 WINDOW #1,1,20,1,1 
5100 OP=16-ANZ+(ANZ/2):RES=OP MOD 2:IF RES <>0 THEN OP=OP-RES 
5110 RES=0:wdth=CINT(OP*3) 
5120 dpth=CINT(wdth*(SIN(35.5))):off=202-INT(dpth*s30/2)   
5130 MOVE 62,400:DRAW 62,0,3
5140 FOR y=360 TO 30 STEP -40
5150  MOVE 58,y:DRAW 66,y:MOVE 0,y+6
5160  PRINT USING "###";ABS(y-200)*0.625;
5170 NEXT y
5180 MOVE 58,200:DRAW 640,200
5190 FOR xst=75 TO 600 STEP stp
5195 ZAHL=ZAHL+1
5200 H1=H1(ZAHL)
5210 IF h1<=0 THEN 5320
5220 hg1=hg1+CINT(H1/1.625)
5230 FOR y=0 TO h1 STEP 2
5240  MOVE xst,off+y
5250  DRAWR wdth,0,1  
5260  DRAWR dpth*c30,dpth*s30,2   
5270 NEXT y
5280 y=y-2:GOSUB 5430
5290 NEXT xst
5300 PLOT 640,0,3:MOVE 380,42:TAGOFF:PRINT CHR$(23);CHR$(3);:TAG:MOVE 412,22:PRINT"Monatsstriche";:TAGOFF:PRINT CHR$(23);CHR$(0);
5302 q1$=RIGHT$(STR$(hg1),LEN(STR$(hg1))-1) 
5303 q2$=RIGHT$(STR$(hg2),LEN(STR$(hg2))-1) 
5304 q3$=RIGHT$(STR$(hg1+hg2),LEN(STR$(hg1+hg2))-1) 
5305 hg1$="+"+q1$+"":hg2$="-"+q2$+"":hg3$=""+CHR$(255)+Q3$+""
5310 INK 1,20:INK 0,1:BORDER 1:LOCATE 1,1:PRINT hg1$;:LOCATE 1,25:PRINT hg2$;:LOCATE 1,13:PRINT hg3$;:GOTO 5410  
5320 y=-2:GOSUB 5430
5330 hg2=hg2+h1 
5340 FOR y=-2 TO h1 STEP -2
5350 MOVE xst,off+y 
5360  DRAWR wdth,0,1 
5370  DRAWR dpth*C30,dpth*S30,2
5380 NEXT y
5390 MOVE xst,200:DRAW 640,200,3
5400 GOTO 5290
5410 T=TIME
5420 IF TIME-T<700 THEN 5420 ELSE 5480 
5430 FOR y1=y+2 TO y+dpth*s30 STEP 2  
5440  MOVE xst+(y1-y)*T60,off+y1    
5450  DRAWR wdth,0,3    
5460 NEXT y1   
5470 RETURN
5480 CALL &BB18
5490 RUN
*/ });
