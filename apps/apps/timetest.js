/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem timetest - Time Tester
2 rem (c) K.H. Denham, 1986
3 rem https://www.cpcwiki.eu/index.php/CWTA_Issue_14_-_February_1986_-_Type-Ins
4 rem
5 rem Modifications: delays; converted ASM to BASIC; accept Y or J for Yes
6 rem
10 REM TIMETESTER by K.H.Denham
20 REM (c) Computing with the Amstrad
30 REM
500 MODE 1:'IF HIMEM<>39999 THEN SYMBOL AFTER 240:MEMORY 39999
505 ch=65:SYMBOL AFTER ch:hm=HIMEM+(256-ch)*8
1000 REM initialise,set up m/code,etc
1010 SPEED INK 30,20:DIM TIM(3,3),SHAPE(36,8),MAZE(1),HI(5),NAME$(5),GRADE$(5):RESTORE 1030:FOR x=1 TO 36:FOR y=1 TO 8:READ shape(x,y):NEXT:NEXT:WINDOW#1,24,39,4,23
1020 'FOR x=&9C40 TO &9CCF:READ a$:POKE x,VAL("&"+a$):NEXT
1030 DATA 24,17,3,6,-6,-1,-21,-22
1040 DATA 29,4,4,4,-5,2,-28,-10
1050 DATA 29,-4,4,-4,-5,-2,-28,10
1060 DATA 24,-17,3,-6,-6,1,-21,22
1070 DATA 11,-28,-1,-6,-5,4,-5,30
1080 DATA -11,-28,1,-6,5,4,5,30
1090 DATA -24,-17,-3,-6,6,1,21,22
1100 DATA -29,-4,-4,-4,5,-2,28,10
1110 DATA -29,4,-4,4,5,2,28,-10
1120 DATA -24,17,-3,6,6,-1,21,-22
1130 DATA -11,28,1,6,5,-4,5,-30
1140 DATA 11,28,-1,6,-5,-4,-5,-30
1150 DATA 19,23,0,6,-5,-2,-14,-27
1160 DATA 28,11,3,5,-5,0,-26,-16
1170 DATA 30,3,5,-3,-5,-3,-30,3
1180 DATA 28,-11,3,-5,-5,0,-26,16
1190 DATA 19,-23,0,-6,-5,2,-14,27
1200 DATA -3,-30,3,-5,3,5,-3,30
1210 DATA -19,-23,0,-6,5,2,14,27
1220 DATA -28,-11,-3,-5,5,0,26,16
1230 DATA -30,-3,-5,3,5,3,30,-3
1240 DATA -28,11,-3,5,5,0,26,-16
1250 DATA -19,23,0,6,5,-2,14,-27
1260 DATA -3,30,3,5,3,-5,-3,-30
1270 DATA 22,27,3,10,-8,-8,-17,-29
1280 DATA 32,13,8,7,-11,-2,-29,-18
1290 DATA 35,3,10,-3,-10,-3,-35,3
1300 DATA 32,-13,8,-7,-11,2,-29,18
1310 DATA 22,-27,3,-10,-8,8,-17,29
1320 DATA 3,-35,-3,-10,-3,10,3,35
1330 DATA -22,-27,-3,-10,8,8,17,29
1340 DATA -32,-13,-8,-7,11,2,29,18
1350 DATA -35,-3,-10,3,10,3,35,-3
1360 DATA -32,13,-8,7,11,-2,29,-18
1370 DATA -22,27,-3,10,8,-8,17,-29
1380 DATA -3,35,3,10,3,-10,-3,-35
1390 DATA 3e,96,cd,5a,bb,06,15,cd,89,9c,3e,9e,cd,5a,bb,06,10,cd,89,9c,3e,9c,cd,5a,bb,06,16,16,01,14,3e,01,cd,91,9c,3e,17,cd,91,9c,3e,28,cd,91,9c,10,ee,3e,93,cd,5a,bb,06,15,cd,89,9c
1400 DATA 3e,9b,cd,5a,bb,06,10,cd,89,9c,3e,99,cd,5a,bb,c9,3e,9a,cd,5a,bb,10,fb,c9,cd,6f,bb,7a,cd,72,bb,3e,95,cd,5a,bb,c9,cd,06,b9,f5,3e,00,cd,a5,bb,dd,21,32,90,06,08
1410 DATA 7e,dd,77,00,dd,77,01,23,dd,23,dd,23,10,f2,f1,cd,0c,b9,3e,fe,21,32,90,cd,a8,bb,3e,ff,21,3a,90,cd,a8,bb,c9
2000 REM do you want instructions?
2010 INK 0,1:INK 1,26:BORDER 1:PAPER 0:PEN 1:CLS
2020 ORIGIN 0,0:PLOT 30,397,1:DRAWR 592,0:DRAWR 0,-340:DRAWR -592,0:DRAWR 0,340
2030 LI=6:GOSUB 15000:LOCATE 6,11:PRINT"Do you want instructions? (Y/N)"
2040 'call &bd19:IF INKEY(43)<>-1 or inkey(71)<>-1 THEN 2050 ELSE IF INKEY(46)<>-1 THEN 3000 ELSE 2040
2045 t$=inkey$:if t$="" then 2045 else t$=upper$(t$):if t$="Y" or t$="J" then 2050 else if t$="N" then 3000 else 2045
2050 CLS:ZONE 17:WINDOW 1,40,4,25:PRINT#2,"     CPC TIMETESTER - INSTRUCTIONS";TAB(6) STRING$(29,"=")
2060 PRINT"  Timetester is a program to teach","  clock reading. It draws a clock face","  and depending on the level chosen"
2070 PRINT"  asks for the time in different ways:",,"  LEVEL 1 - You choose from four";TAB(13) "answers in the form TEN";TAB(13) "PAST FIVE, QUARTER TO";TAB(13) "SIX, etc."
2080 PRINT,"  LEVEL 2 - You choose from four";TAB(13)"answers in the form 3.05,";TAB(13) "10.20, etc."
2090 PRINT,"  LEVEL 3 - You are asked to type in";TAB(13)"the time in the form 3.05,";TAB(13)"10.20, etc without seeing ";TAB(13)"any answers displayed"
2100 PRINT ,,,TAB(8)"PRESS ENTER TO CONTINUE"
2110 'call &bd19:IF INKEY(18)=-1 AND INKEY(6)=-1 THEN 2110
2115 t$=inkey$:if t$="" then 2115
2120 FOR X=1 TO 21:LOCATE 1,22:PRINT CHR$(10):NEXT
2130 LOCATE 3,1:PRINT"After twelve clock faces have been","  shown you are given your score and","  rating. These depend on the level and","  your speed and accuracy. Only the","  time spent between the answer being","  asked for and your answer being"
2140 PRINT"  entered is counted, so spend as long","  as you like looking at the clock and","  answers at other times, especially","  any you have got wrong! A table of"
2150 PRINT"  the five highest scores is provided.",,,"  When selecting the level of play you","  are also asked whether you require","  the optional hidden-maze game feature."
2160 LOCATE 8,19:PRINT"PRESS ENTER TO CONTINUE"
2170 'call &bd19:IF INKEY(18)=-1 AND INKEY(6)=-1 THEN 2170
2175 t$=inkey$:if t$="" then 2175
2180 FOR X=1 TO 19:LOCATE 1,22:PRINT CHR$(10):NEXT
2190 LOCATE 3,2:PRINT"If you choose this option then each","  time during the game that you get"
2200 PRINT"  six answers right in a row you play a","  hidden maze game.",,,"  You and your opponent (controlled by","  the computer) start at the opposite","  sides of an invisible maze in which","  the paths can only be found by trial"
2210 PRINT"  and error. You use the cursor keys or","  a joystick to guide your man out of","  the maze in as short a time as","  possible and before you get caught."
2220 LOCATE 8,18:PRINT"PRESS ENTER TO CONTINUE"
2230 'call &bd19:IF INKEY(18)=-1 AND INKEY(6)=-1 THEN 2230
2235 t$=inkey$:if t$="" then 2235
2240 FOR X=1 TO 25:LOCATE#2,1,25:PRINT#2,CHR$(10):FOR y=1 TO 40/10:call &bd19:NEXT:NEXT
2250 WINDOW 1,40,1,25
3000 REM new game
3010 BORDER 5:INK 0,26:INK 2,5:PAPER 2:CLS:INK 1,0
3020 ORIGIN 0,0:PLOT 30,397,1:DRAWR 592,0:DRAWR 0,-340:DRAWR -592,0:DRAWR 0,340
3030 PLOT 50,377,1:DRAWR 552,0:DRAWR 0,-300:DRAWR -552,0:DRAWR 0,300
3040 LI=6:PEN 1:PAPER 0:GOSUB 15000:LOCATE 12,12:PRINT"What Level (1-3) ?":PLOT -32,-81:DRAWR 0,16:DRAWR 288,0
3050 IF INKEY$<>"" THEN 3050
3060 K$=INKEY$:IF K$<"1" OR k$>"3" THEN 3060 ELSE level=VAL(k$):RANDOMIZE TIME:score=0:goes=0
3070 LOCATE 7,12:PRINT"With maze game feature? (Y/N)":PLOT -112,-81:DRAWR 0,16:DRAWR 464,0
3080 'call &bd19:IF INKEY(43)<>-1 or inkey(71)<>-1 THEN game=1 ELSE IF INKEY(46)<>-1  THEN game=0 ELSE 3080
3085 t$=inkey$:if t$="" then 3085 else t$=upper$(t$):if t$="Y" or t$="J" then game=1 else if t$="N" then game=0 else 3085
4000 REM draw screen
4010 ORIGIN 184,208:inrow=0:hand1=0:hand2=0:BORDER 20:INK 0,20:INK 1,1:PAPER 0:PEN 1:CLS:CLG
4020 gosub 18000: 'CALL 40000
4025 LOCATE 3,2:PRINT"TIMETESTER LEVEL";LEVEL:LOCATE 25,2:PRINT"CORRECT :":IF game=1 THEN LOCATE 25,3:PRINT"IN A ROW:"
4030 GOSUB 14000:kd=1:DEG:PLOT 0,0,1:RESTORE 4100:GOSUB 4070:TAG
4040 RESTORE 4050:FOR x=1 TO 12:READ a,b:PLOT a,b:PRINT MID$(STR$(x),2);:NEXT
4050 DATA 74,120,118,68,132,4,118,-58,74,-110,-6,-132,-92,-110,-136,-58,-150,4,-144,68,-98,120,-16,142
4060 TAGOFF:RESTORE 4120:GOSUB 4070:GOTO 4090
4070 FOR i%=1 TO 45 STEP kd:READ a%,b%
4080 PLOT a%,b%:PLOT -a%,b%:PLOT -a%,-b%:PLOT a%,-b%:PLOT b%,a%:PLOT -b%,a%:PLOT -b%,-a%:PLOT b%,-a%:NEXT:RETURN
4090 REM 
4100 DATA 120,2,120,4,120,6,120,8,120,10,119,13,119,15,119,17,119,19,118,21,118,23,117,25,117,27,116,29,116,31,115,33,115,35,114,37,113,39,113,41,112,43,111,45,110,47,110,49,109,51
4110 DATA 108,53,107,54,106,56,105,58,104,60,103,62,102,64,101,65,99,67,98,69,97,71,96,72,95,74,93,76,92,77,91,79,89,80,88,82,86,83,85,85
4120 DATA 110,2,110,4,110,6,110,8,110,10,109,11,109,13,109,15,109,17,108,19,108,21,108,23,107,25,107,27,106,28,106,30,105,32,105,34,104,36,103,38,103,39,102,41,101,43,100,45,100,46,99,48  
4130 DATA 98,50,97,52,96,53,95,55,94,57,93,58,92,60,91,62,90,63,89,65,88,66,87,68,85,69,84,71,83,72,82,74,80,75,79,76,78,78 
4140 DATA 15,0,15,2,15,3,14,5,14,6,13,8,12,9,11,10
5000 REM next question
5010 INK 2,6:BORDER 20:CLS#1:hour=INT(RND*12)+1:MINUTE=(INT(RND*12))*5
5020 REM plot hands
5030 IF hand1=0 THEN 5040 ELSE PLOT 0,0,0:hand=hand1:GOSUB 5090:hand=hand2:GOSUB 5090:REM unplot old hands
5040 hand=hour+24:IF MINUTE>45 THEN hand=hand+1
5050 IF hand>36 THEN hand=hand-12
5060 IF MINUTE>10 AND MINUTE<50 THEN hand=hand-12
5070 hand1=hand:PLOT 0,0,2:GOSUB 5090:IF minute=0 THEN hand=48 ELSE hand=minute/5+36
5080 hand2=hand:GOSUB 5090:KD=6:RESTORE 4140:GOSUB 4070:GOTO 5100
5090 PLOT 0,0:FOR x=1 TO 7 STEP 2:a=shape(hand-12,x)*2.25:b=shape(hand-12,x+1)*2.25:DRAWR a,b:NEXT:RETURN
5100 IF level=3 THEN m=1:hou=hour:mi=minute:GOTO 8000
6000 REM choose alternatives,etc
6010 m=0:mo=0:LOCATE 25,5:PRINT"IS THE TIME:":cor=INT(RND*4):ERASE TIM:DIM TIM(3,3):tim(cor,2)=hour:tim(cor,3)=MINUTE
6020 IF RND>0.5 THEN hou2=hour+1 ELSE hou2=hour-1
6030 IF RND>0.5 THEN minute2=minute+5 ELSE minute2=minute-5
6040 FOR x=0 TO 3
6050 IF x=cor THEN hou=hour:mi=minute:GOTO 6120
6060 IF RND>0.5 THEN hou=hou2 ELSE hou=hour
6070 IF RND>0.5 THEN mi=minute2 ELSE mi=minute
6080 IF hou>12 THEN hou=hou-12 ELSE IF hou<1 THEN hou=hou+12
6090 IF mi<0 THEN mi=mi+60 ELSE IF mi>55 THEN mi=mi-60
6100 tim(x,2)=hou:tim(x,3)=mi
6110 FOR a=0 TO 3:IF tim(a,2)=tim(x,2) AND tim(a,3)=tim(x,3) AND a<>x THEN 6050 ELSE NEXT
6120 z=x*2+7:LOCATE 24,z:PRINT x+1;CHR$(8);") ";
6130 IF level=1 THEN tim(x,1)=1:GOSUB 12000 ELSE GOSUB 12500
6140 NEXT:LOCATE 25,15:PRINT"WHICH? (1-4)":start=TIME
6150 IF INKEY$<>"" THEN 6150
6160 k$=INKEY$:IF k$<"1" OR k$>"4" THEN 6160 ELSE k=VAL(k$)-1:answ=TIME:INK 2,18,23:INK 3,6,20
7000 IF k=cor THEN 7020 ELSE PAPER 3:GOSUB 7080
7010 inrow=0:BORDER 6:RESTORE 13100:GOSUB 13300:PAPER 0:LOCATE 25,15:PRINT"THAT IS WRONG!":GOTO 7030
7020 score=score+1:inrow=inrow+1:BORDER 18:LOCATE 25,15:PRINT"THAT IS RIGHT!":RESTORE 13000:GOSUB 13300
7030 k=cor:IF m=0 THEN PAPER 2:GOSUB 7080
7040 totim=totim+answ-start:goes=goes+1:mo=3::z=18:LOCATE 25,17:PRINT"THE ANSWER IS:":GOSUB 12500:z=19:GOSUB 12000
7050 GOSUB 14000:LOCATE 26,22:PRINT"ANY KEY FOR":LOCATE 29,23:IF inrow=6 AND game=1 THEN PEN 3:PRINT"GAME":GOTO 7070:ELSE IF goes=12 THEN 9000:ELSE LOCATE 25,23:PRINT"NEXT QUESTION"
7060 CALL &BB18:GOTO 5000
7070 CALL &BB18:GOTO 11000
7080 z=k*2+7:hou=tim(k,2):mi=tim(k,3):IF TIM(k,1)=1 THEN GOSUB 12000 ELSE GOSUB 12500
7090 PAPER 0:RETURN
8000 REM input answer
8010 WHILE INKEY$<>"":r$=INKEY$:WEND:LOCATE 25,5:PRINT"TELL THE TIME":start=TIME:GOTO 8050
8020 LOCATE 25,7:PRINT CHR$(7)+"That is not a  ":LOCATE 25,8:PRINT"real time      ":GOTO 8040
8030 LOCATE 25,7:PRINT CHR$(7)+"Do not use the ":LOCATE 25,8:PRINT"24 hour clock  "
8040 FOR r=1 TO 2500:NEXT
8050 LOCATE 25,7:PRINT"Type in 3.20,  ":LOCATE 25,8:PRINT"8.25,10.00,etc":LOCATE 25,10:PRINT"          ":LOCATE 25,10:INPUT z$:z=LEN(z$):IF z>5 OR z<4 THEN 8020
8060 IF MID$(z$,z-2,1)<>"." THEN 8020
8070 FOR x=1 TO z:IF MID$(z$,x,1)<"." OR MID$(z$,x,1)>"9" OR MID$(z$,x,1)="/" THEN 8020:NEXT
8080 hou1=VAL(LEFT$(z$,1+z-4)):min1=VAL(MID$(z$,z-1)):IF hou1>12 AND hou1<24 THEN 8030 ELSE IF hou1<1 OR hou1>23 OR min1>55 THEN 8020
8090 answ=TIME:IF hou1=hour AND min1=MINUTE THEN 7020 ELSE 7010
9000 REM score routine
9010 PRINT"SCORE":CALL &BB18
9020 INK 0,20:INK 1,26:INK 2,1:PAPER 2:CLS:BORDER 1:PAPER 0:PEN 2:INK 3,6,26
9030 ORIGIN 0,0:PLOT 30,397,1:DRAWR 592,0:DRAWR 0,-340:DRAWR -592,0:DRAWR 0,340 
9040 LI=3:GOSUB 15000:PAPER 1
9050 avge=30-((totim/300)/12-5):IF avge<0.5 THEN avge=0.5 ELSE IF avge>30 THEN avge=30
9060 score2=INT(avge*100*score*(((level-1)*0.25)+1))
9070 pl=448+(16*LEN(STR$(score2))):PLOT -143,-33,1:DRAWR pl,0:LOCATE 5,7:PRINT"Your time adjusted score was";STR$(score2)
9080 PLOT -111,-81,1:DRAWR 448,0:LOCATE 7,10:PRINT"You have earned the title of"
9090 IF score2>=50000 THEN z$="MASTER TIMETELLER" ELSE IF SCORE2<50000 AND SCORE2>=30000 THEN z$="EXPERT TIMETELLER" ELSE IF SCORE2<30000 AND SCORE2>=15000 THEN z$="TIMETELLER GRADE2" ELSE IF score2<15000 THEN z$="NOVICE TIMETELLER"
9100 RESTORE 13000:GOSUB 13300:PLOT -16,-145,3:DRAWR 0,16:DRAWR 272,0:PEN 1:PAPER 3:LOCATE 13,13:PRINT Z$:IF score2>=50000 THEN GOSUB 16000
9110 PEN 0:PAPER 2:IF SCORE2<=HI(5) THEN INK 3,6:GOTO 10100 ELSE LOCATE 10,20:PRINT"PRESS ENTER TO CONTINUE"
9120 call &bd19:IF INKEY(18)=-1 AND INKEY(6)=-1 THEN 9120
10000 REM HI-SCORE ROUTINE
10010 INK 3,15:INK 0,26:INK 1,24:WHILE INKEY$<>"":R$=INKEY$:WEND:WINDOW#4,4,38,6,20:PAPER#4,2:PEN#4,0:CLS#4:RESTORE 13000:GOSUB 13300:LOCATE#4,1,3
10020 IF SCORE2=54000 THEN PRINT#4," Congratulations! You've just made","    the maximum possible score":GOSUB 16000:GOTO 10040:ELSE IF score2>hi(1) THEN PRINT#4," Congratulations! You've just made","         a new hi-score!":GOTO 10040
10030 PRINT#4,"  Your score is being entered on","        the hi-score table!"
10040 LOCATE#4,1,7:PRINT#4," Input your name (up to 9 letters)"
10050 PEN#4,0:NAME$(0)="":LOCATE #4,15,10:PRINT#4,STRING$(9,"-"):PEN#4,3:LOCATE #4,13,10:INPUT#4,name$(0):IF LEN(name$(0))>9 OR name$(0)="" THEN LOCATE #4,13,10:PRINT#4,STRING$(15," "):GOTO 10050
10060 INK 0,25:INK 1,26:CLS#4:HI(5)=SCORE2:GRADE$(5)=Z$:NAME$(5)=NAME$(0):FOR X%=4 TO 1 STEP-1
10070 IF HI(X%)<SCORE2 THEN HI(X%+1)=HI(X%):NAME$(X%+1)=NAME$(X%):GRADE$(X%+1)=GRADE$(X%):HI(X%)=SCORE2:NAME$(X%)=NAME$(0):GRADE$(X%)=Z$
10080 NEXT:PEN#4,1:LOCATE#4,3,2:PRINT#4,"Name";TAB(13) "Score";TAB(19) "Grade"
10090 INK 3,24:FOR X%=1 TO 5:LOCATE#4,1,X%*2+2:PEN#4,1:PRINT#4,MID$(STR$(X%),2);:PEN#4,3:PRINT#4," ";NAME$(X%);TAB(12) HI(X%);TAB(19) GRADE$(X%):NEXT
10100 PEN#4,0:LOCATE#4,5,15:PRINT#4,"PRESS ENTER TO PLAY AGAIN"
10110 EVERY 40 GOSUB 10130
10120 call &bd19:IF INKEY(18)=-1 AND INKEY(6)=-1 THEN 10120 ELSE T=REMAIN(0):FOR X=1 TO 25:LOCATE 1,1:PRINT CHR$(11):FOR Y=1 TO 50/10:call &bd19:NEXT:NEXT:GOTO 3000
10130 R=R+1:IF R>26 THEN R=6 ELSE IF R=9 THEN R=10
10140 INK 0,R:RETURN
11000 REM maze game
11010 INK 0,26:INK 1,18:INK 2,0:INK 3,6:ZONE 15
11020  BORDER 26:PAPER 0:CLS:ERASE MAZE:DIM MAZE(19,19):PEN 2:LOCATE 25,4:PEN 2:PRINT"THE HIDDEN MAZE":LOCATE 25,5:PRINT"===============":WINDOW#6,25,40,8,25:FOR N=1 TO 100
11030 Y=INT(RND*19)+1:X=INT(RND*18)+2:IF Y=9 OR Y=10 AND X=18 OR X=19 THEN 11030 ELSE MAZE(Y,X)=1:NEXT
11040 LOCATE 3,2:PRINT CHR$(150);STRING$(19,(154));CHR$(156)
11050 FOR X=3 TO 21:LOCATE 3,X:PEN 2:PRINT CHR$(149);TAB(23) CHR$(149):LOCATE 4,X:PEN 1:PRINT STRING$(19,CHR$(229)):NEXT
11060 LOCATE 3,22:PEN 2:PRINT CHR$(147);STRING$(19,(154));CHR$(153)
11070 PEN#6,2:PRINT#6,"Use the cursor",,,,"keys or joystick",,,,"to guide your",,,,"man to safety."
11080 DATUM=INT(TIME/300)
11090 lose=0:Y=10:X=0:R=10:V=20:BORDER 18
11100 LOCATE x+3,y+2:PRINT CHR$(248):LOCATE v+3,r+2:PRINT CHR$(225):EVERY 55 GOSUB 11190
11110 call &bd19:FOR loop=1 TO 25:LOCATE 1,loop:PRINT"  ":NEXT
11120 Z=X:T=Y:IF INKEY(8)<>-1 OR INKEY(74)<>-1 THEN x=x-1 ELSE IF INKEY(1)<>-1 OR INKEY(75)<>-1 THEN x=x+1 ELSE IF INKEY(2)<>-1 OR INKEY(73)<>-1 THEN y=y+1 ELSE IF INKEY(0)<>-1 OR INKEY(72)<>-1 THEN y=y-1
11130 IF x<1 THEN x=1 ELSE IF x>19 AND y<>10 THEN x=19 ELSE IF y>19 THEN y=19 ELSE IF y<1 THEN y=1
11140 IF lose=1 OR  r=y AND v=x THEN 11270
11150 IF Y=10 AND X=20 THEN 11250
11160 IF MAZE(Y,X)=1 THEN X=Z:Y=T
11170 IF x<>z OR y<>t THEN SOUND 1,150,8
11180 LOCATE z+3,t+2:PRINT" ":LOCATE x+3,y+2:PEN 3:PRINT CHR$(248):GOTO 11110
11190 E=R:S=V:IF V<X THEN V=V+1 ELSE IF V>X THEN V=V-1
11200 IF R<Y THEN R=R+1 ELSE IF R>Y THEN R=R-1
11210 IF MAZE(R,V)=1 THEN R=E:V=S ELSE SOUND 2,478,10,5
11220 LOCATE s+3,e+2:PEN 2:PRINT" ":LOCATE v+3,r+2:PRINT CHR$(225)
11230 IF R=Y AND V=X THEN lose=1:RETURN:ELSE IF ABS(R-Y)<4 AND ABS(V-X)<4 THEN BORDER 6,26 ELSE IF ABS(R-Y)<7 AND ABS(V-X)<7 THEN BORDER 6 ELSE BORDER 18
11240 RETURN
11250 REM WIN
11260 GOSUB 11320:GOSUB 16000:LOCATE 22,12:PRINT" ";CHR$(248):BORDER 18:PRINT#6,"You escaped":GOTO 11290
11270 REM LOSE
11280 GOSUB 11320:GOSUB 13210:LOCATE v+3,r+2:PRINT CHR$(225):BORDER 6:PRINT#6,"You were caught":GOSUB 13210:GOTO 11290
11290 PRINT#6,,,,"in";datum1-datum;"seconds.",,,,,,,,"PRESS ENTER KEY"
11300 call &bd19:IF INKEY(18)=-1 AND INKEY(6)=-1 THEN 11300
11310 IF GOES=12 THEN 9020 ELSE 4000
11320 ELAP=REMAIN(0):elap=REMAIN(1):DATUM1=INT(TIME/300):CLS#6:PEN 3:INK 3,6,26:RETURN
12000 REM print five past three etc
12010 RESTORE 12080:FOR R=0 TO MI/5:READ A$:NEXT
12020 RESTORE 12090:FOR R=1 TO HOU:READ B$:NEXT:LOCATE 28-MO,Z   
12030 IF MI=0 THEN a$=b$:b$="O'CLOCK":GOTO 12060
12040 IF MI>30 THEN READ B$:C$="TO":ELSE C$="PAST"
12050 IF LEN(A$+C$)+1<12+MO THEN A$=A$+" "+C$ ELSE PRINT A$:LOCATE 28-MO,Z+1:PRINT C$;" ";B$:RETURN
12060 IF LEN(A$+B$)+1<12+MO THEN PRINT A$;" ";B$ ELSE PRINT A$:LOCATE 28-MO,Z+1:PRINT B$
12070 RETURN
12080 DATA "NUL","FIVE","TEN","QUARTER","TWENTY","TWENTY-FIVE","HALF","TWENTY-FIVE","TWENTY","QUARTER","TEN","FIVE"
12090 DATA "ONE","TWO","THREE","FOUR","FIVE","SIX","SEVEN","EIGHT","NINE","TEN","ELEVEN","TWELVE","ONE"
12500 Z$=MID$(STR$(HOU),2)+"."
12510 IF MI<10 THEN Z$=Z$+"0"
12520 Z$=Z$+MID$(STR$(MI),2):LOCATE 28-MO,z:PRINT Z$:RETURN
13000 DATA 8,10,150,140,130,120,110,100,90,80
13100 DATA 2,50,379,478
13200 DATA 16,5,110,120,130,140,150,160,170,180,190,200,210,220,230,240,250,260
13210 FOR x=1 TO 2:RESTORE 13200:GOSUB 13300:NEXT:RETURN
13300 READ no,dur:FOR l=1 TO no:READ n:SOUND 1,n,dur:NEXT:RETURN
13310 RETURN
14000 REM format/print scores
14010 LOCATE 35,2:PRINT MID$(STR$(score),2);"/";MID$(STR$(goes),2):IF game =1 THEN LOCATE 35,3:PRINT MID$(STR$(inrow),2)
14020 RETURN
15000 REM double height
15010 y=288+16*(6-li):ORIGIN 207,y:PLOT 0,0,0:RESTORE 15060:FOR z=1 TO 6:READ x,y:DRAWR x,y:NEXT
15020 LOCATE 14,li:z$="CPC TIMETESTER":FOR x=1 TO 14
15030 ch=ASC(MID$(z$,x)):gosub 18510: 'POKE 40099,ASC(MID$(z$,x)):CALL 40094
15040 PRINT CHR$(254);CHR$(8);CHR$(10);CHR$(255);CHR$(11);:NEXT
15050 RETURN
15060 DATA 0,32,226,0,0,-32,-228,0,0,33,229,0
16000 FOR x=1 TO 2:RESTORE 16030:FOR r=1 TO 34
16010 READ chan,no,dur:SOUND chan,no,dur
16020 NEXT:NEXT:RETURN
16030 DATA 49,478,25,42,379,25,28,239,25,1,0,1,49,379,25,42,506,25,28,301,25,1,0,1,17,379,15,10,506,15,1,0,1,17,478,15,10,379,15,1,0,1,17,379,15,10,506,15,1,0,1
16040 DATA 49,319,25,42,253,25,28,159,25,1,0,1,17,319,15,10,253,15,1,0,1,17,379,15,10,506,15,1,0,1,17,319,15,10,253,15,1,0,1,49,253,50,42,169,50,28,201,50,1,0,1
17980 '
17990 'ASM (&9c49-&9c9d) converted to BASIC: print frame
18000 w=5
18010 ?chr$(&96);string$(&15,&9a);chr$(&9e);string$(&10,&9a);chr$(&9c);
18020 window #w,1,1,2,23:?#w,string$(22,&95);
18030 window #w,&17,&17,2,23:?#w,string$(22,&95);
18040 window #w,&28,&28,2,23:?#w,string$(22,&95);
18050 locate 1,24
18060 ?chr$(&93);string$(&15,&9a);chr$(&9b);string$(&10,&9a);chr$(&99);
18070 return
18080 '
18490 'duplicate character matrix of ch to chr 254 and 255:
18510 ad1=hm-(256-ch)*8:'+(256-ch)*8
18520 ad2=hm-(256-254)*8 :'char 254, and 255 after that
18530 for i=0 to 7
18540 by=peek(ad1):ad1=ad1+1
18550 poke ad2,by:poke ad2+1,by:ad2=ad2+2
18560 next
18570 return
18580 '
19980 '
19990 'disassembly of &9c40-&9ccf
20000 'org #9c40 ;(40000)
20001 'ld a,#96
20002 'call #bb5a ;TXT OUTPUT
20003 'ld b,#15
20004 'call l9c89
20005 'ld a,#9e
20006 'call #bb5a
20007 'ld b,#10
20008 'call l9c89
20009 'ld a,#9c
20010 'call #bb5a
20011 'ld b,#16
20012 'ld d,#01
20013 '.l9c5d
20014 'inc d
20015 'ld a,#01
20016 'call l9c91
20017 'ld a,#17
20018 'call l9c91
20019 'ld a,#28
20020 'call l9c91
20021 'djnz l9c5d
20022 'ld a,#93
20023 'call #bb5a
20024 'ld b,#15
20025 'call l9c89
20026 'ld a,#9b
20027 'call #bb5a
20028 'ld b,#10
20029 'call l9c89
20030 'ld a,#99
20031 'call #bb5a
20032 'ret
20033 '.l9c89
20034 'ld a,#9a
20035 '.l9c8b
20036 'call #bb5a
20037 'djnz l9c8b
20038 'ret
20039 '.l9c91
20040 'call #bb6f ;TXT SET COLUMN
20041 'ld a,d
20042 'call #bb72 ;TXT SET ROW
20043 'ld a,#95
20044 'call #bb5a
20045 'ret
20046 'call #b906 ;KL L ROM ENABLE (&9c9e=40094)
20047 'push af
20048 'ld a,#00 ;(#00 at &9ca3=40099)
20049 'call #bba5
20050 'ld ix,#9032
20051 'ld b,#08
20052 '.l9cad
20053 'ld a,(hl)
20054 'ld (ix+#00),a
20055 'ld (ix+#01),a
20056 'inc hl
20057 'inc ix
20058 'inc ix
20059 'djnz l9cad
20060 'pop af
20061 'call #b90c ;KL L ROM RESTORE
20062 'ld a,#fe
20063 'ld hl,#9032
20064 'call #bba8 ;TXT SET MATRIX
20065 'ld a,#ff
20066 'ld hl,#903a
20067 'call #bba8 ;TXT SET MATRIX
20068 'ret
20069 '
*/ });
