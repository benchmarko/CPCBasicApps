/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
10 rem symbol - Define Symbol
20 rem
30 rem Modifications: delay in 1680
40 rem
1140 DEFINT a-z:CALL &BC00
1150 GOSUB 3410
1160 SYMBOL AFTER 256
1170 OPENOUT"###":MEMORY HIMEM-1:CLOSEOUT
1180 SYMBOL AFTER 0
1190 g.s0!=HIMEM+1:g.sn=&FFFF
1200 g.gf$=CHR$(34):g.p$(0)=CHR$(232):g.p$(1)=CHR$(233)
1210 g.iv$=CHR$(24):g.csd$=CHR$(20):g.clr$=CHR$(18)
1220 g.b$=CHR$(7):g.cb$=CHR$(8)
1230 ON ERROR GOTO 4370
1240 DIM g.s(255,8):FOR v=0 TO 255:g.s(v,0)=&FFFF:NEXT
1250 :
1260 GOSUB 3500
1270 GOSUB 3630
1280 GOSUB 3700
1290 :
1300 :
1310 :
1320 LOCATE 1,20:PRINT""
1330 LOCATE 1,20:PRINT"Welches Symbol (0-255) soll definiert werden?"
1340 LOCATE 1,22:PRINT"Momentan dargestelltes, nur <ENTER> eingeben."
1350 LOCATE 1,24:PRINT"Ihre Eingabe bitte: ";:GOSUB 4240
1360 IF z$=""AND g.sn<>&FFFF GOTO 1620
1370 IF z$=""THEN z$="0"
1380 IF LEFT$(z$,1)="0"THEN z$=MID$(z$,2):GOTO 1380
1390 IF LEN(z$)>3 THEN 1410
1400 g.sn=VAL(z$):IF g.sn<256 THEN 1460
1410 LOCATE 1,24:PRINT"Falsche Eingabe.   <ENTER> eingeben!"
1420 IF INKEY$<>""THEN 1420
1430 IF INKEY$<>CHR$(13)THEN 1430
1440 GOTO 1320
1450 :
1460 LOCATE 10,16:PRINT""CHR$(g.sn)
1470 LOCATE 16,16:PRINT"SYMBOL "USING"###&";g.sn;SPACE$(32)
1480 v!=g.s0!+(g.sn*8)
1490 FOR vz=0 TO 7
1500 vm=128:v=PEEK(v!+vz)
1510 FOR vs=0 TO 7
1520 h(vz,vs)=ABS((v AND vm)>0)
1530 LOCATE 16+vs,6+vz:PRINT g.p$(h(vz,vs))
1540 vm=vm/2
1550 NEXT
1560 NEXT
1570 GOSUB 4000
1580 GOSUB 4110
1590 :
1600 :
1610 :
1620 LOCATE 26,16:PRINT SPC(32)
1630 LOCATE 1,20:PRINT""
1640 GOSUB 3840
1650 s=16:z=6
1660 LOCATE s,z:PRINT""g.p$(h(z-6,s-16))"";
1670 IF INKEY$<>""THEN 1670
1680 while time<t!:call &bd19:wend:t!=time+50:j=JOY(0):IF j=0 AND INKEY$=""THEN 1680
1690 LOCATE s,z:PRINT g.p$(h(z-6,s-16));
1700 IF INKEY(58)=128 THEN 3270
1710 IF INKEY(61)=128 THEN 2420
1720 IF INKEY(69)=128 THEN 2360
1730 IF INKEY(46)=128 THEN 2460
1740 IF INKEY(36)=128 THEN 2180
1750 IF INKEY(16)=128 THEN 1850
1760 IF INKEY(9)=0 OR(j AND 16)THEN 2000
1770 IF INKEY(0)=0 OR(j AND 1)THEN 2090
1780 IF INKEY(2)=0 OR(j AND 2)THEN 2100
1790 IF INKEY(8)=0 OR(j AND 4)THEN 2110
1800 IF INKEY(1)=0 OR(j AND 8)THEN 2120
1810 GOTO 1660
1820 :
1830 :
1840 :
1850 IF clr THEN PRINT""
1860 LOCATE 7,10:PRINT"    "
1870 LOCATE 9,13:PRINT"  "
1880 LOCATE 10,16:PRINT" "
1890 IF clr THEN PRINT""
1900 FOR vz=0 TO 7
1910 FOR vs=0 TO 7
1920 h(vz,vs)=clr:LOCATE 16+vs,6+vz:PRINT g.p$(clr);
1930 NEXT
1940 NEXT
1950 clr=clr XOR 1
1960 GOTO 1650
1970 :
1980 :
1990 :
2000 h(z-6,s-16)=h(z-6,s-16)XOR 1
2010 vs=s-16:vz=16-((z-5)*2)
2020 ORIGIN 48,240:FOR v=0 TO 3:PLOT vs*4+v,vz,h(z-6,s-16):NEXT
2030 ORIGIN 64,192:PLOT vs*2,vz:PLOT vs*2+1,vz
2040 ORIGIN 72,144:PLOT vs,vz,h(z-6,s-16)
2050 GOTO 2130
2060 :
2070 :
2080 :
2090 z=z+(z>6):GOTO 2130
2100 z=z-(z<13):GOTO 2130
2110 s=s+(s>16):GOTO 2130
2120 s=s-(s<23):GOTO 2130
2130 FOR v=1 TO 200:IF JOY(0)THEN NEXT
2140 GOTO 1660
2150 :
2160 :
2170 :
2180 GOSUB 3770
2190 vs=28:vz=4
2200 FOR v=0 TO 255
2210 IF g.s(v,0)=&FFFF THEN 2230
2220 SYMBOL v,g.s(v,1),g.s(v,2),g.s(v,3),g.s(v,4),g.s(v,5),g.s(v,6),g.s(v,7),g.s(v,8)
2230 LOCATE vs,vz:PRINT CHR$(1)CHR$(v);
2240 vs=vs+2:IF vs>78 THEN vs=28:vz=vz+1
2250 NEXT
2260 SYMBOL AFTER 0
2270 LOCATE 1,20:PRINT""
2280 LOCATE 1,20:PRINT"Zum Weitermachen bitte <ENTER> eingeben."
2290 IF INKEY$<>""THEN 2290
2300 IF INKEY$<>CHR$(13)THEN 2300
2310 GOSUB 3770
2320 GOTO 1630
2330 :
2340 :
2360 g.s(g.sn,0)=&FFFF
2370 LOCATE 26,16:PRINT"    nicht definiert"SPC(13)
2380 v$="A":GOTO 2600
2390 :
2400 :
2410 :
2420 v$="D":GOTO 2500
2430 :
2440 :
2450 :
2460 v$="N"
2470 :
2480 :
2490 :
2500 g.s(g.sn,0)=g.sn:FOR v=1 TO 8:g.s(g.sn,v)=0:NEXT
2510 FOR vz=0 TO 7
2520 vm=128
2530 FOR vs=0 TO 7
2540 g.s(g.sn,vz+1)=g.s(g.sn,vz+1)+h(vz,vs)*vm
2550 vm=vm/2
2560 NEXT
2570 NEXT
2580 LOCATE 26,16:FOR v=1 TO 8:PRINT",&"HEX$(g.s(g.sn,v),2);:NEXT
2590 :
2600 GOSUB 3770
2610 IF v$="N"THEN 1320
2620 IF v$="A"THEN 1320
2630 :
2640 :
2650 :
2660 LOCATE 1,20:PRINT""
2670 LOCATE 1,20:PRINT"Bitte Dateinamen (d:filename.typ) eingeben, dann <ENTER>"
2680 LOCATE 1,22:PRINT"Zum Abbrechen nur <ENTER> eingeben."
2690 LOCATE 1,24:LINE INPUT"Ihre Eingabe bitte: ",dn$
2700 IF dn$=""THEN 1320
2710 v=INSTR(dn$,":"):IF v<>0 AND v<>2 THEN 2660
2720 d$="A":IF v THEN d$=LEFT$(dn$,1):dn$=MID$(dn$,3)
2730 v=INSTR(dn$,"."):IF v=1 OR v>9 THEN 2660
2740 n$=dn$:t$="":IF v THEN n$=LEFT$(dn$,v-1):t$=MID$(dn$,v+1)
2750 IF LEN(t$)>3 THEN 2660
2760 dn$=d$+":"+n$+"."+t$
2770 :
2780 :
2790 :
2800 LOCATE 1,20:PRINT""
2810 LOCATE 1,20:PRINT"Die Symbol-Definitionen werden jetzt als BASIC Zeilen abgespeichert. Sie koennen";
2820 LOCATE 1,21:PRINT"dann spaeter an das eigentliche Programm geMERGEd und mit GOSUB 60000 aufgerufen";
2830 LOCATE 1,22:PRINT"werden. Damit dies nicht zweimal geschieht, wird das Unterprogramm mit der Vari-";
2840 LOCATE 1,23:PRINT"ablen g.v% gesperrt. Diese Variable also nicht im Hauptprogramm benutzen!"
2850 LOCATE 1,25:PRINT"Bitte  <ENTER>  eingeben!";
2860 IF INKEY$<>""THEN 2860
2870 IF INKEY$<>CHR$(13)THEN 2870
2880 :
2890 LOCATE 1,20:PRINT""
2900 :
2910 zn!=60000:sw!=10
2920 :
2930 OPENOUT dn$
2940 bl$=STR$(zn!)+" IF g.v%=&ffff then return"
2950 PRINT#9,bl$:LOCATE 1,20:PRINT""bl$:zn!=zn!+sw!
2960 bl$=STR$(zn!)+" symbol after 256"
2970 PRINT#9,bl$:LOCATE 1,20:PRINT""bl$:zn!=zn!+sw!
2980 bl$=STR$(zn!)+" openout"+g.gf$+"$$$"+g.gf$+":memory himem-1:closeout"
2990 PRINT#9,bl$:LOCATE 1,20:PRINT""bl$:zn!=zn!+sw!
3000 FOR v=0 TO 255:IF g.s(v,0)=&FFFF THEN 3010 ELSE NEXT
3010 bl$=STR$(zn!)+" symbol after"+STR$(v)
3020 PRINT#9,bl$:LOCATE 1,20:PRINT""bl$:zn!=zn!+sw!
3030 FOR v=0 TO 255
3040 IF g.s(v,0)=&FFFF THEN 3110
3050 bl$=STR$(zn!)+" symbol "+DEC$(v,"###")
3060 FOR vz=1 TO 8
3070 v$=HEX$(g.s(v,vz)):IF LEN(v$)=1 THEN v$=v$+" "
3080 bl$=bl$+",&"+v$
3090 NEXT
3100 PRINT#9,bl$:LOCATE 1,20:PRINT""bl$:zn!=zn!+sw!
3110 NEXT
3120 bl$=STR$(zn!)+" g.v%=&ffff"
3130 PRINT#9,bl$:LOCATE 1,20:PRINT""bl$:zn!=zn!+sw!
3140 bl$=STR$(zn!)+" return"
3150 PRINT#9,bl$:LOCATE 1,20:PRINT""bl$:zn!=zn!+sw!
3160 CLOSEOUT
3170 :
3180 LOCATE 1,20:PRINT""
3190 LOCATE 1,20:PRINT"Die BASIC Zeilen sind jetzt abgespeichert."
3200 LOCATE 1,22:PRINT"Bitte  <ENTER>  eingeben."
3210 IF INKEY$<>""THEN 3210
3220 IF INKEY$<>CHR$(13)THEN 3220
3230 GOTO 1320
3240 :
3260 :
3270 GOSUB 3770
3280 LOCATE 1,20:PRINT""
3290 LOCATE 1,20:PRINT"Programm verlassen (J/N) ?"
3300 LOCATE 1,22:PRINT"Ihre Eingabe bitte: ";
3310 IF INKEY$<>""THEN 3310
3320 v$=UPPER$(INKEY$):IF v$=""OR v$<>"J"AND v$<>"N"THEN 3320
3330 PRINT v$:IF v$="N"THEN 1320
3340 :
3350 END
3360 :
3370 :
3380 :
3390 :
3400 :
3410 MODE 2:PRINT SPC(32)"SYMBOL.BAS   1.0"
3420 :
3430 LOCATE 1,2:PRINT STRING$(80,218)
3440 FOR v=3 TO 18:LOCATE 1,v:PRINT CHR$(219):LOCATE 80,v:PRINT CHR$(217):NEXT
3450 LOCATE 1,v:PRINT STRING$(80,216);
3460 RETURN
3470 :
3480 :
3490 :
3500 LOCATE 9,15:PRINT CHR$(150)CHR$(154)CHR$(156)
3510 LOCATE 9,16:PRINT CHR$(149)" "CHR$(149)
3520 LOCATE 9,17:PRINT CHR$(147)CHR$(154)CHR$(153)
3530 LOCATE 8,12:PRINT CHR$(150)STRING$(2,154)CHR$(156)
3540 LOCATE 8,13:PRINT CHR$(149)"  "CHR$(149)
3550 LOCATE 8,14:PRINT CHR$(147)STRING$(2,154)CHR$(153)
3560 LOCATE 6,9:PRINT CHR$(150)STRING$(4,154)CHR$(156)
3570 LOCATE 6,10:PRINT CHR$(149)"    "CHR$(149)
3580 LOCATE 6,11:PRINT CHR$(147)STRING$(4,154)CHR$(153)
3590 RETURN
3600 :
3610 :
3620 :
3630 LOCATE 15,15:PRINT CHR$(150)STRING$(42,154)CHR$(156)
3640 LOCATE 15,16:PRINT CHR$(149)SPC(42)CHR$(149)
3650 LOCATE 15,17:PRINT CHR$(147)STRING$(42,154)CHR$(153)
3660 RETURN
3670 :
3680 :
3690 :
3700 LOCATE 15,5:PRINT CHR$(150)STRING$(8,154)CHR$(156)
3710 FOR v=6 TO 13:LOCATE 15,v:PRINT CHR$(149)SPC(8)CHR$(149):NEXT
3720 LOCATE 15,v:PRINT CHR$(147)STRING$(8,154)CHR$(153)
3730 RETURN
3740 :
3750 :
3760 :
3770 FOR v=4 TO 14
3780 LOCATE 28,v:PRINT SPACE$(51)
3790 NEXT
3800 RETURN
3810 :
3820 :
3830 :
3840 LOCATE 30,6:PRINT"<JOY 0> oder <PFEILE>   Cursor bewegen"
3850 LOCATE 30,7:PRINT"<FIRE> oder <COPY>      Punkt an/aus"
3860 LOCATE 30,8:PRINT"<CTRL><CLR>             alle Punkte an/aus"
3870 LOCATE 30,9:PRINT"<CTRL><N>               neues Zeichen"
3880 LOCATE 30,10:PRINT"<CTRL><A>               abbrechen"
3890 LOCATE 30,11:PRINT"<CTRL><L>               Liste"
3900 LOCATE 30,12:PRINT"<CTRL><D>               Diskette"
3910 LOCATE 30,13:PRINT"<CTRL><E>               Ende"
3920 RETURN
3930 :
3940 :
3950 :
3960 FOR v=6 TO 13:LOCATE 16,v:PRINT SPACE$(8);:NEXT:RETURN
3970 :
3980 :
3990 :
4000 ORIGIN 64,192
4010 FOR vz=0 TO 7
4020 FOR vs=0 TO 7
4030 PLOT vs*2,14-vz*2,h(vz,vs)
4040 PLOTR 1,0
4050 NEXT
4060 NEXT
4070 RETURN
4080 :
4090 :
4100 :
4110 ORIGIN 48,240
4120 FOR vz=0 TO 7
4130 FOR vs=0 TO 7
4140 PLOT vs*4,14-vz*2,h(vz,vs)
4150 DRAWR 3,0
4160 NEXT
4170 NEXT
4180 RETURN
4190 :
4200 :
4210 :
4220 :
4230 :
4240 z$=""
4250 PRINT" ";
4260 v$=INKEY$:IF v$=""THEN 4260
4270 PRINT" ";
4280 IF v$=CHR$(13)THEN RETURN
4290 IF ASC(v$)=127 AND z$<>""THEN 4320
4300 IF v$>"/"AND v$<":"THEN 4330
4310 PRINT g.b$;:GOTO 4250
4320 z$=LEFT$(z$,LEN(z$)-1):PRINT" ";:GOTO 4250
4330 z$=z$+v$:PRINT v$;:GOTO 4250
4340 :
4350 :
4360 :
4370 PRINT"Error";ERR;"in";ERL
4380 END
*/ });
