/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 REM dihunt - Diamond Hunt
2 rem (c) Shaun Walton, 1992
3 rem Amstrad Action, 1992
4 rem https://www.cpc-power.com/index.php?page=detail&num=14345
5 rem Modifications: delay
6 rem
10 REM **** Diamond Hunt **** 
20 REM **** Shaun Walton 1992 **** 
30 ON BREAK GOSUB 320
40 DIM H(5),H$(5) 
50 FOR i=1 TO 5:H$(i)="Fred":H(i)=(6-i)*25:NEXT i
60 x=2:y=2:a=1:L1=3:T1=36:A1=0 
70 ENT 1,10,-50,1,10,50,1:ENT 2,10,-50,10,10,50,10:ENV 1,15,-1,10,15,1,10:ENV 2,4,2,5,4,-2,5,4,2,5,4,-2,5
80 MODE 1:INPUT"Which Level (1 - Hard,15 - Easy)";L0
90 IF L0<1 OR L0>15 THEN 80
100 t1=t1+INT(1+L0/5)
110 MODE 0
120 SPEED INK 1,1:BORDER 0:INK 0,0:INK 1,26:INK 2,6:INK 3,24:INK 5,5
130 FRAME
140 MOVE 12,4:DRAW 12,395:DRAW 627,395:DRAW 627,4:DRAW 12,4
150 GOSUB 410
160 AFTER 350*INT(2+L0/5),2 GOSUB 740
170 AFTER 500*INT(2+L0/5),3 GOSUB 760
180 EVERY 50,1 GOSUB 790
190 WHILE LOWER$(INKEY$)<>"q" AND A1<>1
200 CLEAR INPUT
210 IF INKEY(0)=0 OR JOY(0)=1 THEN LOCATE x,y:PRINT" ":y=y-1:GOSUB 350
220 IF INKEY(2)=0 OR JOY(0)=2 THEN LOCATE x,y:PRINT" ":y=y+1:GOSUB 350
230 IF INKEY(8)=0 OR JOY(0)=4 THEN LOCATE x,y:PRINT" ":x=x-1:GOSUB 350
240 IF INKEY(1)=0 OR JOY(0)=8 THEN LOCATE x,y:PRINT" ":x=x+1:GOSUB 350
250 IF b=0 THEN b=1 ELSE b=0
260 LOCATE x,y:PEN a:PRINT CHR$(250+b);
270 FOR e=1 TO (L0*4/10)+1:call &bd19:NEXT e
280 a=RND*15:IF a=0 THEN a=1
290 WEND
300 IF A1=1 THEN 320 ELSE F=REMAIN(1):F=REMAIN(2):F=REMAIN(3):FOR l=1 TO 15:PEN l:LOCATE 3,5:PRINT"You scored ";S0*L1;"!":NEXT l 
310 T0=S0*L1:GOSUB 870
320 MODE 1:INK 0,0:INK 1,26:PEN 2:LOCATE 14,1:PRINT"HIGH SCORES":PEN 1:FOR i=1 TO 5:LOCATE 2,(i+1)*2:PRINT i;" ";H$(i),H(i):NEXT i:PRINT:PRINT:PEN 3:INPUT"Another go (Y/N)";G$:PEN 1
330 IF LOWER$(LEFT$(G$,1))="y"THEN ERASE p,q,r,s:GOTO 60 ELSE IF LOWER$(LEFT$(G$,1))<>"n"THEN 320
340 GOSUB 900:MODE 2:END
350 IF x<2 THEN x=19
360 IF x>19 THEN x=2
370 IF y<2 THEN y=24
380 IF y>24 THEN y=2
390 LOCATE x,y:c$=COPYCHR$(#0):IF c$=CHR$(252)THEN GOSUB 570:GOTO 400 ELSE IF c$=CHR$(227) THEN GOSUB 590:GOTO 400
400 RETURN
410 DIM p(15),q(15),r(15),s(15):FOR z=1 TO 15
420 PEN 2
430 p(z)=INT(2+RND*17):q(z)=INT(2+RND*22):IF p(z)=2 AND q(z)=2 THEN 430
440 FOR w=1 TO 15:IF w=z THEN 460
450 IF p(z)=r(w)AND q(z)=s(w)THEN 430
460 NEXT w
470 LOCATE p(z),q(z):PRINT CHR$(252);
480 PEN 3
490 r(z)=INT(2+RND*17):s(z)=INT(2+RND*22):IF r(z)=2 AND s(z)=2 THEN 490
500 FOR w=1 TO 15:IF w=z THEN 520
510 IF r(w)=r(z)AND s(w)=s(z)THEN 490
520 NEXT w
530 LOCATE r(z),s(z):PRINT CHR$(227);
540 NEXT z
550 PEN 1
560 RETURN
570 L1=L1-1:GOSUB 700:GOSUB 620:IF L1=0 THEN F=REMAIN(1):F=REMAIN(2):F=REMAIN(3):FOR l=1 TO 15:LOCATE 5,5:PEN 1:PRINT"DEAD!":LOCATE 3,6:PRINT"You scored ";S0+T1;"!":NEXT l:T0=S0+T1:GOSUB 870:GOTO 320
580 RETURN
590 S0=S0+1:SOUND 1,500,10,10,,1:IF S0=15 THEN F=REMAIN(1):F=REMAIN(2):F=REMAIN(3):SOUND 1,350,250,10,1,2:FOR l=1 TO 15:LOCATE 5,5:PEN 1:PRINT"WELL DONE!":LOCATE 3,6:PRINT"You scored ";(S0+T1)*L1;"!":NEXT l:GOTO 600 ELSE 610
600 T0=(S0+T1)*L1:GOSUB 870:GOTO 320
610 RETURN
620 REM *** Screen Clear Routine by ???? ***
630 OUT &BC00,1:FOR q=40 TO 1 STEP-1
640 FOR t=1 TO 10:NEXT t
650 OUT &BD00,q:NEXT q
660 OUT &BC00,1:FOR q=1 TO 40
670 FOR t=1 TO 10:NEXT t
680 OUT &BD00,q:NEXT q
690 RETURN
700 REM *** Explosion Sound Routine by ???? ***
710 MEMORY 39999:c=40000:GOSUB 720:CALL c,6,30:CALL c,7,39:CALL c,8,16:CALL c,11,255:CALL c,12,30:CALL c,13,1:RETURN
720 RESTORE 720:FOR i =0 TO 9:READ v:POKE c+i,v:NEXT:RETURN:DATA&dd,&7e,&02,&dd,&4e
730 DATA&00,&cd,&34,&bd,&c9
740 SOUND 1,200,100,10,2
750 RETURN
760 SOUND 1,200,100,15,2,1:F=REMAIN(3)
770 F=REMAIN(1):F=REMAIN(2):FOR l=1 TO 15:LOCATE 5,5:PEN 1:PRINT"OUT OF TIME!":LOCATE 3,6:PRINT"You scored ";S0*L1;"!":NEXT l
780 T0=S0*L1:GOSUB 870:A1=1:RETURN
790 T1=T1-1:RETURN
800 MODE 1:INK 0,0:INK 1,26:PEN 1:FOR i=1 TO 10:FOR j=1 TO 3:LOCATE 2,5:PEN j:PRINT"NEW HIGH SCORE!!":NEXT j,i:PRINT:PRINT:PEN 1:INPUT"Enter your name(max 7 letters)";N$
810 IF LEN(N$)>8 THEN 800
820 FOR i=4 TO D STEP-1
830 H$(i+1)=H$(i):H(i+1)=H(i)
840 NEXT i
850 H$(D)=N$:H(D)=T0
860 RETURN
870 FOR i=1 TO 5:IF T0>H(i)THEN D=i:GOSUB 800:GOTO 880 ELSE NEXT i
880 T0=0:S0=0
890 RETURN
900 REM *** Fade Routine by ???? *** 
910 RESTORE 950:MEMORY 40199
920 FOR A0=&9D08 TO &9D19:READ V0$:POKE A0,VAL("&"+V0$):NEXT A0
930 CALL 40200
940 RETURN
950 DATA 1e,fe,21,00,c0
960 DATA 7b,a6,77,23,7d
970 DATA b4,20,f8,cb,13
980 DATA 38,f1,c9
*/ });
