/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem lenin2 - Leningrad (Program) - TODO 1 REM LENINGRAD COPYRIGHT BY SAKIS KAFFESAKIS 2022 - MUSIC BY MANOSSG
2 SYMBOL AFTER 256:MEMORY &6FFF 
3 SYMBOL AFTER n
5 GOSUB 3500
7 re=0:CP=0
8 SONG$="MUSIC1.AKG":GOSUB 9500:
9 REM GOTO 498
10 MODE 1:CLS:MODE 1:BORDER 11:PEN 3:INK 3,20:INK 0,0
11 LOCATE 7,2:PRINT"Welcome to..."
12 LOCATE 10,7:PEN 2:SPEED INK 40,40:INK 2,6,19:PRINT "BATTLE OF LENINGRAD!" 
13 PEN 1:INK 1,15:LOCATE 13,10:PRINT"1. START GAME":LOCATE 13,12:PRINT"2. INSTRUCTIONS/KEYS":LOCATE 13,14:PRINT"3. HISTORY":LOCATE 13,16:PRINT"4. CHECKPOINT":cr$=CHR$(164)
14 LOCATE 7,19:PEN 3:PRINT"by SAKIS KAFFESAKIS":LOCATE 27,19:PRINT cr$:LOCATE 29,19:PRINT"2022":LOCATE 7,21:PRINT"Music by MANOSSG":LOCATE 7,23:PRINT"Digitized images by BRUNDIJ":LOCATE 7,25:PRINT"www.amstradsakis.blogspot.com":MOVE 90,1:DRAW 565,1 
15 A$=INKEY$
16 IF A$="1" THEN GOTO 30
17 IF A$="2" THEN GOSUB 400:GOTO 10
18 IF A$="3" THEN GOSUB 200:GOTO 10
19 IF A$="4" THEN GOSUB 350:GOTO 10
20 GOTO 15
30 IF BEG=0 THEN beg=1:GOTO 498
31 IF BEG=1 THEN GOTO 499
199 END
200 REM history
210 MODE 2:INK 0,0:INK 1,26:PEN 1
220 LOCATE 37,1:PRINT"HISTORY"
230 LOCATE 1,3:PRINT"WINTER 1941-1942."
240 LOCATE 1,5:PRINT"LENINGRAD is besieged by the German troops."   
250 LOCATE 1,7:PRINT"The major problem is the transport of supplies for the people and the army."
260 LOCATE 1,9:PRINT"The only way for transporting supplies is over the iced lake LADOGA."
270 LOCATE 1,11:PRINT"You must help the general Zhukov in the battle but also in transporting suppliesto the city, which is starving. The food portions are very little."
280 LOCATE 1,14:PRINT"Otherwise, everyone will die due to starvation and extreme cold."
290 q$=INKEY$:y$=INKEY$:z$=INKEY$:w$=INKEY$
300 t$=INKEY$
310 IF t$="" THEN GOTO 300
320 RETURN
350 CLS:MODE 2:INK 0,0:INK 1,26:PEN 1
360 LOCATE 32,11:PRINT"PASSWORD?"
362 LOCATE 32,13:INPUT"",CPP$
365 IF CPP$<>"OFI" THEN GOSUB 9730:LOCATE 32,15:PRINT"WRONG PASSWORD!":FOR DL=1 TO 1000:NEXT DL:RETURN 
368 IF CPP$="OFI" THEN CP=1:GOSUB 9720:LOCATE 32,15:PRINT"CORRECT PASSWORD!":LOCATE 10,18:PRINT"You start from the point where you started the supply mission":GOSUB 490:RETURN
399 RETURN
400 REM INST
402 MODE 2:PEN 1:INK 1,26:INK 0,0:LOCATE 35,1:PRINT"INSTRUCTIONS"
405 LOCATE 1,3:PRINT"To play the game, use only the numbers (0-5)."
415 LOCATE 1,5:PRINT"Please have in mind that:" 
418 LOCATE 1,7:PRINT"When you take an object, the object you were already holding returns to its ini-tial place."
420 LOCATE 1,10:PRINT"It is important to hold a specific object when you make a specific choice."
422 LOCATE 1,12:PRINT"Some wrong choices may lead you to dead-end in next points."
430 LOCATE 1,15:PRINT"Good luck! We hope you enjoy the game!"
448 GOSUB 490
449 RETURN
490 GOSUB 999
491 a$=INKEY$
492 IF a$<>"" THEN RETURN
493 GOTO 491
497 END
498 PEN 1:REM MUS
499 GOSUB 9700:SONG$="MUSIC2.AKG":GOSUB 9500
500 REM START
505 OBJ$="NOTHING":KAM=0:TRA=0:ENI=0:ZOY=0:XAR=0:ROY=0:XR=0:BE=0:AT=0:GO=0:BEE=0
506 IF CP=1 THEN GOSUB 9700:SONG$="MUSIC3.AKG":GOSUB 9500:KAM=1:TRA=1:ENI=1:ZOY=1:XAR=1:GO=1:XR=1:Z$=INKEY$:U$=INKEY$:GOTO 7000
509 GOSUB 999
510 MODE 1: BORDER 11:INK 1,26:INK 0,0:PEN 1:INK 2,3:INK 3,9:REM ROOM 0
520 REM 
525 GOSUB 700
530 GOSUB 9020
535 LOCATE 2,15:PRINT"Actions available:"
536 LOCATE 2,17:PRINT"1. Go to the military base":LOCATE 2,19:PRINT"2. Go to the headquarters":LOCATE 2,21:PRINT"3. Go to the storage house":LOCATE 2,23:PRINT"4. Go to the trench"
541 LOCATE 5,5
550 R$=INKEY$
560 IF R$="1" THEN GOTO 1000
570 IF R$="2" THEN GOTO 2000
580 IF R$="3" THEN GOTO 3000
590 IF R$="4" THEN GOTO 4000
699 GOTO 550
700 REM road
701 MODE 1:PEN 1:INK 0,0:INK 1,26:INK 2,12:INK 3,10
702 GOSUB 9030
703 PLOT 20,190:DRAW 620,190:PLOT 20,240:DRAW 620,240:FOR j=0 TO 8:PLOT 30+70*j,215:DRAW 60+70*j,215:NEXT j
710 pr$=CHR$(233):pn$=pr$+" "+pr$:FOR j=0 TO 2:LOCATE 7,3+2*j:PRINT pn$:NEXT j 
715 PLOT 92,250:DRAW 92,380:DRAW 147,380:DRAW 147,250:DRAW 92,250:PLOT 115,250:DRAW 115,275:DRAW 125,275:DRAW 125,250
720 PLOT 226,250:DRAW 226,303:DRAW 236,303:DRAW 236,250:DRAW 226,250
725 PEN 3:y$=CHR$(143):LOCATE 13,6:PRINT CHR$(214)+y$+y$+y$+CHR$(215)
726 LOCATE 14,5:PRINT CHR$(214)+y$+CHR$(215):LOCATE 15,4:PRINT CHR$(254):PEN 1
740 FOR j=0 TO 1:LOCATE 35,5+2*j:PRINT pn$:NEXT j
741 PLOT 540,250:DRAW 540,339:DRAW 595,339:DRAW 595,250:DRAW 540,250:PLOT 563,250:DRAW 563,270:DRAW 573,270:DRAW 573,250
750 LOCATE 28,2:PRINT CHR$(165):PLOT 335,250:DRAW 480,250:DRAW 480,350:DRAW 335,350:DRAW 335,250:PLOT 397,250:DRAW 397,280:DRAW 417,280:DRAW 417,250:PLOT 335,350:DRAW 407,380:DRAW 480,350:
751 LOCATE 23,5:PRINT pr$+"  "+pr$+"  "+pr$:LOCATE 23,7:PRINT pr$+"  "+pr$+"  "+pr$
759 REM MOVE 50,260:FILL 2
760 MOVE 100,260:FILL 2:MOVE 120,260:FILL 3:MOVE 232,260:FILL 2:MOVE 350,260:FILL 2:MOVE 410,260:FILL 3:MOVE 410,370:FILL 3:MOVE 580,260:FILL 2:MOVE 545,260:FILL 2:MOVE 568,260:FILL 3
798 LOCATE 1,15
799 RETURN
899 END
999 W$=INKEY$:Y$=INKEY$:Z$=INKEY$:X$=INKEY$:RETURN
1000  REM ROOM 1
1010 MODE 1
1011 PEN 1:INK 0,0:INK 1,26:INK 2,13:INK 3,20:LOAD"1.scr":GOSUB 9030
1030 GOTO 1100
1089 GOSUB 1995
1100 LOCATE 2,16:PRINT"What will you do?":LOCATE 2,18:PRINT"1. Escape with the truck"
1101 LOCATE 2,19:PRINT"2. Search for enemies and attack"
1102 LOCATE 2,20:PRINT"3. Leave with the truck"
1103 IF KAM=0 THEN LOCATE 2,21:PRINT"4. Check the road for enemies"
1104 IF KAM=1 AND ENI=0 THEN LOCATE 2,21:PRINT"4. Call reinforcements and attack"
1105 IF KAM=1 AND ENI=1 THEN LOCATE 2,21:PRINT"4. Road clear!"
1107 LOCATE 2,22:PRINT"0. Go back to the main road"
1108 GOSUB 9020
1109 GOSUB 999
1110 T$=INKEY$
1115 IF T$="1" THEN GOSUB 9490:GOTO 1900
1120 IF T$="2" AND OBJ$="BAZOOKA" THEN GOSUB 9490:GOTO 1910
1125 IF T$="2" AND OBJ$="GRENADE" THEN GOSUB 9490:GOTO 1915
1130 IF T$="2" AND OBJ$="KALASHNIKOV" THEN GOSUB 9490:GOTO 1920
1135 IF T$="2" AND OBJ$<>"BAZOOKA" AND OBJ$<>"GRENADE" AND OBJ$<>"KALASHNIKOV" THEN GOSUB 9490:GOTO 1925
1140 IF T$="3" AND ZOY=0 THEN GOSUB 9730:MODE 2:GOSUB 1930:GOSUB 490:GOTO 1000
1145 IF T$="3" AND ZOY=1 AND ENI=0 THEN GOSUB 9490:GOTO 1935
1150 IF T$="3" AND ZOY=1 AND GO=0 AND ENI=1 THEN GO=1:GOSUB 9720:MODE 2:GOSUB 1940:SONG$="MUSIC3.AKG":GOSUB 9700:GOSUB 9500:GOSUB 490:GOTO 7000
1151 IF T$="3" AND ZOY=1 AND GO=1 AND ENI=1 THEN GOTO 7000
1152 IF T$="4" AND OBJ$<>"CAMOUFLAGE" AND KAM=0 THEN GOSUB 9490:GOTO 1945
1153 IF T$="4" AND OBJ$="CAMOUFLAGE" AND KAM=0 THEN KAM=1: GOSUB 9720:MODE 2:GOSUB 1975:GOSUB 490:GOTO 1000 
1154 IF T$="4" AND KAM=1 AND ENI=1 THEN GOSUB 9720:GOTO 1110
1156 IF T$="4" AND KAM=1 AND ENI=0 AND OBJ$<>"BAZOOKA" AND OBJ$<>"GRENADE" AND OBJ$<>"KALASHNIKOV" THEN GOSUB 9490:GOTO 1925
1157 IF T$="4" AND KAM=1 AND ENI=0 AND OBJ$="BAZOOKA" THEN GOSUB 9490:GOTO 1910
1158 IF T$="4" AND KAM=1 AND ENI=0 AND OBJ$="GRENADE" THEN GOSUB 9490:GOTO 1915
1159 IF T$="4" AND KAM=1 AND ENI=0 AND OBJ$="KALASHNIKOV" THEN ENI=1:GOSUB 9720:MODE 2:GOSUB 1950:GOSUB 490:GOTO 1000
1160 IF T$="0" THEN GOTO 509
1200 GOTO 1110
1900 REM
1901 LOCATE 1,15:PRINT"Are you leaving the battle, traitor? You get killed from Soviet and German fire"
1909 GOSUB 490:GOTO 30
1910 LOCATE 10,15:PRINT"Attacking with one rocket only? They are many and they kill you"
1914 GOSUB 490:GOTO 30
1915 LOCATE 7,15:PRINT"You found many enemies but you have only one grenade. You are dead" 
1919 GOSUB 490:GOTO 30
1920 LOCATE 4,15:PRINT"You killed many with the Kalashnikov but they are too many. You are killed"
1924 GOSUB 490:GOTO 30
1925 LOCATE 12,15:PRINT"Where are you going unarmed? You have an unglorious death"
1929 GOSUB 490:GOTO 30
1930 INK 1,26:LOCATE 25,15:PRINT"You don't have an order to leave!":RETURN
1935 LOCATE 4,15:PRINT"You fell into a trap some hundreds meters after you started. You are dead"
1939 GOSUB 490:GOTO 30
1940 INK 1,26:LOCATE 27,15:PRINT"You start the supply mission!":LOCATE 25,17:PRINT"Checkpoint reached! Password: OFI":RETURN
1945 LOCATE 7,15:PRINT"They saw you from far away and they killed you when you got closer"
1949 GOSUB 490:GOTO 30
1950 INK 1,26:LOCATE 15,15:PRINT"You cleaned the road from many enemies with heroism!":RETURN
1975 INK 1,26:LOCATE 1,15:PRINT"They are too many. You need reinforcements. Hopefully you were under camouflage":RETURN
1995 a$=INKEY$
1996 IF a$<>"" THEN RETURN
1997 GOTO 1995
1999 END
2000 REM ROOM 2
2010 MODE 1:
2011 PEN 2:INK 0,0:INK 1,3:INK 2,25:INK 3,15:LOAD"2.scr":GOSUB 9030
2030 GOTO 2100
2089 GOSUB 1995
2100 LOCATE 2,16:PRINT"What will you do?":LOCATE 2,18:PRINT"1. Take the KALASHNIKOV rifle":LOCATE 2,19:PRINT"2. Take the camouflage":LOCATE 2,20:PRINT"3. Take the grenade"
2101 LOCATE 2,21:PRINT"4. Report to Zhukov":LOCATE 2,22:PRINT"0. Go back to the main road"
2102 GOSUB 9020
2109 GOSUB 999
2110 T$=INKEY$ 
2115 IF T$="1" THEN OBJ$="KALASHNIKOV":GOSUB 9720:GOSUB 9010:GOTO 2110
2120 IF T$="2" THEN OBJ$="CAMOUFLAGE":GOSUB 9720:GOSUB 9010:GOTO 2110
2125 IF T$="3" THEN OBJ$="GRENADE":GOSUB 9720:GOSUB 9010:GOTO 2110
2130 IF T$="4" AND OBJ$<>"MAP" THEN MODE 2:INK 1,26:GOSUB 9730:GOSUB 2900:GOSUB 490:GOTO 2000
2135 IF T$="4" AND OBJ$="MAP" AND TRA=0 THEN GOSUB 9490:GOTO 2910
2140 IF T$="4" AND OBJ$="MAP" AND TRA=1 THEN ZOY=1:GOSUB 9720:MODE 2:GOSUB 2920:GOSUB 490:GOTO 2000
2160 IF T$="0" THEN GOTO 509
2200 GOTO 2110
2900 PEN 1:LOCATE 28,15:PRINT"There is nothing to report":RETURN
2909 GOSUB 490:GOTO 30
2910 PEN 1:LOCATE 5,15:PRINT"You brought the map, but you left the comrade unhelped. You are punished"
2919 GOSUB 490:GOTO 30
2920 PEN 1:INK 1,26:LOCATE 10,15:PRINT"Well done comrade! The map is invaluable! Now take the truck.":LOCATE 3,17:PRINT"Go to the <Road of life> on the Ladoga lake and bring supplies. Be careful!":RETURN
2999 END
3000 REM ROOM 3
3010 MODE 1:
3011 PEN 1:INK 0,0:INK 1,26:INK 2,15:INK 3,12:LOAD"3.scr":GOSUB 9030
3030 GOTO 3100
3090 GOSUB 1995
3100 LOCATE 2,16:PRINT"What will you do?"
3101 LOCATE 2,18:PRINT"1. Take the bulletproof vest"
3102 LOCATE 2,19:PRINT"2. Take with you 1 kg of bread":LOCATE 2,20:PRINT"3. Take the gauzes":LOCATE 2,21:PRINT"4. Take the morphine injection"
3103 IF BEE=0 THEN LOCATE 2,22:PRINT"5. Take the can with the gas"
3105 IF BEE=1 THEN LOCATE 2,22:PRINT"5. Take the Molotov"
3107 LOCATE 2,23:PRINT"0. Go back to the main road"
3108 GOSUB 9020
3109 GOSUB 999
3110 T$=INKEY$
3115 IF T$="1" THEN OBJ$="BULLETPR.VEST":GOSUB 9720:GOSUB 9010:GOTO 3110
3120 IF T$="2" THEN GOSUB 9490:GOTO 3900
3125 IF T$="3" THEN OBJ$="GAUZES":GOSUB 9720:GOSUB 9010:GOTO 3110
3130 IF T$="4" THEN OBJ$="MORPH.INJECT.":GOSUB 9720:GOSUB 9010:GOTO 3110
3133 IF T$="5" AND BEE=1 THEN OBJ$="MOLOTOV":GOSUB 9720:GOSUB 9010:GOTO 3110
3135 IF T$="5" AND OBJ$="BOTTLE" THEN BEE=1:GOSUB 9720:MODE 2:GOSUB 7930:GOSUB 490:GOTO 3000
3140 IF T$="5" THEN OBJ$="GAS":GOSUB 9720:GOSUB 9010:GOTO 3110
3160 IF T$="0" THEN GOTO 509
3200 GOTO 3110
3500 REM greek
3827 SYMBOL 251,3,15,63,127,127,63,15,3
3828 SYMBOL 254,0,0,0,0,24,60,126,255
3830 RETURN
3900 PEN 1:INK 1,26:LOCATE 5,15:PRINT"Each person gets 250gr bread per day and you take 1 kilo? Shame on you!":LOCATE 32,17:PRINT"You are imprisoned"
3909 GOSUB 490:GOTO 30
3999 END
4000 REM ROOM 4
4010 MODE 1
4011 PEN 1:INK 0,0:INK 1,26:INK 2,13:INK 3,16:LOAD"4.SCR":GOSUB 9030
4030 GOTO 4100
4090 GOSUB 1995
4100 IF XAR=0 THEN LOCATE 2,16:PRINT"Four Germans try to go behind you!"
4101 IF XAR=1 THEN LOCATE 2,16:PRINT"What will you do?"
4102 IF TRA=0 THEN LOCATE 2,18:PRINT"1. Help the wounded soldier" ELSE LOCATE 2,18:PRINT"1. Take the bazooka"
4103 LOCATE 2,19:PRINT"2. Surrender temporarily":LOCATE 2,20:PRINT"3. Fall back and hide"
4104 IF XAR=0 THEN LOCATE 2,21:PRINT"4. Attack the enemy soldiers" ELSE LOCATE 2,21:PRINT"4. Take the Germans' map"
4106 LOCATE 2,22:PRINT"0. Go back to the main road"
4108 GOSUB 9020
4109 GOSUB 999
4110 T$=INKEY$
4114 IF TRA=1 THEN GOTO 4130
4115 IF T$="1" AND OBJ$<>"GAUZES" AND OBJ$<>"MORPH.INJECT." THEN GOSUB 9730:LOCATE 2,24:PRINT"You hold nothing to help":GOSUB 4990:GOTO 4110
4116 IF T$="1" AND XAR=0 THEN GOSUB 9490:GOTO 4930
4120 IF T$="1" AND OBJ$="MORPH.INJECT." THEN GOSUB 9490:GOTO 4910
4125 IF T$="1" AND OBJ$="GAUZES" THEN TRA=1:GOSUB 9720:MODE 2:GOSUB 4920:GOSUB 490:GOTO 4000
4130 IF T$="1" AND TRA=1 THEN OBJ$="BAZOOKA":GOSUB 9720:GOSUB 9010:GOTO 4110
4135 IF T$="2" THEN GOSUB 9490:GOTO 4940
4140 IF T$="3" THEN GOSUB 9490:GOTO 4950
4145 IF T$="4" AND XAR=1 THEN OBJ$="MAP":GOSUB 9720:GOSUB 9010:GOTO 4110
4147 IF T$="4" AND XAR=0 AND OBJ$="BULLETPR.VEST" THEN GOSUB 9490:GOTO 4985
4150 IF T$="4" AND XAR=0 AND OBJ$="GRENADE" THEN XAR=1:GOSUB 9720:MODE 2:GOSUB 4960:GOSUB 490:GOTO 4000
4152 IF T$="4" AND XAR=0 AND OBJ$<>"BAZOOKA" AND OBJ$<>"GRENADE" AND OBJ$<>"KALASHNIKOV" THEN GOSUB 9490:GOTO 1925
4153 IF T$="4" AND XAR=0 AND OBJ$="KALASHNIKOV" THEN GOSUB 9490:GOTO 4975
4160 IF T$="0" THEN GOTO 509
4200 GOTO 4110
4910 LOCATE 8,15:PRINT"Will you cure him with the morphine? The comrade is dead, traitor!"
4919 GOSUB 490:GOTO 30
4920 INK 1,26:LOCATE 14,15:PRINT"Well done! He is heavily injured, but he will survive!":LOCATE 20,17:PRINT"He gives you the bazooka with 1 rocket only":RETURN
4930 LOCATE 10,15:PRINT"They shoot you just while you try to help him - You are killed"
4939 GOSUB 490:GOTO 30
4940 LOCATE 2,15:PRINT"Seriously? There is no temporary surrender! You are sent to Auschwitz and die"
4949 GOSUB 490:GOTO 30
4950 LOCATE 6,15:PRINT"Have you heard the order <Not a step back>? You are killed by a rocket"
4959 GOSUB 490:GOTO 30
4960 INK 1,26:LOCATE 14,15:PRINT"You killed all of them using the grenade. Smart move!":LOCATE 18,17:PRINT"You found an invaluable map of the Ladoga lake":RETURN
4975 LOCATE 20,15:PRINT"They were four, they shot you first. You die"
4979 GOSUB 490:GOTO 30
4985 LOCATE 7,15:PRINT"How much can a bulletproof vest protect you? A bullet hits your head"
4989 GOSUB 490:GOTO 30
4990 FOR d=1 TO 600:NEXT d:LOCATE 2,24:PRINT SPC(24):RETURN
4999 END
5000 CLS:REM tank
5001 GOSUB 999:BORDER 0:MODE 1::INK 0,11:INK 1,26:INK 2,9:INK 3,0
5008 FOR j=0 TO 200 STEP 50
5010 ORIGIN 90+j,120
5015 DEG
5020 MOVE 0,15
5025 FOR a=0 TO 360 STEP 10
5030 DRAW 15*SIN(a),15*COS(a)
5035 NEXT
5040 ORIGIN 0,0
5045 NEXT j
5050 PLOT 90,120:PLOT 140,120:PLOT 190,120:PLOT 240,120:PLOT 290,120
5060 FOR j=0 TO 260 STEP 260
5065 ORIGIN 60+j,130
5070 DEG
5075 MOVE 0,10
5080 FOR a=0 TO 360 STEP 10
5085 DRAW 10*SIN(a),10*COS(a)
5090 NEXT
5093 ORIGIN 0,0
5096 NEXT j 
5098 PLOT 60,130:PLOT 320,130
5100 PLOT 83,100:DRAW 295,100:DRAW 338,127:DRAW 340,132:DRAW 340,138:DRAW 335,145:DRAW 45,145:DRAW 40,140:DRAW 40,132:DRAW 45,125:DRAW 83,100 
5105 PLOT 45,145:DRAW 90,170:DRAW 260,170:DRAW 290,145:PLOT 140,170:DRAW 150,215:DRAW 265,215:DRAW 272,208:DRAW 275,200:DRAW 275,185:DRAW 271,178:DRAW 265,173:DRAW 260,170
5110 PLOT 275,200:DRAW 282,200:DRAW 282,189:DRAW 275,189
5115 REM PLOT 282,198:DRAW 370,198:DRAW 370,191:DRAW 282,191
5116 PLOT 282,198:DRAW 395,220:DRAW 396,218:DRAW 398,215:DRAW 282,192
5120 PLOT 190,215:DRAW 190,225:DRAW 193,227:DRAW 220,227:DRAW 223,224:DRAW 223,215:PLOT 200,228:DRAW 205,228:PLOT 200,229:DRAW 205,229:PLOT 200,230:DRAW 205,230
5125 PLOT 110,150:DRAW 110,164:DRAW 150,164:DRAW 150,150:DRAW 110,150:PLOT 150,164:DRAW 180,164:DRAW 180,160:DRAW 150,160
5127 PEN 1: LOCATE 12,14:PRINT"112"
5130 REM COL
5135 MOVE 170,120:FILL 2:MOVE 95,120:FILL 3:MOVE 145,120:FILL 3:MOVE 195,120:FILL 3:MOVE 245,120:FILL 3:MOVE 295,120:FILL 3:MOVE 62,130:FILL 3:MOVE 322,130:FILL 3
5140 MOVE 100,150:FILL 2:MOVE 130,158:FILL 3:MOVE 170,162:FILL 3
5145 MOVE 230,200:FILL 2:MOVE 200,220:FILL 3:MOVE 278,195:FILL 3:MOVE 290,197:FILL 3
5200 REM ovida
5205 OV$=CHR$(251)+CHR$(143)+CHR$(143)+CHR$(143):PS$=CHR$(143)
5210 FOR DL=1 TO 1000:NEXT DL:SOUND 1,100,15,12,,,15:PEN 3:FOR V=0 TO 21:LOCATE 37-V,14:PRINT OV$:FOR DL=1 TO 20:NEXT DL:LOCATE 37-V,14:PRINT"    ":NEXT V:LOCATE 15,14:PRINT OV$
5215 SOUND 1,800,20,15,,,15:PEN 3:FOR L=1 TO 3:FOR P=0 TO 4:LOCATE 18-L,14+P:PRINT CHR$(143):NEXT P:NEXT L:SOUND 1,800,20,15,,,15
5220 FOR L=1 TO 35:PEN 3
5225 TYXA=INT(RND*13):TYXB=INT(RND*4)
5230 SOUND 1,600,2,15,,,15:LOCATE 7+TYXA,14+TYXB:PRINT CHR$(143)
5235 NEXT L
5244 PEN 1
5245 GOSUB 490
5246 INK 0,0:BORDER 11
5398 RETURN
5399 END
5400 GOSUB 999:PEN 1:BORDER 0:CLS:INK 0,11:INK 1,26:INK 2,10:INK 3,0:REM zogr
5401 ORIGIN 100,90
5405 DEG
5410 MOVE 0,25
5415 FOR a=0 TO 360 STEP 10
5420 DRAW 25*SIN(a),25*COS(a)
5425 NEXT
5430 ORIGIN 0,0
5435 ORIGIN 100,90
5440 DEG 
5445 MOVE 0,40
5450 FOR a=0 TO 360 STEP 10
5455 DRAW 40*SIN(a),40*COS(a)
5460 NEXT
5465 ORIGIN 0,0
5470 ORIGIN 100,90
5475 DEG
5480 MOVE 0,5
5485 FOR a=0 TO 360 STEP 10
5490 DRAW 5*SIN(a),5*COS(a)
5495 NEXT
5500 ORIGIN 0,0
5505 MOVE 100,105:FILL 3:REM color
5510 PLOT 100,90:DRAW 84,110:PLOT 100,90:DRAW 116,110:PLOT 100,90:DRAW 123,78:PLOT 100,90:DRAW 100,64:PLOT 100,90:DRAW 77,78
5520 ORIGIN 320,90
5525 DEG
5530 MOVE 0,25
5535 FOR a=0 TO 360 STEP 10
5540 DRAW 25*SIN(a),25*COS(a)
5545 NEXT
5550 ORIGIN 0,0
5555 ORIGIN 320,90
5560 DEG 
5565 MOVE 0,40
5570 FOR a=0 TO 360 STEP 10
5575 DRAW 40*SIN(a),40*COS(a)
5580 NEXT
5585 ORIGIN 0,0
5590 ORIGIN 320,90
5595 DEG
5600 MOVE 0,5
5605 FOR a=0 TO 360 STEP 10
5610 DRAW 5*SIN(a),5*COS(a)
5615 NEXT
5620 ORIGIN 0,0
5625 MOVE 320,105:FILL 3:REM color
5630 PLOT 320,90:DRAW 304,110:PLOT 320,90:DRAW 336,110:PLOT 320,90:DRAW 343,78:PLOT 320,90:DRAW 320,64:PLOT 320,90:DRAW 297,78
5635 REM fortigo
5640 PLOT 90,130:DRAW 90,140:PLOT 110,130:DRAW 110,140:PLOT 310,130:DRAW 310,140:PLOT 330,130:DRAW 330,140
5645 PLOT 30,120:DRAW 50,120:DRAW 80,140:DRAW 120,140:DRAW 150,120:DRAW 270,120:DRAW 300,140:DRAW 340,140:DRAW 370,120:DRAW 405,120:DRAW 405,200 
5650 DRAW 405,317:DRAW 402,320:DRAW 283,320:DRAW 280,317:DRAW 280,200:DRAW 30,200:DRAW 30,120 
5655 PLOT 295,240:DRAW 295,300:DRAW 325,300:DRAW 390,240:DRAW 295,240:PLOT 300,210:DRAW 320,210
5660 PLOT 40,200:DRAW 40,254:DRAW 270,254:DRAW 270,200:MOVE 50,210:FILL 2:FOR k=1 TO 230 STEP 10:MOVE  39+k,200:DRAW 49+k,254:MOVE 271-k,200:DRAW 261-k,254:NEXT k:REM col
5665 FOR k=1 TO 13 STEP 2::an$=CHR$(225):LOCATE 3+k,9:PRINT an$:NEXT k:LOCATE 22,9:PRINT an$::LOCATE 22,10:PRINT CHR$(143):LOCATE 20,9:PRINT an$::LOCATE 20,10:PRINT CHR$(143)
5670 PLOT 280,300:DRAW 280,126:PLOT 280,190:DRAW 300,160:DRAW 340,160:DRAW 370,210:DRAW 405,210
5675 REM color
5680 MOVE 100,150:FILL 2:MOVE 320,150:FILL 3:MOVE 100,250:FILL 2:MOVE 100,120:FILL 3:MOVE 320,120:FILL 3:MOVE 320,200:FILL 2:MOVE 325,250:FILL 3
5688 GOSUB 5750
5689 RETURN
5690 REM explosion
5750 REM ovida
5755 OV$=CHR$(251)+CHR$(143)+CHR$(143)+CHR$(143):PS$=CHR$(143)
5760 FOR DL=1 TO 1000:NEXT DL:SOUND 1,100,15,12,,,15:GOSUB 9710:PEN 3:FOR V=0 TO 14:LOCATE 37-V,11:PRINT OV$:FOR DL=1 TO 50:NEXT DL:LOCATE 37-V,11:PRINT"    ":NEXT V:LOCATE 22,11:PRINT OV$
5770 SOUND 1,800,20,15,,,15:PEN 3:FOR L=1 TO 6:FOR P=0 TO 4:LOCATE 24-L,12-P:PRINT CHR$(143):NEXT P:NEXT L:SOUND 1,800,20,15,,,15
5779 FOR L=1 TO 110:PEN 3
5780 TYXA=INT(RND*18):TYXB=INT(RND*7)
5784 SOUND 1,600,2,15,,,15:GOSUB 9710:LOCATE 4+TYXA,8+TYXB:PRINT CHR$(143)
5785 NEXT L
5786 FOR P=1 TO 7:LOCATE 2+2*P,9:PRINT CHR$(143):NEXT P
5798 GOSUB 490
5799 BORDER 11:INK 0,0:RETURN
5910 RETURN
5999 END
6000 REM ROOM 6
6009 SC6=SC6+1
6011 PEN 1:MODE 1:INK 0,0:INK 1,26:INK 2,10:INK 3,21:LOAD"6.scr":GOSUB 9030
6012 GOSUB 999
6018 IF SC6<>1 THEN GOTO 6100
6020 GOTO 6100
6090 GOSUB 1995:GOSUB 6900
6100 LOCATE 2,16:PRINT"German tank approaching in 100m!"
6101 LOCATE 2,18:PRINT"1. Negotiate using white flag":LOCATE 2,19:PRINT"2. Attack the tank":LOCATE 2,20:PRINT"3. Try to make a flanking maneuver":LOCATE 2,21:PRINT"4. Accelerate and try to escape"
6102 LOCATE 2,22:PRINT"0. Go back to the military base"
6105 GOSUB 9020 
6109 GOSUB 999
6110 T$=INKEY$
6115 IF T$="1" THEN GOSUB 9490:GOTO 6910
6116 IF T$="2" AND OBJ$="MOLOTOV" THEN GOSUB 9490:GOTO 6992
6117 IF T$="2" AND OBJ$="BOTTLE" THEN GOSUB 9490:GOTO 6996
6120 IF T$="2" AND OBJ$="BAZOOKA" AND ROY=1 THEN GOSUB 9730:GOSUB 7990:GOSUB 6990:GOTO 6110
6121 IF T$="2" AND OBJ$="GRENADE" THEN GOSUB 9490:GOTO 6920
6122 IF T$="2" AND OBJ$="KALASHNIKOV" THEN GOSUB 9490:GOTO 6960
6123 IF T$="2" AND OBJ$<>"KALASHNIKOV" AND OBJ$<>"BAZOOKA" AND OBJ$<>"GRENADE" THEN GOSUB 9730:GOSUB 6900:GOSUB 6990:GOTO 6110
6124 IF T$="2" AND OBJ$="BAZOOKA" AND ROY=0 THEN GOSUB 9720:GOSUB 9700:GOSUB 5000:MODE 2:GOSUB 6995:GOSUB 490:GOTO 8000 
6125 IF T$="3" THEN GOSUB 9490:GOTO 6930
6130 IF T$="4" THEN GOSUB 9490:GOTO 6940
6160 IF T$="0" THEN GOSUB 9490:GOTO 6970
6200 GOTO 6110
6900 LOCATE 2,24:PRINT"No weapons carried":RETURN
6910 LOCATE 17,15:PRINT"Come on, do something brave! It's just a game!"
6919 GOSUB 490:GOTO 30
6920 LOCATE 1,15:PRINT"You have no chance using only 1 grenade for the tank. You are killed by a rocket"
6929 GOSUB 490:GOTO 30
6930 LOCATE 1,15:PRINT"How can you make a flanking maneuver with 1 truck to a tank? Truck was blown up"
6939 GOSUB 490:GOTO 30
6940 PEN 1:INK 1,26:LOCATE 7,15:PRINT"Not so easy to go far away - your truck was blown up by a bombshell"
6949 GOSUB 490:GOTO 30
6950 PEN 1:INK 1,26:LOCATE 1,15:PRINT"You had one shot only, but you made it! The tank was destroyed with the bazooka!":RETURN
6960 LOCATE 5,15:PRINT"Difficult to face a tank with a Kalashnikov. You are killed by a rocket"
6969 GOSUB 490:GOTO 30
6970 LOCATE 2,15:PRINT"Going back was a bad option. You fall into a trap and your truck is destroyed" 
6979 GOSUB 490:GOTO 30
6990 FOR d=1 TO 600:NEXT d:LOCATE 2,24:PRINT SPC(25):RETURN
6992 LOCATE 12,15:PRINT"How can you throw the Molotov in 100 meters? You are killed"
6993 GOSUB 490:GOTO 30
6995 LOCATE 14,15:PRINT"Well done! You destroyed the tank! Road is clear now!":RETURN
6996 LOCATE 19,15:PRINT"Are you serious? Attacking with a bottle?"
6997 GOSUB 490:GOTO 30
6999 END
7000 REM ROOM 7
7006 SC7=SC7+1
7010 MODE 1
7012 PEN 3:INK 0,0:INK 1,15:INK 2,9:INK 3,26
7013 LOAD"7.SCR"
7014 GOSUB 9030
7015 GOSUB 999
7018 IF SC7<>1 THEN GOTO 7100
7020 GOTO 7100 
7090 GOSUB 1995:
7100 IF AT=1 THEN GOTO 7200
7101 LOCATE 2,16:PRINT"You see an enemy truck with 20 Germans!"
7102 LOCATE 2,18:PRINT"1. Negotiate using white flag":LOCATE 2,19:PRINT"2. Accelerate and crash into the truck"
7103 IF BE=0 THEN LOCATE 2,20:PRINT"3. Take the glass bottle" ELSE LOCATE 2,20:PRINT"3. Take the Molotov"
7104 LOCATE 2,21:PRINT"4. Hide in the forest"
7107 IF AT=0 THEN LOCATE 2,22:PRINT"5. Attack the truck" ELSE LOCATE 2,22:PRINT"5. Continue mission"
7108 LOCATE 2,23:PRINT"0. Go back to the military base"
7109 GOSUB 9020
7110 t$=INKEY$
7115 IF T$="1" THEN GOSUB 9490:GOTO 7900
7120 IF T$="2" THEN GOSUB 9490:GOTO 7910
7125 IF T$="3" AND BE=1 THEN OBJ$="MOLOTOV":GOSUB 9720:GOSUB 9010:GOTO 7110
7130 IF T$="3" AND OBJ$<>"GAS" THEN OBJ$="BOTTLE":GOSUB 9720:GOSUB 9010:GOTO 7110
7132 IF T$="3" AND OBJ$="GAS" THEN BE=1:GOSUB 9720:MODE 2:GOSUB 7930:GOSUB 490:GOTO 7000
7140 IF T$="4" THEN GOSUB 9490:GOTO 7920
7141 IF T$="5" AND AT=1 THEN GOTO 6000
7142 IF T$="5" AND OBJ$="MOLOTOV" THEN AT=1:GOSUB 9720:MODE 2:GOSUB 7995:GOSUB 490:GOTO 7000
7145 IF T$="5" AND OBJ$="KALASHNIKOV" THEN GOSUB 9490:GOTO 7940
7150 IF T$="5" AND OBJ$="GRENADE" THEN GOSUB 9490:GOTO 7950
7157 IF T$="5" AND OBJ$="BAZOOKA" THEN AT=1:ROY=1:GOSUB 9720:GOSUB 5400:MODE 2:GOSUB 7980:GOSUB 490:GOTO 7000
7158 IF T$="5" AND OBJ$="BOTTLE" THEN GOSUB 9490:GOTO 6996
7159 IF T$="5" AND OBJ$<>"KALASHNIKOV" AND OBJ$<>"BAZOOKA" AND OBJ$<>"GRENADE" AND OBJ$<>"BOTTLE" THEN GOSUB 9730:GOSUB 6900:GOSUB 6990:GOTO 7110
7170 IF T$="0" THEN GOTO 1000
7190 GOTO 7110
7200 GOSUB 9020:LOCATE 2,16:PRINT"What will you do?"
7210 LOCATE 2,18:PRINT"1. Continue mission"
7220 IF BE=0 THEN LOCATE 2,19:PRINT"2. Take the glass bottle" ELSE LOCATE 2,19:PRINT"2. Take the Molotov" 
7230 LOCATE 2,20:PRINT"0. Go back to the headquarters"
7240 T$=INKEY$
7245 IF T$="1" THEN GOTO 6000
7246 IF T$="2" AND BE=1  THEN OBJ$="MOLOTOV":GOSUB 9720:GOSUB 9010:GOTO 7240
7247 IF T$="2" AND OBJ$<>"GAS" THEN OBJ$="BOTTLE":GOSUB 9720:GOSUB 9010:GOTO 7240
7249 IF T$="2" AND OBJ$="GAS" THEN BE=1:GOSUB 9720:MODE 2:GOSUB 7930:GOSUB 490:GOTO 7000
7250 IF T$="0" THEN GOTO 2000
7260 GOTO 7240
7900 LOCATE 17,15:PRINT"Come on, do something brave! It's just a game!"
7909 GOSUB 490:GOTO 30
7910 LOCATE 4,15:PRINT"You just did the bravest choice, if had run out of ammo. You are all dead"
7919 GOSUB 490:GOTO 30
7920 LOCATE 12,15:PRINT"You didn't have much time to hide. You are shot and killed "
7929 GOSUB 490:GOTO 30
7930 PEN 1:INK 1,26:LOCATE 12,15:PRINT"Well done! You made a Molotov using the gas and the bottle!"
7939 RETURN
7940 LOCATE 5,15:PRINT"So hard to kill everyone with a Kalashnikov. You are killed with honor!"
7949 GOSUB 490:GOTO 30
7950 LOCATE 7,15:PRINT"So hard to kill everyone with a grenade. You are killed with honor!"
7959 GOSUB 490:GOTO 30
7980 PEN 1:INK 1,26:LOCATE 18,15:PRINT"Well done! You blew up the whole enemy truck!":LOCATE 19,17:PRINT"But did you spend quickly the only rocket?":RETURN
7990 LOCATE 2,24:PRINT"You had only 1 rocket!!!":RETURN
7995 PEN 1:INK 1,26:LOCATE 17,15:PRINT"Well done! You burnt all soldiers of the truck!":RETURN
8000 REM
8500 REM WINNING!
8800 REM END SCREEN
8804 GOSUB 9700
8805 BORDER 11:INK 0,0:MODE 1:INK 1,26:INK 2,13:INK 3,3:LOAD"8.scr":SONG$="MUSIC4.AKG":GOSUB 9500
8810 GOSUB 9030
8820 PEN 1:LOCATE 1,16:PRINT"You saved many lives by transporting    supplies to LENINGRAD!":LOCATE 1,19:PRINT"After much time, the siege was broken   and the Red Army triumphed!" 
8830 LOCATE 1,22:PRINT"You were awarded the medal of the Hero  of the Soviet Union!"
8850 FOR DEL=1 TO 2000:NEXT DEL:LOCATE 16,25:PRINT"GAME OVER"
8855 GOSUB 490
8856 LOCATE 40,16:PRINT" ":LOCATE 40,22:PRINT" ":FOR j=0 TO 9:LOCATE 1,16+J:PRINT SPC(39):NEXT J:GOSUB 999:LOCATE 3,20:PRINT"DEDICATED TO THE HEROES OF LENINGRAD"
8858 t$=INKEY$
8859 IF t$="" THEN GOTO 8858
8860 MODE 2:INK 1,26:INK 0,0:PEN 1:GOSUB 999
8861 LOCATE 1,1:PRINT"CREDITS"
8865 LOCATE 1,4:PRINT"SPECIAL THANKS TO:":LOCATE 1,6:PRINT"MANOSSG FOR MUSIC, PROOFREADING, IDEAS!":LOCATE 1,8:PRINT"BRUNDIJ FOR DIGITIZED IMAGES!"
8866 LOCATE 1,10:PRINT"ROBERTO CARANGELO FOR FRENCH TRANSLATION!":LOCATE 1,12:PRINT"MIGUELSKY FOR SPANISH TRANSLATION!"
8867 LOCATE 1,14:PRINT"ANIMALGRIL987 FOR TESTING THE GAME!
8868 LOCATE 1,16:PRINT"FREDERIC BEZIES FOR TESTING FRENCH TRANSLATION!"
8870 LOCATE 1,18:PRINT""
8997 GOSUB 490
8998 GOSUB 9700:GOTO 5
8999 END
9000 REM DIE
9010 LOCATE 27,25:PRINT SPC(13):LOCATE 27,25:PRINT OBJ$:RETURN
9020 LOCATE 20,25:PRINT"OBJECT:";OBJ$:RETURN
9030 MOVE 1,178:DRAW 640,178:RETURN
9400 GOSUB 9490
9490 GOSUB 9700:MODE 2:PEN 1:INK 1,26:SOUND 1,500,15,15,,,15:RETURN
9499 REM MUSIC
9500 REM
9510 music=&7000
9520 sfx=&9000
9530 player=&9500
9540 LOAD song$,music
9550 LOAD"sfx.akx",sfx
9560 LOAD"player.bin",player
9570 CALL player,music,0   
9580 CALL player+6,sfx  
9590 REM
9620 RETURN
9700 CALL PLAYER+3:RETURN
9710 CALL player+9,2,1,5:RETURN:REM g
9720 CALL player+9,4,1,5:RETURN:REM C
9730 CALL player+9,3,1,5:RETURN:REM n
*/ });
