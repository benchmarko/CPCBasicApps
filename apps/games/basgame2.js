/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem basgame2 - BASIC Game 2
2 rem (c) Roald (Mr.Lou) Strauss
3 rem https://www.cpcwiki.eu/forum/programming/another-unfinished-basic-game-project/
4 rem adapted by AMSDOS
5 rem 
100 KEY 0,"ink 0,11:border 11:ink 1,26:pen 1:paper 0:mode 2"+CHR$(13)
110 ENV 1,15,-1,1:ENV 2,15,-1,5:ENT 2,40,2,1:ENV 3,15,-1,10:ENT 3,100,2,2:ENV 4,15,1,1:ENT 4,100,-5,1
120 SYMBOL 249,0,0,0,0,0,0,56,0
130 SYMBOL 250,16,16,56,108,108,254,198,130
140 SYMBOL 251,16,32,32,16,8,8,16,16
150 SYMBOL 252,170,0,0,0,0,0,0,0
160 SYMBOL 253,173,154,170,164,84,72,40,16
170 SYMBOL 254,8,4,4,8,16,16,8,8
180 SYMBOL 255,16,16,16,16,16,16,16,16
190 PAPER 0:INK 0,1:BORDER 0:INK 1,22,18:INK 2,25,24:INK 3,15,24:SPEED INK 3,3
200 MODE 1
210 DEFINT a-z:DIM e(32)
220 FOR n=0 TO 32
230   e(n)=1
240 NEXT
250 x=16:y=23:c=1:f=1:s=12:l=2
260 PEN 1
270 FOR n=1 TO 32
280   SOUND 1,1000-n*10,1,15,1
290   LOCATE n,e(n):PEN 1:PRINT CHR$(253);
300   LOCATE n,21:PEN 2:PRINT CHR$(252);
310 NEXT:r=INT(RND*32)+1:b=29:GOSUB 1000
320 GOSUB 1020
330 WHILE 1
335   t!=time
340   c=(c+1) AND 16383
350   j=(JOY(0) AND 31)
360   ON J GOSUB 420,420,420,430,430,430,420,440,440,440,420,420,420,420,420,450,450,450,420,550,550,550,420,560,560,560
370   IF (JOY(0) AND 32) OR INKEY(47)=0 THEN GOSUB 620 ELSE f=1:IF (JOY(0) AND 16)=0 THEN LOCATE 34,5:PRINT c
380   IF (c MOD s)=0 THEN GOSUB 570
390   IF (c MOD l)=0 THEN l=l*2:s=s-1:IF s=0 THEN s=1
400   IF p=x THEN GOSUB 1000
406   if time-t!<18 then call &bd19:goto 406
410 WEND
420 RETURN ' Do nothing
430 GOSUB 1110:x=((x-2) AND 31)+1:GOSUB 1020:RETURN
440 GOSUB 1110:x=(x AND 31)+1:GOSUB 1020:RETURN
450 IF b=0 THEN RETURN ELSE SOUND 2,10,10,15,2,2,1 ' Fire
460 a=e(x)-INT(b/50):IF a<2 THEN a=2
470 LOCATE x,e(x)+1:PRINT USING"&";" ";CHR$(8);CHR$(11);:PEN 3
480 FOR n=a TO e(x)
490   PRINT USING "&";CHR$(251);CHR$(8);CHR$(11);
500 NEXT:GOSUB 570:LOCATE x,e(x):
510 FOR n=a+1 TO e(x)
520   PRINT USING "&";" ";CHR$(8);CHR$(11);
530 NEXT:e(x)=a-1
540 b=b-1:LOCATE 34,1:PRINT b:RETURN
550 GOSUB 450:GOSUB 430:RETURN ' Fire + left
560 GOSUB 450:GOSUB 440:RETURN ' Fire + right
570 FOR d=1 TO 2
580  r=INT(RND*32)+1:e(r)=e(r)+1:LOCATE r,e(r):PEN 1:PRINT CHR$(254);:IF e(r)=20 THEN GOTO 980
590 NEXT
600 SOUND 4,INT(RND*500)+200,10,15,1
610 RETURN
620 IF f THEN f=0 ELSE RETURN ' Bomb
630 IF b<10 THEN RETURN
640 SOUND 2,10,50,15,3,3,15:PEN 3:LOCATE x,e(x)+1
650 FOR n=1 TO e(x)
660   PRINT USING"&";CHR$(251);CHR$(8);CHR$(11);
670 NEXT:z=((x-2) AND 31)+1:LOCATE z,e(z)+1
680 FOR n=1 TO e(z)-1
690   PRINT USING"&";CHR$(251);CHR$(8);CHR$(11);
700 NEXT:z=(x AND 31)+1:LOCATE z,e(z)+1
710 FOR n=1 TO e(z)-1
720   PRINT USING"&";CHR$(251);CHR$(8);CHR$(11);
730 NEXT:LOCATE x,e(x)+1
740 FOR n=2 TO e(x)
750   PRINT USING"&";" ";CHR$(8);CHR$(11);
760 NEXT
770 e(x)=1:z=((x-3) AND 31)+1:LOCATE z,e(z)+1
780 FOR n=1 TO e(z)-2
790   PRINT USING"&";CHR$(251);CHR$(8);CHR$(11);
800 NEXT:z=((x+1) AND 31)+1
810 LOCATE z,e(z)+1
820 FOR n=1 TO e(z)-2
830   PRINT USING"&";CHR$(251);CHR$(8);CHR$(11);
840 NEXT:z=((x-2) AND 31)+1:LOCATE z,e(z)+1
850 FOR n=3 TO e(z)
860   PRINT USING"&";" ";CHR$(8);CHR$(11);
870 NEXT:e(z)=1:z=(x AND 31)+1:LOCATE z,e(z)+1
880 FOR n=3 TO e(z)
890   PRINT USING"&";" ";CHR$(8);CHR$(11);
900 NEXT:e(z)=1:z=((x-3) AND 31)+1:LOCATE z,e(z)+1
910 FOR n=4 TO e(z)
920   PRINT USING"&";" ";CHR$(8);CHR$(11);
930 NEXT:e(z)=1:z=((x+1) AND 31)+1:LOCATE z,e(z)+1
940 FOR n=4 TO e(z)
950   PRINT USING"&";" ";CHR$(8);CHR$(11);
960 NEXT:e(z)=1
970 b=b-10:LOCATE 34,1:PRINT b:RETURN
980 PRINT "Dead"
990 END
1000 SOUND 1,478,50,5,4,4:p=INT(RND*32)+1
1010 b=b+5:LOCATE 34,1:PRINT b:RETURN ' New powerup position
1020 LOCATE x,y
1030 PEN 2
1040 PRINT CHR$(250);
1050 PRINT CHR$(22);CHR$(1);
1060 PRINT CHR$(8);
1070 PEN 3
1080 PRINT CHR$(249);
1090 PRINT CHR$(22);CHR$(2);
1100 RETURN
1110 LOCATE x,y
1120 PRINT" "
1130 PEN 3
1140 LOCATE 31,24
1150 RETURN
*/ });
