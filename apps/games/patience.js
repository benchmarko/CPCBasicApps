/* globals cpcBasic */

"use strict";

cpcBasic.addItem("", function () { /*
1 rem patience - Patience: The Card Game
2 rem (c) Clive Blackmore
3 rem https://www.cpcwiki.eu/forum/programming/basic-files/ (BASIC01.DSK)
4 rem
10 REM *** PATIENCE - THE CARD GAME ***        Computerised by  Clive Blackmore
20 BORDER 13:INK 0,13:INK 1,26:PEN 1:PAPER 0
30 MODE 0:DIM C%(51):DIM A%(3):DIM R%(51):DIM S$(3):DIM H%(23):DIM L%(19,7):DIM LL%(19,7):DIM N$(12):DIM T$(2)
40 SYMBOL AFTER 122:SYMBOL 123,78,219,219,219,219,219,206,0:SYMBOL 125,0,85,170,85,170,85,170,85
50 T$(0)="1st":T$(1)="2nd":T$(2)="3rd"
60 S$(0)=CHR$(227):S$(1)=CHR$(228):S$(2)=CHR$(226):S$(3)=CHR$(229)
70  FOR x=0 TO 12
80 READ V$
90 N$(x)=V$ 
100 NEXT
110 DATA A,2,3,4,5,6,7,8,9,{,J,Q,K
120 MODE 0:BORDER 13:INK 0,13:INK 1,10,26
130 PAPER 0:PEN 1:LOCATE 6,10:PRINT"SHUFFLING":LOCATE 6,15:PRINT"THE CARDS!" 
140 T=0:NA=24
150 FOR x=0 TO 51:C%(x)=0:NEXT
160 FOR x=0 TO 51
170 RANDOMIZE TIME
180 A=INT(RND*52) 
190 IF C%(A)>0 GOTO 930
200 SOUND 1,1000/(x+1),5,7
210 C%(A)=1:R%(x)=A
220 NEXT
230 N=0
240 MODE 1
250 INK 1,26:INK 2,6:INK 3,0
260 KEY DEF 7,1,65
270 FOR ROW=1 TO 7
280 SOUND 1,210/ROW,120-15*ROW,7
290 FOR COL=ROW TO 7
300 Q=R%(N)
310 L%(ROW,COL)=R%(N):LL%(ROW,COL)=1 
320 H=10+4*COL:V=1+ROW
330 LOCATE H,V:IF COL=ROW THEN GOSUB 950 ELSE GOSUB 1260
340 N=N+1
350 NEXT
360 NEXT
370 FOR N=28 TO 51 
380 H%(N-28)=R%(N)
390 NEXT
400 H=6:V=14:GOSUB 1280
410 H=2:V=14:GOSUB 1260
420 LOCATE 15,1:PAPER 0:PEN 3:PRINT"1   2   3   4   5   6   7"
430 PLOT 8,392:DRAWR 128,0:DRAWR 0,-192:DRAWR-128,0:DRAWR 0,192:PLOT 8,296:DRAWR 128,0:PLOT 72,392:DRAWR 0,-192:LOCATE 5,7:PRINT"8":LOCATE 5,18:PRINT"9"
440 LOCATE 9,15:PRINT T$(T):LOCATE 9,16:PRINT"time":LOCATE 1,21:PAPER 0:PEN 3:PRINT"To move cards,       ","press old column number,","then new column number.","(Press X to cancel mistake.)";"To turn up next card press 0.";
450 CA=0
460 M$=UPPER$(INKEY$) 
470 IF M$="8" GOTO 1300
480 IF M$="0" AND NA=0 THEN SOUND 1,48,10,7:RUN 30
490 IF M$="0" AND T<3 GOTO 990  
500 IF M$="0"AND T=3 THEN SOUND 1,48,10,7:RUN 30
510 IF M$="R" OR M$="r" THEN RUN
520 IF VAL (M$)>0 AND VAL (M$)<8 THEN SOUND 1,64,15,7:GOTO 550
530 IF M$="9" THEN SOUND 1,64,15,7:GOTO 600
540 GOTO 460
550 COL=VAL(M$) 
560 FOR ROW=1 TO 19
570 IF LL%(ROW,COL)=2 AND LL%(ROW-1,COL)<2 THEN START=ROW
580 IF LL%(ROW,COL)=0 AND LL%(ROW-1,COL)=2 THEN FIN=ROW-1
590 NEXT
600 IF M$="9" AND CA=0 GOTO 1300
610 PAPER 0:LOCATE 1,20:PRINT"             "
620 N$=UPPER$(INKEY$)
630 IF VAL(N$)>0 AND VAL(N$)<8 THEN SOUND 1,128,15,7:GOTO 680
640 IF N$="X" THEN SOUND 1,2000,50,7:GOTO 460
650 IF N$="8" THEN SOUND 1,128,15,7:GOTO 840
660 IF N$="9" GOTO 1300
670 GOTO 620
680 COL=VAL(N$)
690 SOUND 1,128,20,7
700 IF M$="9" GOTO 720
710 IF L%(FIN,VAL(M$)) MOD 13=0 GOTO 1300
720 FOR ROW=1 TO 19
730 IF LL%(ROW,COL)=0 AND LL%(ROW-1,COL)=2 THEN BOT=ROW-1
740 NEXT
750 IF M$="9" GOTO 1170
760 IF LL%(1,VAL(N$))=0 AND L%(START,VAL(M$)) MOD 13=12 THEN BOT=0:GOTO 780  
770 IF (L%(BOT,VAL(N$))MOD 13)-(L%(START,VAL(M$))MOD 13)<>1 OR INT(L%(BOT,VAL(N$))/26)=INT(L%(START,VAL(M$))/26) GOTO 1300 
780 FOR M=START TO FIN
790 H=10+4*VAL(N$):V=BOT+2-START+M:Q=L%(M,VAL(M$)):LL%(M,VAL(M$))=0:LL%(BOT+1+M-START,VAL(N$))=2:L%(BOT+1+M-START,VAL(N$))=L%(M,VAL(M$)):GOSUB 960
800 H=10+4*VAL(M$):V=FIN+1-M+START:GOSUB 1280
810 NEXT
820 IF START>1 THEN H=10+4*VAL(M$):V=START:Q=L%(START-1,VAL(M$)):LL%(START-1,VAL(M$))=2:GOSUB 960
830 GOTO 460
840 IF M$="9" THEN Q=H%(CA-1):GOTO 860
850 Q=L%(FIN,VAL(M$))
860 IF Q MOD 13+1-A%(INT(Q/13))<>1 GOTO 1300 
870 H=2+INT(Q/26)*4:V=2+INT((Q MOD 26)/13)*6:GOSUB 960
880 A%(INT(Q/13))=(Q MOD 13)+1:IF M$="9" GOTO 1200
890 LL%(FIN,VAL(M$))=0
900 H=10+4*VAL(M$):V=1+FIN:GOSUB 1280
910 IF FIN>1 THEN Q=L%(FIN-1,VAL(M$)):V=FIN:GOSUB 960:LL%(FIN-1,VAL(M$))=2
920 GOTO 460
930 A=A+1:IF A=52 THEN A=0
940 GOTO 190
950 LL%(ROW,COL)=2 
960 PAPER INT(Q/26+2):PEN 1
970 LOCATE H,V:PRINT N$(Q MOD 13);" ";S$(INT(Q/13)):LOCATE H,V+1:PRINT "   ":LOCATE H,V+2:PRINT" ";N$(Q MOD 13);" ":LOCATE H,V+3:PRINT"   ":LOCATE H,V+4:PRINT S$(INT(Q/13));" ";N$(Q MOD 13) 
980 RETURN
990 H=6:v=14:TURN=0
1000 SOUND 1,48,10,7:LOCATE 1,20:PAPER 0:PRINT"             "
1010 CA=CA+1:TURN=TURN+1 
1020 IF CA=NA+1 GOTO 1080
1030 Q=H%(CA-1)
1040 IF TURN=3 OR CA=NA THEN GOSUB 960 ELSE GOTO 1010
1050 IF CA=NA THEN H=2:V=14:GOSUB 1280 
1060 IF CA=NA AND T=2 THEN LOCATE 1,15:PEN 3:PAPER 0:PRINT "LAST":LOCATE 1,16:PRINT "CARD":LOCATE 9,15:PRINT "   ":LOCATE 9,16:PRINT "    ":LOCATE 1,25:PEN 1:PAPER 3:PRINT"FOR NEW GAME PRESS 0 TWICE.. R FOR RULES";
1070 GOTO 460
1080 X=H%(0)
1090 IF NA=0 GOTO 400
1100 FOR N=0 TO NA-2
1110 H%(N)=H%(N+1)
1120 NEXT
1130 H%(NA-1)=X
1140 T=T+1
1150 IF T=3 GOTO 460
1160 GOTO 400
1170 IF LL%(1,VAL(N$))=0 AND H%(CA-1)MOD 13=12 THEN H=10+4*VAL(N$):V=2:L%(1,VAL(N$))=H%(CA-1):LL%(1,VAL(N$))=2:Q=H%(CA-1):GOSUB 960:GOTO 1200
1180 IF (L%(BOT,VAL(N$))MOD 13)-H%(CA-1)MOD 13<>1 OR INT(L%(BOT,VAL(N$))/26)=INT(H%(CA-1)/26) GOTO 1300
1190 H=10+4*VAL(N$):V=BOT+2:Q=H%(CA-1):LL%(BOT+1,VAL(N$))=2:L%(BOT+1,VAL(N$))=H%(CA-1):GOSUB 960 
1200 CA=CA-1:NA=NA-1:IF CA>0 THEN H=6:V=14:Q=H%(CA-1):GOSUB 960
1210 IF CA=0 THEN H=6:V=14:GOSUB 1280
1220 FOR N=CA TO NA-1
1230 H%(N)=H%(N+1)
1240 NEXT
1250 GOTO 460
1260 PAPER 3:PEN 2:LOCATE H,V:PRINT STRING$(3,CHR$(125)):FOR j=1 TO 3:LOCATE H,V+j:PRINT CHR$(207);CHR$(238);CHR$(207):NEXT:LOCATE H,V+4:PRINT STRING$(3,CHR$(207))
1270 RETURN
1280 PAPER 0:FOR P=0 TO 4:LOCATE H,V+P:PRINT"   ":NEXT
1290 RETURN
1300 PEN 1:PAPER 2:LOCATE 1,20:PRINT"ILLEGAL MOVE!":FOR y=1 TO 3:FOR x=1 TO 5:SOUND 1,1000/x,10,7:NEXT:NEXT
1310 GOTO 460
*/ });
